
Function: GET

Syntax:   (GET arg1 arg2 &optional arg3)

 arg1 (required, evaluated) - a symbol.
 arg2 (required, evaluated) - any LISP object, but generally a symbol.
 arg3 (optional, evaluated) - any LISP object.

Returns:

 The property value of the indicator specified by arg2 from the property
 list of arg1; arg3 (default NIL) if the indicator is not found.

Processing:

 The property list of the symbol specified by arg1 is searched for an
 indicator-value "pair" for which the indicator is EQ to arg2.  If the
 specified property is not present on the property list, arg3 (or NIL if
 arg3 was omitted) is returned.

 Although arg1 normally must be a symbol, ZIL will accept a first
 argument of a list, in which case the CDR of the list is treated like a
 property list.  This is the "disembodied property list" used in
 Maclisp.

 If arg1 is not a symbol or a list, a warning message is issued and arg3
 (or NIL) is returned.  This actually should be an error, but is
 accepted for compatibility with Maclisp.

Errors signaled:

 Property list errors (see member $PROP).

Notes:

 See member $PROP for information about property lists.

Examples:

 (SETPLIST 'A NIL)                   ==> A
 (GET 'A 'FOO)                       ==> NIL
 (SETPLIST 'A '(FOO BAR BAZ (FROB NICK ATE)))
                                     ==> A
 (GET 'A 'FOO)                       ==> BAR
 (GET 'A 'FOO "default")             ==> BAR
 (GET 'A 'BAR)                       ==> NIL
 (GET 'A 'BAR "default")             ==> "default"
 (GET 'A 'BAZ)                       ==> (FROB NICK ATE)

 (REMPROP 'A 'BAZ)                   ==> (FROB NICK ATE)
 (GET 'A 'BAZ)                       ==> NIL
 (GET 'A 'BAZ "default")             ==> "default"

;The comparison test is EQ, not EQUAL or EQUALP.

 (PUTPROP 'A 'X 1.0)                 ==> X
 (PLIST 'A)                          ==> (1.0 X FOO BAR)
 (GET 'A 1.0)                        ==> NIL

;Maclisp compatibility.

 (GET '(A B C D E) 'A)  ==> NIL
 (GET '(A B C D E) 'B)  ==> C
 (GET '(A B C D E) 'C)  ==> NIL
 (GET '(A B C D E) 'D)  ==> E
 (GET '(A B C D E) 'E)  ==> NIL

 (GET 1 2)              ==> NIL  ;  with a warning message
 (GET 1 2 3)            ==> 3    ;  with a warning message

