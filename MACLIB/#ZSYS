         MACRO
         #ZSYS
.**********************************************************************
.*                                                                    *
.* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.  *
.*                     All rights reserved.                           *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.*  This macro establishes the global system specifications for       *
.*  assembling ZIL code.  It interrogates the value of the SYSPARM    *
.*  parameter and determines the operating system for which code is   *
.*  being assembled.  Failure to provide a suitable SYSPARM value     *
.*  will result in the failure of the assembly.                       *
.*                                                                    *
.**********************************************************************
.*
         LCLC  &ZPARM
         GBLB  &ZSYSMAC
         GBLB  &ZILMVS,&ZIL370,&ZILXA,&ZILVF,&ZILCMS,&ZILTSO,&ZILVPS
         GBLC  &SYSSPLV              The MVS/XA "SPLEVEL" global
.*
         AIF   (&ZSYSMAC).MEND       Exit if #ZSYS already in assembly
&ZSYSMAC SETB  1
&SYSSPLV SETC  '1'                   Default SPLEVEL
&ZPARM   SETC  '&SYSPARM'
.*
         AIF   ('&ZPARM' EQ '').DEFAULT
         AIF   ('&ZPARM' EQ 'VECTOR').VF
         AIF   ('&ZPARM' EQ 'VF').VF
         AIF   ('&ZPARM' EQ '370').S370
         AIF   ('&ZPARM' EQ 'MVS').S370
         AIF   ('&ZPARM' EQ 'XA').XA
         AIF   ('&ZPARM' EQ 'VM').CMS
         AIF   ('&ZPARM' EQ 'VMXA').VMXA
         AIF   ('&ZPARM' EQ 'CMS').CMS
         AIF   ('&ZPARM' EQ 'VPS').VPS
         AGO   .BADPARM
.DEFAULT ANOP  ,                   No SYSPARM specified
         MNOTE 0,'No SYSPARM value specified, assuming MVS/XA with VF.'
         AGO   .VF
.VF      ANOP  ,                   MVS/XA with vector facility
&ZILMVS  SETB  1
&ZILTSO  SETB  1
&ZIL370  SETB  0
&ZILXA   SETB  1
&ZILVF   SETB  1
&ZILCMS  SETB  0
&ZILVPS  SETB  0
         AGO   .DONE
.S370    ANOP  ,                   MVS/370
&ZILMVS  SETB  1
&ZILTSO  SETB  1
&ZIL370  SETB  1
&ZILXA   SETB  0
&ZILVF   SETB  0
&ZILCMS  SETB  0
&ZILVPS  SETB  0
         AGO   .DONE
.XA      ANOP  ,                   MVS/XA, no vector facility
.*       MNOTE 0,'Warning, SYSPARM(XA) means no vector facility.'
&ZILMVS  SETB  1
&ZILTSO  SETB  1
&ZIL370  SETB  0
&ZILXA   SETB  1
&ZILVF   SETB  0
&ZILCMS  SETB  0
&ZILVPS  SETB  0
         AGO   .DONE
.VMXA    ANOP  ,                   VM/XA
&ZILMVS  SETB  0
&ZILTSO  SETB  0
&ZIL370  SETB  0
&ZILXA   SETB  1
&ZILVF   SETB  0
&ZILCMS  SETB  1
&ZILVPS  SETB  0
         AGO   .DONE
.CMS     ANOP  ,                   VM/CMS
&ZILMVS  SETB  0
&ZILTSO  SETB  0
&ZIL370  SETB  1
&ZILXA   SETB  0
&ZILVF   SETB  0
&ZILCMS  SETB  1
&ZILVPS  SETB  0
         AGO   .DONE
.VPS     ANOP  ,                   VPS
&ZILMVS  SETB  1                   MVS is simulated by VPS
&ZILTSO  SETB  0                   but TSO, generally, is not
&ZIL370  SETB  1
&ZILXA   SETB  0
.*ILVF   SETB  1                   VPS has the vector facility at BU
&ZILVF   SETB  0                   but we're not going to use it now
&ZILCMS  SETB  0
&ZILVPS  SETB  1
         AGO   .DONE
.BADPARM ANOP
         MNOTE 16,'Unknown SYSPARM value specified: &SYSPARM'
         MEXIT
.DONE    ANOP
         AIF   (NOT &ZILXA).NOSPLVL
&SYSSPLV SETC  '2'                 SPLEVEL SET=2
.NOSPLVL ANOP
.MEND    MEND
