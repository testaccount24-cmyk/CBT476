         MACRO
&NAME    #TPUT ,
.*
.**********************************************************************
.*                                                                    *
.* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.  *
.*                     All rights reserved.                           *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.* THIS MACRO GENERATES A CALL TO TPUT TO WRITE TERMINAL MESSAGES.    *
.*                                                                    *
.* THIS USES REGISTERS 0, 1, 14, AND 15, AND REQUIRES 13 TO POINT TO  *
.* THE COMMON AREA.                                                   *
.*                                                                    *
.* FROM 1 TO 4 OPERANDS MAY BE CODED:                                 *
.*                                                                    *
.*   (1) THE SEVERITY LEVEL OF THE MESSAGE (0 THROUGH 4) - OPTIONAL   *
.*   (2) THE ADDRESS OF THE MESSAGE TEXT - REQUIRED                   *
.*   (3) THE LENGTH OF THE MESSAGE TEXT - OPTIONAL                    *
.*   (4) "ASIS" - OPTIONAL                                            *
.*                                                                    *
.*  THE ADDRESS MAY BE CODED AS (1), MEANING IT IS ALREADY IN REG 1   *
.*  (IN WHICH CASE THE LENGTH MUST BE CODED AS WELL).                 *
.*  THE LENGTH MAY BE CODED AS (0), MEANING IT IS ALREADY IN REG 0.   *
.*                                                                    *
.*  IF THE SEVERITY LEVEL IS OMITTED, THE MESSAGE IS ALWAYS SENT.     *
.*  IF THE LENGTH IS OMITTED, IT IS DERIVED FROM THE MESSAGE TEXT,    *
.*  WHICH MUST BE A CHARACTER DC IN THAT CASE.                        *
.*                                                                    *
.*  SEVERITY LEVELS ARE SENSITIVE TO THE ZIL (MSGLEVEL) SETTING:      *
.*                                                                    *
.*    0 -      INFORMATIONAL: SUPPRESSED BY MSGLEVEL 1, 2 OR 3        *
.*    1 -      WARNING:       SUPPRESSED BY MSGLEVEL 2 OR 3           *
.*    2 -      ERROR:         SUPPRESSED BY MSGLEVEL 3                *
.*    3 -      SEVERE:        NOT SUPPRESSED                          *
.*    4 -      DISASTROUS:    NOT SUPPRESSED                          *
.*    * -      UNCONDITIONAL: NOT SUPPRESSED                          *
.*                                                                    *
.* EXAMPLES:                                                          *
.*                                                                    *
.*       #TPUT MSG1                                                   *
.*       #TPUT 2,MSG1                                                 *
.*       #TPUT 2,MSG1,10                                              *
.*       #TPUT 2,MSG1,10,ASIS                                         *
.* MSG1  DC    C'A MESSAGE.'                                          *
.*                                                                    *
.**********************************************************************
.*
         LCLC  &SEV,&MSG,&LEN,&LBL,&QUO,&ASIS
&LBL     SETC  'ZLCT&SYSNDX'
&QUO     SETC  ''''
         AIF   (N'&SYSLIST EQ 1).SET1
         AIF   (N'&SYSLIST EQ 2).SET2
         AIF   (N'&SYSLIST EQ 3).SET3
         AIF   (N'&SYSLIST EQ 4).SET4
         MNOTE 12,'TOO MANY ARGUMENTS ON #TPUT MACRO'
         MEXIT
.SET1    ANOP
&SEV     SETC  ''
&MSG     SETC  '&SYSLIST(1)'
&LEN     SETC  'L&QUO&MSG'
         AGO   .SET
.SET2    ANOP
&SEV     SETC  '&SYSLIST(1)'
&MSG     SETC  '&SYSLIST(2)'
&LEN     SETC  'L&QUO&MSG'
         AGO   .SET
.SET3    ANOP
&SEV     SETC  '&SYSLIST(1)'
&MSG     SETC  '&SYSLIST(2)'
&LEN     SETC  '&SYSLIST(3)'
         AGO   .SET
.SET4    ANOP
&SEV     SETC  '&SYSLIST(1)'
&MSG     SETC  '&SYSLIST(2)'
&LEN     SETC  '&SYSLIST(3)'
&ASIS    SETC  '&SYSLIST(4)'
.SET     ANOP
         AIF   ('&SEV' EQ '0' OR '&SEV' EQ '1' OR '&SEV' EQ '2' OR '&SEX
               V' EQ '3' OR '&SEV' EQ '4' OR '&SEV' EQ '*' OR '&SEV' EQX
                '').SEVOK
         MNOTE 12,'INVALID #TPUT FIRST OPERAND - &SEV'
         MEXIT
.SEVOK   ANOP
&NAME    DS    0H
         AIF   ('&SEV' EQ '' OR '&SEV' EQ '*').NOSEV
         CLI   ZLCMSLVL,&SEV          IF MSGLEVEL EXCEEDS SEVERITY CODE
         BH    &LBL                   THEN DON'T DISPLAY MESSAGE
.NOSEV   ANOP
         AIF   ('&MSG' EQ '(1)').NOMSG
         LA    1,&MSG                 GET ADDRESS OF MESSAGE
.NOMSG   ANOP
         AIF   ('&LEN' EQ '(0)').NOLEN
         LA    0,&LEN                 GET LENGTH OF MESSAGE
.NOLEN   ANOP
         AIF   ('&ASIS' EQ 'ASIS').ASIS
         AIF   ('&ASIS' EQ '').NOASIS
         MNOTE 12,'INVALID OPERAND 4 - "ASIS" IS THE ONLY VALID VALUE'
         MEXIT
.ASIS    ANOP
         LCR   0,0                    INDICATE ASIS SPECIFIED
.NOASIS  ANOP
         L     15,ZLCTPUT             GET ADDRESS OF TPUT PROCESSOR
         #BASR 14,15                  CALL ZILTPUT
&LBL     DS    0H
         MEND
