         MACRO
&SYM     #MKAT &TYPE
.**********************************************************************
.*                                                                    *
.* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.  *
.*                     All rights reserved.                           *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.* THIS MACRO BUILDS AN ATOM FROM A NUMERIC VALUE CONTAINED IN        *
.* REGISTER 2.  REGISTERS 1, 2, 14, AND 15 ARE USED WHEN CONS IS      *
.* INVOKED TO BUILD THE ATOM.  THE RESULTING ATOM IS RETURNED         *
.* IN REGISTER 15.                                                    *
.*                                                                    *
.* ON ENTRY, REG 2 MUST CONTAIN THE VALUE; REG 0 MUST CONTAIN THE     *
.* CURRENT TOP-OF-STACK ADDRESS.                                      *
.*                                                                    *
.* VALID CALLS:                                                       *
.*                                                                    *
.*       #MKAT  FIXED       REG 2 CONTAINS FIXNUM VALUE               *
.*                                                                    *
.*       #MKAT  FLOAT       REG 2 CONTAINS FLONUM VALUE               *
.*                                                                    *
.*       #MKAT  ALPHA       REG 2 CONTAINS POINTER TO PNAME STRING    *
.*                          (HIGH-ORDER BYTE MUST BE CLEAN).          *
.*                          NOTE: THIS CREATES AN UNINTERNED SYMBOL!  *
.*                                                                    *
.**********************************************************************
         AIF   ('&TYPE' EQ 'ALPHA').MKALF
         AIF   ('&TYPE' EQ 'FIXED').MKFIX
         AIF   ('&TYPE' EQ 'FLOAT').MKFLO
         MNOTE 12,'#MKAT TYPE MUST BE FIXED, FLOAT OR ALPHA'
         MEXIT
.*
.* OLD FIXNUM CODE, BEFORE FIXNUM TABLE WAS CREATED
.*
.*KFIX  ANOP
.*YM     LA    #1,##FIXNUM            SET ATOM TYPE
.*       L     #15,ZLCCONS            GET ADDRESS OF CONS ROUTINE
.*       SLL   #1,24                  SHIFT TYPE TO HIGH ORDER BYTE
.*       AGO   .CONS
.*
.* ACTIVATED BECAUSE THE FIXNUM TABLE EXISTS.
.* THIS ASSUMES THAT (1) THERE IS A TABLE OF CONSTANT FIXNUMS
.* SITUATED PAST THE ATOM T, AND THEREFORE ADDRESSABLE BY #8, AND
.* (2) THE ATOM T IS EITHER 8, 16, OR 24 BYTES LONG.
.*
.MKFIX  ANOP
&SYM     L     #15,ZLCCONS            GET ADDRESS OF CONS ROUTINE
         L     #1,#SFT                LOAD FIXNUM TYPE FROM FIXTBL
         AGO   .CONS
.MKFLO   ANOP
&SYM     LA    #1,##FLONUM            SET ATOM TYPE
         L     #15,ZLCCONS            GET ADDRESS OF CONS ROUTINE
         SLL   #1,24                  SHIFT TYPE TO HIGH ORDER BYTE
         AGO   .CONS
.MKALF   ANOP
&SYM     LA    #1,##SYMBOL            SET ATOM TYPE
         L     #15,ZLCCONS            GET ADDRESS OF CONS ROUTINE
         SLL   #1,24                  SHIFT TYPE TO HIGH ORDER BYTE
         OR    #1,#2                  SET PRINT NAME POINTER
         LR    #2,#10                 SET PROPERTY LIST TO NIL
.CONS    ANOP
.******* L     #15,ZLCCONS            (INTERLEAVED FOR PIPELINING)
         #BASR #14,#15                CALL CONS TO BUILD ATOM
         MEND
