         MACRO
&NAME    #AATM &PNAME
.**********************************************************************
.*                                                                    *
.* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.  *
.*                     All rights reserved.                           *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.* THIS MACRO GENERATES AN IN-LINE SYMBOL.                            *
.*                                                                    *
.* EXAMPLE:  #AATM 'FOO'                                              *
.*                                                                    *
.* NOTE: USE OF THIS MACRO REQUIRES USE OF ZILSINT.                   *
.*       SEE "OPEN" FOR AN EXAMPLE OF HOW TO CODE THIS.               *
.*                                                                    *
.**********************************************************************
         LCLA  &I,&L,&N,&Z
         LCLC  &KNAME
&KNAME   SETC  '&PNAME'            . PRINT NAME INCLUDING PASSED QUOTES
&N       SETA  K'&KNAME-2          . LENGTH OF CHARACTERS INSIDE QUOTES
&KNAME   SETC  '&KNAME'(2,&N)      . PRINT NAME WITH QUOTES REMOVED
&I       SETA  1                   . INDEX THROUGH PRINT NAME
&L       SETA  0                   . COMPUTED LENGTH OF PRINT NAME
.LOOP    ANOP
         AIF   (&I GT &N).LEND
         AIF   (&I EQ &N).NOTQA
         AIF   ('&KNAME'(&I,2) EQ '''''').P2
         AIF   ('&KNAME'(&I,2) EQ '&&').P2
.NOTQA   ANOP
&L       SETA  &L+1
&I       SETA  &I+1
         AGO   .LOOP
.P2      ANOP
&L       SETA  &L+1
&I       SETA  &I+2
         AGO   .LOOP
.LEND    ANOP
&Z       SETA  4-(&L-((&L/4)*4))
&NAME    DS    0A                  ALIGN SYMBOL TO FULLWORD
         DC    YL1(##SYMBOL)       SYMBOL TYPE INDICATOR
         DC    XL7'00'             RESERVED AREA FOR ZILSINT USE
         DC    YL1(##STRING)       PRINT NAME TYPE INDICATOR
         DC    AL3(&L)             PRINT NAME LENGTH
         AIF   (&L EQ 0).NULL
         DC    C'&KNAME'
         AIF   (&Z EQ 4).NOFILL
         DC    XL&Z'00'            FILL OUT TO FULLWORD BOUNDARY
.NOFILL  ANOP
         AGO   .MEND
.NULL    ANOP
         DC    XL4'00'             NULL PRINT NAME
.MEND    ANOP
         MEND
