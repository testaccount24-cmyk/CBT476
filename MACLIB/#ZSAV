         MACRO
&SYM     #ZSAV &ARGS=
.**********************************************************************
.*                                                                    *
.* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.  *
.*                     All rights reserved.                           *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.* THIS MACRO SAVES REGISTERS ON THE STACK.  SEE #ZRET.               *
.* THE NUMBER OF ARGUMENTS MUST BE SPECIFIED CORRECTLY.               *
.*                                                                    *
.* REGISTER 12 IS MADE TO BE THE BASE REGISTER, AND REGISTER 11 IS    *
.* MADE THE NEW CURRENT STACK POINTER; ARGUMENTS MAY BE LOADED FROM   *
.* 0, 4, ETC. OFF REGISTER 11.                                        *
.*                                                                    *
.* NON-RECURSIVE FUNCTIONS MAY NOT NEED TO USE THIS (SEE CONS).       *
.* IN THAT CASE REGISTER 15 IS THE BASE REGISTER AND THE CURRENT      *
.* STACK POINTER IS REGISTER 1.                                       *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.* 04/13/2000 Changed to support HLASM.                               *
.*                                                                    *
.**********************************************************************
         GBLA  &ZSVOF1,&ZSVOF2,&ZSVARGS
         LCLA  &ZARGS
         AIF   ('&ARGS' EQ '').NOARGS
&ZARGS   SETA  &ARGS
         AIF   (&ZSVARGS EQ &ZARGS).ARGSOK
         MNOTE 4,'#ZSAV ARGS VALUE DOES NOT AGREE WITH #ZBEG ARG SPEC'
         AGO   .ARGSOK
.NOARGS  ANOP
&ZARGS   SETA  &ZSVARGS
.ARGSOK  ANOP
&ZSVOF1  SETA  4*&ZARGS
&ZSVOF2  SETA  &ZSVOF1+8
&SYM     ST    14,&ZSVOF2.(,1)     SAVE RETURN REGISTER
         STM   11,12,&ZSVOF1.(1)   SAVE BASE AND STACK REGISTERS
         LR    12,15               SET UP BASE REGISTER
         DROP  15
         USING &SYSECT,12
         LR    11,1                SET UP STACK POINTER
         MEND
