         MACRO
&NAME    #SATM &TEXT
.**********************************************************************
.*                                                                    *
.* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.  *
.*                     All rights reserved.                           *
.*                                                                    *
.**********************************************************************
.*                                                                    *
.* THIS MACRO GENERATES AN IN-LINE STRING.                            *
.*                                                                    *
.* EXAMPLE:  #SATM 'DON''T TREAD ON ME!'                              *
.*                                                                    *
.* NOTE: STRINGS ARE NOT AUTOMATICALLY ALIGNED BY THIS MACRO.         *
.*                                                                    *
.**********************************************************************
         LCLA  &I,&L,&N,&Z
         LCLC  &KTEXT
&KTEXT   SETC  '&TEXT'             . STRING TEXT INCLUDING QUOTES
&N       SETA  K'&KTEXT-2          . LENGTH OF CHARACTERS INSIDE QUOTES
&KTEXT   SETC  '&KTEXT'(2,&N)      . STRING TEXT WITH QUOTES REMOVED
&I       SETA  1                   . INDEX THROUGH STRING TEXT
&L       SETA  0                   . COMPUTED LENGTH OF STRING TEXT
.LOOP    ANOP
         AIF   (&I GT &N).LEND
         AIF   (&I EQ &N).NOTQA
         AIF   ('&KTEXT'(&I,2) EQ '''''').P2
         AIF   ('&KTEXT'(&I,2) EQ '&&').P2
.NOTQA   ANOP
&L       SETA  &L+1
&I       SETA  &I+1
         AGO   .LOOP
.P2      ANOP
&L       SETA  &L+1
&I       SETA  &I+2
         AGO   .LOOP
.LEND    ANOP
&Z       SETA  4-(&L-((&L/4)*4))
&NAME    DC    YL1(##STRING)       STRING TEXT TYPE INDICATOR
         DC    AL3(&L)             STRING TEXT LENGTH
         AIF   (&L EQ 0).NULL
         DC    C'&KTEXT'
         AIF   (&Z EQ 4).NOFILL
         DC    XL&Z'00'            FILL OUT TO FULLWORD BOUNDARY
.NOFILL  ANOP
         AGO   .MEND
.NULL    ANOP
         DC    XL4'00'             NULL STRING TEXT
.MEND    ANOP
         MEND
