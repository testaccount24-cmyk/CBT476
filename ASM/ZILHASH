         TITLE 'ZILHASH - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS IS THE PRIMITIVE ATOM-HASHER USED BY SXHASH.                   *
*                                                                     *
***********************************************************************
*                                                                     *
* Note: This calls the hashing algorithm used by ZILINTN to           *
*       hash symbol names and strings.  The hash code returned by     *
*       this interface is a raw hash code, unadjusted for the size    *
*       of any hash table.                                            *
*                                                                     *
***********************************************************************
         SPACE
ZILHASH  #ZBEG MINARGS=1,MAXARGS=1,NAME='ZILHASH'
         #ZPDS 16
         #ZSAV ARGS=1
         SPACE 1
*
* CHECK TYPE OF ARG 1
*
         L     #6,0(,#1)           LOAD ARG
         CLI   ##TYPE(#6),##SYMBOL
         BE    ARG1SYM
         CLI   ##TYPE(#6),##STRING
         BE    ARG1STR
         CLI   ##TYPE(#6),##FIXNUM
         BE    ARG1FIX
         CLI   ##TYPE(#6),##SFLOAT
         BE    ARG1SFL
         CLI   ##TYPE(#6),##BIGNUM
         BE    ARG1BIG
         CLI   ##TYPE(#6),##DFLOAT
         BE    ARG1DFL
         CLI   ##TYPE(#6),##SUBR
         BE    ARG1SUB
         CLI   ##TYPE(#6),##CLOSUR
         BE    ARG1CLO
         B     ERROR               ALL OTHER TYPES ARE INVALID.
         SPACE 1
ARG1SYM  DS    0H                  ARG 1 IS SYMBOL
         SPACE 1
         L     #6,##PNAME(,#6)     GET POINTER TO PRINT NAME
******** B     ARG1STR             DO LIKE STRING
         SPACE 1
ARG1STR  DS    0H                  ARG 1 IS STRING
         SPACE 1
         XR    #5,#5               Clear length register
         LA    #4,##VECDAT(,#6)    Set address of string text
         ICM   #5,7,1(#6)          Set length of string text
         L     #7,#SFWM1           Set #7 to a negative value to
*                                   tell ZILINTN to return hash code
         L     #15,ZLCINTN         Get address of ZILINTN
         #BASR #14,#15             Call ZILINTN to get hash code
         LR    #2,#15              Get hash code into register
         B     RETURN
         SPACE 1
ARG1FIX  DS    0H                  ARG 1 IS FIXNUM
         SPACE 1
         L     #2,##CDR(,#6)       GET FIXNUM OR FLONUM VALUE
         B     RETURN
         SPACE 1
ARG1SFL  EQU   ARG1FIX             ARG 1 IS SINGLE FLOAT
         SPACE 1
ARG1DFL  DS    0H                  ARG 1 IS DOUBLE FLOAT
         SPACE 1
         L     #2,8(,#6)           (XOR WORD1 WORD2)
         X     #2,12(,#6)
         B     RETURN
         SPACE 1
ARG1BIG  EQU   ARG1STR             ARG 1 IS BIGNUM
         SPACE 1
*                                  DO LIKE STRING
         SPACE 1
ARG1CLO  EQU   ARG1SUB             ARG 1 IS CLOSURE
         SPACE 1
ARG1SUB  DS    0H                  ARG 1 IS SUBR
         SPACE 1
         L     #2,CODEHASH         WHAT ELSE CAN I DO?
******** B     RETURN
         SPACE 1
RETURN   DS    0H                  HAS NUMBER IS IN #2
         SPACE 1
         N     #2,=X'7FFFFFFF'     FORCE NON-NEGATIVE
         #MKAT FIXED               MAKE A FIXNUM OUT OF IT
         #ZRET ,
         EJECT
         DS    0F
CODEHASH DC    XL4'7FFFFFFF'       SOMETHING UNLIKELY FOR COMPILED CODE
         SPACE 1
ERROR    DS    0H
         #ERR  'Invalid type of object passed to ZILHASH - ',(#6)
         SPACE 1
         LTORG
         END
