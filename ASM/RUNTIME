         TITLE 'RUNTIME - ZIL 1.3 INTERNAL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*
* RUNTIME RETURNS THE NUMBER OF THOUSANDTHS OF A SECOND OF CPU TIME
* USED DURING THE CURRENT SESSION.  THE VALUE IS TAKEN FROM THE
* ASCBEJST FIELD OF THE ASCB.
*
* FORMERLY CALLED "CPUTIME", THIS FUNCTION WAS RENAMED BECAUSE MOST
* OTHER LISPS HAVE A "RUNTIME" FUNCTION.
*
RUNTIME  #ZBEG MINARGS=0,MAXARGS=0,NAME='RUNTIME'
         #ZPDS 12                  DEFINE STACK SIZE
         #ZSAV ARGS=0              SAVE REGISTERS
         SPACE
         STIMER TASK,BINTVL=DUMS   FORCE OS TO UPDATE TIMER HERE
         L     #2,X'224'           GET ADDRESS OF ASCB
         LM    #2,#3,X'40'(#2)     GET JOB STEP ELAPSED TIME (ASCBEJST)
         SRDL  #2,12               CONVERT FROM MICROSECONDS
         D     #2,=F'1000'         TO THOUSANDTHS OF A SECOND
         LR    #2,#3               GET RESULT INTO REGISTER
         LA    #0,12(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM
         SPACE
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE
         DS    0D
DUMS     DC    XL8'00'
         SPACE
         LTORG
         SPACE
         END
