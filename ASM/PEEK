         TITLE 'PEEK - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
*  PEEK TAKES 1 ARGUMENT, A FIXNUM, AND USES IT AS A POINTER INTO     *
*       VIRTUAL STORAGE.  IT RETURNS A FIXNUM WHICH CONTAINS THE      *
*       CONTENTS OF THE FULLWORD OF MEMORY AT THE SPECIFIED LOCATION. *
*                                                                     *
***********************************************************************
*                                                                     *
* NOTE: THIS MODULE USES 31-BIT ADDRESSING AND REQUIRES H ASSEMBLER.  *
*                                                                     *
***********************************************************************
         GBLB  &ZILXA
         #ZSYS
         SPACE 1
PEEK     #ZBEG MINARGS=1,MAXARGS=1,NAME='PEEK'
         #ZPDS 16
         #ZSAV ,
         SPACE 1
         L     #1,0(,#11)          GET ARGUMENT 1
         CLI   ##TYPE(#1),##FIXNUM MUST BE A FIXNUM
         BNE   ERROR1              ELSE ERROR
         L     #2,##CDR(,#1)       GET FIXNUM VALUE
         SPACE 1
         AIF   (NOT &ZILXA).SKIP1
*---------------------------------SWITCH FROM CURRENT AMODE TO AMODE 31
         L     #1,=A(CODE31+X'80000000') ADDRESS OF THIS CODE
         LA    #3,CODE31E          RETURN ADDRESS
         BSM   #3,#1
CODE31   DS    0H
*---------------------------------THE AMODE IS NOW 24
.SKIP1   ANOP
*
* WARNING: PROGRAM CHECK MAY OCCUR HERE...
*
         L     #2,0(,#2)           GET DATA AT POINTER LOCATION
*
* END OF WARNING
*
         AIF   (NOT &ZILXA).SKIP2
*---------------------------------SWITCH FROM AMODE 31 TO CURRENT AMODE
         BSM   0,#3
CODE31E  DS    0H
*---------------------------------THE AMODE IS NOW SAME AS ON ENTRY
.SKIP2   ANOP
         SPACE 1
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   DS    0H
         #ERR  'Argument to Peek not a fixnum - ',0(#11)
         SPACE 1
         LTORG
         END
