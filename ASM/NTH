         TITLE 'NTH - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* NTH TAKES TWO ARGUMENTS - A NUMBER AND A LIST.                      *
*                                                                     *
* THIS FUNCTION IS NON-RECURSIVE AND TAKES NO STACK SPACE.            *
*                                                                     *
***********************************************************************
         SPACE 1
NTH      #ZBEG MINARGS=2,MAXARGS=2,NAME='NTH'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING NTH,#3
         SPACE 1
         LM    #15,#0,0(#1)        LOAD THE ARGUMENTS
         LA    #2,1                PREPARE A 1
         CLI   ##TYPE(#15),##FIXNUM IF ARG 1 NOT A FIXNUM
         BNE   ERROR1              THEN ERROR
         A     #2,##CDR(,#15)      GET FIXNUM VALUE + 1
         BNP   ERROR1              IF VALUE WAS NEGATIVE, ERROR
LOOP     DS    0H                  ELSE...
         LR    #15,#0              LOAD NEXT ARG2
         CR    #15,#10             IF ARG 2 IS NULL
         BER   #14                 THEN RETURN NIL
         LM    #15,#0,##CAR(#15)   LOAD NEXT CAR AND CDR
         LTR   #15,#15             IF WE LOADED FROM AN ATOM
         BM    ERROR2              THEN AN IMPROPER LIST WAS PASSED
         BCT   #2,LOOP             DECR COUNT. IF NONZERO, CONTINUE
         BR    #14                 ELSE RETURN THIS CAR
         SPACE 1
ERROR1   DS    0H
         L     #2,0(,#1)
         #ERR  'Argument 1 to NTH not a nonnegative fixnum - ',(#2)
         SPACE 1
ERROR2   DS    0H
         L     #2,4(,#1)
         #ERR  'Argument 2 to NTH not a proper list - ',(#2)
         SPACE 1
         LTORG
         END
