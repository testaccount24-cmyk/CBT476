         TITLE 'VSET - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
         SPACE 1
VSET     #ZBEG MINARGS=3,MAXARGS=3,NAME='VSET'
         SPACE 1
         USING VSET,#15
         LM    #2,#4,0(#1)         LOAD ARGUMENTS
         TM    ##TYPE(#2),##VEC    IF ARG1 IS NOT A VECTOR
         BNO   ERROR1              THEN ERROR.  ELSE...
         CLI   ##TYPE(#3),##FIXNUM IF ARG2 IS NOT A FIXNUM
         BNE   ERROR2              THEN ERROR.  ELSE...
         L     #5,##CDR(,#3)       GET VALUE OF FIXNUM INTO REGISTER
         XR    #0,#0               CLEAR INSERT REGISTER
         SLA   #5,2                MULTIPLY BY 4 TO CONVERT TO OFFSET
         BM    ERROR3              IF LESS THAN ZERO, OUT OF RANGE
         ICM   #0,7,1(#2)          GET LENGTH OF VECTOR TEXT
         CR    #5,#0               IF NOT LESS THAN TOTAL VECTOR LENGTH
         BNL   ERROR3              THEN OUT OF RANGE
         ST    #4,##VECDAT(#5,#2)  ELSE STORE ARG 3 INTO ELEMENT N
         LR    #15,#4               OF VECTOR AND RETURN IT AS VALUE
         BR    #14                 AND RETURN TO CALLER
         SPACE 1
         DROP  #15
         SPACE 1
         USING VSET,#7
         SPACE 1
ERROR1   DS    0H
         LR    #7,#15
         #ERR  'Argument 1 to VSET not a vector - ',(#2)
ERROR2   DS    0H
         LR    #7,#15
         #ERR  'Argument 2 to VSET not a fixnum - ',(#3)
ERROR3   DS    0H
         LR    #7,#15
         #ERR  'VSET index ',(#3),' out of range for vector ',(#2)
         SPACE 1
         LTORG
         SPACE 1
         END
