         TITLE 'RESTARG - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS MODULE RETURNS T OR NIL, DEPENDING ON WHETHER THE COMPILED     *
* FUNCTION TAKES AN &REST ARGUMENT.                                   *
* THE INPUT PARAMETER MUST BE CODE, I.E. THE VALUE OF THE ZILLOAD     *
* FUNCTION OR OF THE SUBR, FSUBR, ETC. PROPERTY OF A FUNCTION ATOM.   *
*                                                                     *
***********************************************************************
         SPACE
RESTARG  #ZBEG MINARGS=1,MAXARGS=1,NAME='RESTARG'
         SPACE
         L     #5,0(,#1)           PICK UP PARAMETER (FUNCTION CODE)
         TM    ##TYPE(#5),##CODE   MUST BE A COMPILED CODE OBJECT
         BNO   ERROR               (SUBR OR CLOSURE)
         XR    #4,#4               ELSE CLEAR INSERT REGISTER
         ICM   #4,7,##CAR+1(#5)    GET POINTER TO COMPILED CODE
         XR    #2,#2               CLEAR INSERT REG
         IC    #2,4(,#4)           GET LENGTH OF IDENTIFIER
         LA    #3,5+3(#2,#4)       POINT TO MODULE CODE PAST IDENTIFIER
         N     #3,=X'FFFFFFFC'     ROUNDING UP TO NEXT FULLWORD
         USING ZINT,#3             TO POINT TO "ZINT" AREA
         TM    ZINFLAG2,ZINGREST   IF &REST ARG IS EXPECTED
         BO    TRUE                THEN RETURN T, ELSE RETURN NIL
FALSE    LR    #15,#10
         BR    #14
TRUE     DS    0H
         LR    #15,#8
         BR    #14
         SPACE 1
         DROP  #15
         SPACE 1
ERROR    DS    0H
         LR    #2,#15
         USING RESTARG,#2
         #ERR  'Argument to RESTARG not a compiled code object (SUBR orX
                closure) - ',(#5)
         SPACE 1
         LTORG
         SPACE
         #ZINT ,                   MAP THE CODE HEADER AREA
         END
