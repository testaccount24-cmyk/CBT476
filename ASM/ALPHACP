         TITLE 'ALPHACP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ALIAS "ALPHA-CHAR-P".                                               *
*                                                                     *
***********************************************************************
         SPACE
ALPHACP  #ZBEG MINARGS=1,MAXARGS=1,NAME='ALPHA-CHAR-P'
         SPACE
         L     #3,0(,#1)           GET ATOM (ARG 1 TO ALPHACP)
         CLI   ##TYPE(#3),##FIXNUM MUST BE A FIXNUM
         BNE   ERROR1              OR ELSE
*
* GET THE NUMERIC VALUE OF THE FIXNUM.
*
         L     #1,##CDR(,#3)       GET FIXNUM CONTENTS
         CL    #1,=X'000000FF'     MUST BE 1-BYTE CHARACTER FIXNUM
         BH    FALSE               ELSE NOT ALPHABETIC CHARACTE R
         XR    #2,#2
         IC    #2,TABLE(#1)        IF IN RANGE, LOAD CHAR FROM TABLE
         LTR   #2,#2               IF TABLE ENTRY SAYS ALPHABETIC
         BZ    FALSE               THEN...
TRUE     LR    #15,#8               RETURN T
         BR    #14                 ELSE
FALSE    LR    #15,#10              RETURN NIL
         BR    #14
         SPACE 1
TABLE    DC    256YL1(0)
         ORG   TABLE+C'A'-X'40'
         DC    X'010101010101010101'
         ORG   TABLE+C'J'-X'40'
         DC    X'010101010101010101'
         ORG   TABLE+C'S'-X'40'
         DC    X'0101010101010101'
         ORG   TABLE+C'A'
         DC    X'010101010101010101'
         ORG   TABLE+C'J'
         DC    X'010101010101010101'
         ORG   TABLE+C'S'
         DC    X'0101010101010101'
         ORG
         SPACE 1
ERROR1   DS    0H
         LR    #2,#15
         DROP  #15
         USING ALPHACP,#2
         #ERR  'Argument to ALPHA-CHAR-P not a fixnum - ',(#3)
         SPACE 1
         LTORG
         END
