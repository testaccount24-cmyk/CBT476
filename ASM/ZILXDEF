         TITLE 'ZILXDEF - ZIL 1.3 COMPILED FUNCTION DEFINER'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
*  THIS MODULE IS INVOKED FROM COMPILED CODE TO IMPLEMENT THE         *
*  COMPILER-GENERATED FORM FOR "DEFUN".                               *
*                                                                     *
* ON ENTRY:  REGISTER 0 = CURRENT TOP-OF-STACK POINTER                *
*            REGISTER 1 --> THE FUNCTION NAME SYMBOL                  *
*            REGISTER 2 --> THE COMPILED CODE ITSELF                  *
*                                                                     *
* ON RETURN: REGISTER 15 IS THE RETURN VALUE FROM ZEVDSUB             *
*                                                                     *
*  LOGIC:                                                             *
*                                                                     *
*  THIS ROUTINE CALLS ZEVDSUB TO ASSIGN THE COMPILED CODE PROPERTY TO *
*  THE NAMED FUNCTION.  NOTE THAT DYNAMIC BINDING IS EFFECTED.  NO    *
*  SUPPORT FOR LEXICAL CLOSURES DEFUNNING IS AVAILABLE HERE.          *
*                                                                     *
***********************************************************************
         SPACE 1
ZILXDEF  #ZBEG NAME='ZILXDEF',SUBRS=ZILSUBR
         SPACE 1
         LR    #3,#0               GET CURRENT TOP-OF-STACK POINTER
         LA    #0,32(,#3)          CHECK ROOM ON STACK
         #ZPDS (0)
         STM   #5,#7,0(#3)
         STM   #11,12,12(#3)
         ST    #14,20(,#3)
         LR    #12,#15
         DROP  #15
         USING ZILXDEF,#12
         LR    #11,#3
         SPACE 1
         ST    #1,24(,#11)         SAVE SYMBOL ARG
         LR    #1,#2               GET COMPILED CODE
         XR    #2,#2               SET ALIST ARG = NIL
         L     #15,ZLCCONS         GET ADDRESS OF ZILCONS
         OI    ZLCGCFLG,ZLCGCBLD   TELL CONS WE'RE IN CHARGE OF #0
         #BASR #14,#15             CALL CONS TO MAKE A SUBR
         NI    ZLCGCFLG,255-ZLCGCBLD TURN OFF #0-HANDLING FLAG
         MVI   ##TYPE(#15),##SUBR  SET TYPE BITS
         ST    #15,28(,#11)        SET ARG 2 TO ZEVDSUB
         L     #15,@ZEVDSUB        GET ADDRESS OF ZEVDSUB
         LA    #1,24(,#11)         POINT TO ARGS ON STACK
         #BASR #14,#15             CALL ZEVDSUB FOR RUN-TIME DEFUN
         SPACE 1
         LR    #3,#11
         LM    #5,#7,0(#3)
         LM    #11,12,12(#3)
         L     #14,20(,#3)
         BR    #14
         SPACE 1
         #ZRET ,
         SPACE 2
ZILSUBRS DS    0A
@ZEVDSUB DC    V(ZEVDSUB)          ADDRESS OF SUBROUTINE
         DC    CL8'ZEVDSUB'
ZILSUBRE EQU   *
         SPACE 2
         LTORG
         END
