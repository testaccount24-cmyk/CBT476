         TITLE 'GCMSG - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* GCMSG TAKES ONE OPTIONAL ARGUMENT - NIL OR NON-NIL.                 *
*                                                                     *
* IF THE ARG IS OMITTED, T OR NIL IS RETURNED, TELLING WHETHER OR     *
* NOT GC MESSAGES ARE CURRENTLY ENABLED.                              *
* IF THE ARG IS NIL, GC MESSAGES ARE SUPPRESSED;                      *
* OTHERWISE, GC MESSAGES ARE ENABLED.                                 *
*                                                                     *
***********************************************************************
         SPACE
GCMSG    #ZBEG MINARGS=0,MAXARGS=1,NAME='GCMSG'
         SPACE
         LR    #3,#15
         DROP  #15
         USING GCMSG,#3
         L     #15,0(,#1)          PICK UP ARG 1
         CR    #15,#13             IF ARG IS OMITTED
         BE    GCMSGQ              THEN QUERY CURRENT GCMSG SETTING
         CR    #15,#10             IF ARG IS NIL
         BNE   GCMSGT              THEN...
         NI    ZLCGCFLG,255-ZLCGCMSG  SET GC MESSAGES OFF
         BR    #14                  RETURN FALSE
GCMSGT   DS    0H                  ELSE...
         OI    ZLCGCFLG,ZLCGCMSG    SET GC MESSAGES ON
         LR    #15,#8               RETURN TRUE
         BR    #14
         SPACE 1
GCMSGQ   DS    0H
         TM    ZLCGCFLG,ZLCGCMSG   IF GC MESSAGES ARE ENABLED
         BO    TRUE                THEN RETURN T (TRUE)
         LR    #15,#10             ELSE RETURN NIL (FALSE)
         BR    #14
TRUE     LR    #15,#8
         BR    #14
         END
