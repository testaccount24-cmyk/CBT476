         TITLE 'SETASCII - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* SETASCII takes one optional argument - NIL or non-NIL.              *
*                                                                     *
* If the arg is omitted, the current ASCII mode setting is returned.  *
* If the arg is NIL, ASCII mode is disabled;                          *
* otherwise, ASCII mode is enabled.                                   *
*                                                                     *
***********************************************************************
         SPACE
SETASCII #ZBEG MINARGS=0,MAXARGS=1,NAME='SETASCII'
         SPACE
         LR    #3,#15
         DROP  #15
         USING SETASCII,#3
         L     #15,0(,#1)          PICK UP ARG 1
         CR    #15,#13             IF ARG IS OMITTED
         BE    ASCIIQ              THEN QUERY CURRENT ASCII SETTING
         CR    #15,#10             IF ARG IS NIL
         BNE   ASCIIT              THEN...
         NI    ZLCLDFLG,255-ZLCLDASC SET ASCII MODE OFF
         BR    #14
ASCIIT   DS    0H                  ELSE...
         OI    ZLCLDFLG,ZLCLDASC    SET ASCII MODE ON
         BR    #14
         SPACE 1
ASCIIQ   DS    0H
         TM    ZLCLDFLG,ZLCLDASC   IF ASCII MODE IS ON
         BO    TRUE                THEN RETURN T (TRUE)
         LR    #15,#10             ELSE RETURN NIL (FALSE)
         BR    #14
TRUE     LR    #15,#8
         BR    #14
         END
