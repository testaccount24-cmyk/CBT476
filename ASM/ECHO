         TITLE 'ECHO - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ECHO SYNTAX IS AS FOLLOWS:                                          *
*                                                                     *
* (ECHO FILE1 &OPTIONAL FILE2 EVEN-IF-BOTH-FILES-ARE-TERMINAL-P)      *
*                                                                     *
* FILE1 MUST BE AN INPUT FILE.                                        *
* FILE2 MUST BE AN OUTPUT FILE.                                       *
*                                                                     *
* IF FILE2 IS OMITTED, ECHO MODE IS TURNED OFF FOR FILE1.             *
* OTHERWISE, FILE2 BECOMES THE ECHO FILE FOR FILE1.                   *
*                                                                     *
* IF THE THIRD ARG IS SUPPLIED AND NON-NIL, ECHOING IS ACTIVE EVEN    *
* IF BOTH FILES HAPPEN TO BE ALLOCATED TO THE TERMINAL.  THE DEFAULT  *
* IS FOR ECHOING TO BE DISABLED IF THE TERMINAL IS IN USE FOR BOTH.   *
*                                                                     *
***********************************************************************
         SPACE 1
ECHO     #ZBEG MINARGS=1,MAXARGS=3,NAME='ECHO'
         #ZPDS 3*4+12+0            DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE 1
         #IFIL 1                   ARG 1 = INPUT FILE SPEC
         LR    #6,#7               #6 = ADDRESS OF INPUT FILE BLOCK
         SPACE 1
         XR    #5,#5               CLEAR ADDRESS OF OUTPUT FILE BLOCK
         C     #13,4(,#11)         IF ARG 2 WAS NOT SUPPLIED
         BE    NOFILE2             THEN GO TO TURN ECHOING OFF
         SPACE 1
         #OFIL 2                   ARG 2 = OUTPUT FILE SPEC
         LR    #5,#7               #5 = ADDRESS OF OUTPUT FILE BLOCK
         SPACE 1
NOFILE2  DS    0H
         SPACE 1
***********************************************************************
*                                                                     *
* FIRST OF ALL, IF FILE1 IS ALREADY BEING ECHOED, TURN OFF THE        *
* CURRENT ECHOING MODE.                                               *
*                                                                     *
***********************************************************************
         SPACE 1
         DROP  #7
         USING ZLFILBLK,#6         ADDRESS THE INPUT FILE BLOCK
         SPACE 1
         ICM   #4,15,ZLFECHO       IF FILE1 IS CURRENTLY BEING ECHOED
         BZ    NOECHO1             THEN
         LH    #0,ZLFECCNT-ZLFILBLK(,#4)
         BCTR  #0,0                 DECREMENT ECHO FILE'S REFERENCE CT
         STH   #0,ZLFECCNT-ZLFILBLK(,#4)
*                                   (NOTE #5 = 0 BECAUSE NO FILE2)
         ST    #5,ZLFECHO           CLEAR FILE1'S ECHO POINTER
NOECHO1  DS    0H
         LTR   #5,#5               IF THERE'S A FILE2 TO ECHO TO...
         BZ    NOECHO2             THEN...
         ST    #5,ZLFECHO           SET FILE1'S ECHO POINTER
         LA    #0,1                 INCREMENT ECHO FILE'S REFERENCE CT
         AH    #0,ZLFECCNT-ZLFILBLK(,#5)
         STH   #0,ZLFECCNT-ZLFILBLK(,#5)
         C     #13,8(,#11)         IF ARG3 IS NOT SUPPLIED
         BE    NOETIO               OR
         C     #10,8(,#11)           ARG3 IS NIL
         BNE   ETIO                   THEN
NOETIO   NI    ZLFFLAG2-ZLFILBLK(#5),255-ZLFETIO  TURN OFF ETIO FLAG
         B     NOECHO2             ELSE
ETIO     OI    ZLFFLAG2-ZLFILBLK(#5),ZLFETIO  TURN ON ETIO FLAG
NOECHO2  DS    0H
         SPACE 1
         LR    #15,#10             RETURN NIL (WHAT ELSE? T MAYBE?)
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
         LTORG
         SPACE 1
         END
