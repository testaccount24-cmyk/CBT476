         TITLE 'ZILSUB - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ZILSUB TAKES TWO ARGUMENTS. BOTH MUST BE NUMERIC ATOMS.             *
*                                                                     *
***********************************************************************
*                                                                     *
* CONVERSION RULES ARE AS FOLLOWS:                                    *
*                                                                     *
* 1. BOTH ARGS FIXED         - PERFORM INTEGER SUBTRACTION.           *
* 2. BOTH ARGS FLOAT         - PERFORM REAL SUBTRACTION.              *
* 3. ONE FIXED, ONE FLOAT    - CONVERT THE FIXED ARGUMENT TO          *
*                              FLOAT AND PERFORM REAL SUBTRACTION.    *
*                                                                     *
* 07/18/86 - IF ONE OR BOTH OF THE ARGS IS A BIGNUM, THE SECOND ARG   *
*            IS NEGATED AND ZILADD IS INVOKED TO DO BIGNUM ADD.       *
*                                                                     *
* ANY OTHER TYPES CAUSE AN ERROR.                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* Note: At present, ZILSUB calls MINUS and ZILADD to do subtraction   *
*       if one or both of the arguments is a bignum.                  *
*                                                                     *
***********************************************************************
         SPACE
ZILSUB   #ZBEG MINARGS=2,MAXARGS=2,NAME='ZILSUB',SUBRS=ZILSUBR
         #ZPDS 24                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         LM    #6,#7,0(#11)        LOAD ARGUMENTS
*
* CHECK TYPE OF ARG 1
*
         CLI   ##TYPE(#6),##FIXNUM IF ARG1 IS NOT FIXED
         BNE   ARG1NFIX            THEN TRY OTHER TYPE
         CLI   ##TYPE(#7),##FIXNUM IF ARG2 IS NOT FIXED
         BNE   ARG2NFIX            THEN TRY OTHER TYPE
*                                  FIXED - FIXED = FIXED
         L     #2,##CDR(,#6)       GET FIXED VALUE 1
         S     #2,##CDR(,#7)       SUBTRACT FIXED VALUE 2
         BO    BIGSTUFF            IF OVERFLOW OCCURRED, DO BIGNUM ADD
         LA    #0,20(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM
******** B     RETURN              AND RETURN WITH IT
         SPACE
RETURN   DS    0H                  RETURN WITH NEW ATOM
         SPACE
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ARG2NFIX DS    0H                  ARG 1 IS FIXED, BUT ARG 2 IS NOT
         CLI   ##TYPE(#7),##BIGNUM
         BE    BIGSTUFF            FIXED - BIG
         CLI   ##TYPE(#7),##SFLOAT
         BE    FIXSFLO             FIXED - SFLOAT
         CLI   ##TYPE(#7),##DFLOAT
         BE    FIXDFLO             FIXED - DFLOAT
         B     ERROR2              FIXED - NONUM = ERROR
FIXSFLO  L     #0,##CDR(,#6)       GET FIXED VALUE 1
         #BAS  #14,FIX2FLO         CONVERT FIXED TO FLOAT
         LRER  #F2,#F0             GET FLOAT VALUE 1
         B     SUBSFLO1            FIXED - FLOAT = FLOAT
FIXDFLO  L     #0,##CDR(,#6)       GET FIXED VALUE 1
         #BAS  #14,FIX2FLO         CONVERT FIXED TO FLOAT
         LDR   #F2,#F0             GET FLOAT VALUE 1
         B     SUBDFLO1            FIXED - FLOAT = FLOAT
         SPACE
ARG1NFIX DS    0H                  ARG1 is not fixed
         CLI   ##TYPE(#6),##BIGNUM
         BE    BIGSTUFF
         CLI   ##TYPE(#6),##SFLOAT
         BE    ARG1SFLO
         CLI   ##TYPE(#6),##DFLOAT
         BE    ARG1DFLO
         B     ERROR1
         SPACE
ARG1SFLO DS    0H                  ARG 1 IS SINGLE FLOAT
         SPACE
*---------------------------------------------------------------------*
*                      CHECK TYPE OF ARG 2                            *
*---------------------------------------------------------------------*
         CLI   ##TYPE(#7),##SFLOAT
         BE    SSUBSFLO            SFLOAT - SFLOAT = SFLOAT
         CLI   ##TYPE(#7),##DFLOAT
         BE    SSUBDFLO            SFLOAT - DFLOAT = DFLOAT
         CLI   ##TYPE(#7),##FIXNUM
         BE    SFLOPFIX            SFLOAT - FIXED = SFLOAT
         CLI   ##TYPE(#7),##BIGNUM
         BE    BIGSTUFF            SFLOAT - BIG
         B     ERROR2              FLOAT - NONUM = ERROR
         SPACE 1
*---------------------------------------------------------------------*
*                      FIXNUM TO SINGLE FLOAT                         *
*---------------------------------------------------------------------*
SFLOPFIX LE    #F2,##CDR(,#6)      Get single float value 1
         L     #0,##CDR(,#7)       GET FIXED VALUE 2
         #BAS  #14,FIX2FLO         CONVERT FIXED TO FLOAT
         LRER  #F4,#F0             GET FLOAT VALUE 2
         B     SUBSFLO2            FLOAT - FIXED = FLOAT
         SPACE
ARG1DFLO DS    0H                  ARG 1 IS DOUBLE FLOAT
         SPACE
         LD    #F2,8(,#6)          GET FLOAT VALUE 1
*---------------------------------------------------------------------*
*                      CHECK TYPE OF ARG 2                            *
*---------------------------------------------------------------------*
         CLI   ##TYPE(#7),##DFLOAT
         BE    SUBDFLO1            DFLOAT - DFLOAT = DFLOAT
         CLI   ##TYPE(#7),##SFLOAT
         BE    DSUBSFLO            DFLOAT - SFLOAT = DFLOAT
         CLI   ##TYPE(#7),##FIXNUM
         BE    DFLOPFIX            DFLOAT - FIXED = DFLOAT
         CLI   ##TYPE(#7),##BIGNUM
         BE    BIGSTUFF            DFLOAT - BIG
         B     ERROR2              DFLOAT - NONUM = ERROR
         SPACE 1
*---------------------------------------------------------------------*
*                      FIXNUM TO DOUBLE FLOAT                         *
*---------------------------------------------------------------------*
DFLOPFIX L     #0,##CDR(,#7)       GET FIXED VALUE 2
         #BAS  #14,FIX2FLO         CONVERT FIXED TO FLOAT
         LDR   #F4,#F0             GET FLOAT VALUE 2
         B     SUBDFLO2
         SPACE
*---------------------------------------------------------------------*
*                       SINGLE FLOAT SUBTRACT                         *
*---------------------------------------------------------------------*
SSUBSFLO DS    0H
         LE    #F2,##CDR(,#6)      Get single float value 1
SUBSFLO1 DS    0H
         LE    #F4,##CDR(,#7)      Get single float value 2
SUBSFLO2 DS    0H
         SER   #F2,#F4             SUBTRACT THEM
         STE   #F2,ZLCWORK         PUT RESULT IN REG FOR ATOM-MAKING
         L     #2,ZLCWORK
         LA    #0,20(,#11)         SET STACK LOCATION FOR CONS
         #MKAT FLOAT               MAKE A FLONUM
         B     RETURN              AND RETURN WITH IT
         SPACE 1
*---------------------------------------------------------------------*
*                       DOUBLE FLOAT SUBTRACT                         *
*---------------------------------------------------------------------*
DSUBSFLO DS    0H
         SDR   #F4,#F4
         LE    #F4,##CDR(,#7)      Get double float value 2
         B     SUBDFLO2
SSUBDFLO DS    0H
         SDR   #F2,#F2
         LE    #F2,##CDR(,#6)      Get double float value 1
SUBDFLO1 DS    0H
         LD    #F4,8(,#7)          Get double float value 2
SUBDFLO2 DS    0H
         SDR   #F2,#F4             SUBTRACT THEM
         LDR   #F0,#F2
         L     #15,ZLCMDF          Get address of ZILMDF
         #BASR #14,#15             Call ZILMDF to make a double-float
         B     RETURN              AND RETURN WITH IT
         SPACE 1
BIGSTUFF DS    0H                  ONE ARG IS A BIGNUM, OR OVERFLOW
         SPACE
         ST    #7,20(,#11)         PASS ARG 2 TO MINUS
         L     #15,@MINUS          GET ADDRESS OF MINUS
         LA    #1,20(,#11)         POINT TO ARGS ON STACK
         #BASR #14,#15             CALL MINUS TO NEGATE ARG 2
         ST    #15,4(,#11)         STORE RESULT IN ARG 2
         LR    #1,#11              RELOAD STACK POINTER
         L     #15,@ZILADD         GET ADDRESS OF ZILADD
         LM    #11,#12,8(#1)       RELOAD REGS AS IF RETURNING
         L     #14,16(,#1)         RELOAD REGS AS IF RETURNING
         BR    #15                 BRANCH TO ZILADD TO DO BIGNUM ADD
         EJECT
         COPY  FIX2FLO             FIXED TO FLOAT CONVERSION ROUTINE
         EJECT
ZILSUBRS DS    0A
@MINUS   DC    V(MINUS)            ADDRESS OF SUBROUTINE
         DC    CL8'MINUS'
@ZILADD  DC    V(ZILADD)           ADDRESS OF SUBROUTINE
         DC    CL8'ZILADD'
ZILSUBRE EQU   *
         EJECT
ERROR1   #ERR  'Argument 1 to subtract not a number - ',0(#11)
ERROR2   #ERR  'Argument 2 to subtract not a number - ',4(#11)
         SPACE 1
         LTORG
         END
