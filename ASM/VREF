         TITLE 'VREF - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
         SPACE 1
VREF     #ZBEG MINARGS=2,MAXARGS=2,NAME='VREF'
         SPACE 1
         USING VREF,#15
         LM    #2,#3,0(#1)         LOAD ARGUMENTS
         TM    ##TYPE(#2),##VEC    IF ARG1 IS NOT A VECTOR
         BNO   ERROR1              THEN ERROR.  ELSE...
         CLI   ##TYPE(#3),##FIXNUM IF ARG2 IS NOT A FIXNUM
         BNE   ERROR2              THEN ERROR.  ELSE...
         L     #4,##CDR(,#3)       GET VALUE OF FIXNUM INTO REGISTER
         XR    #0,#0               CLEAR INSERT REGISTER
         SLA   #4,2                MULTIPLY BY 4 TO CONVERT TO OFFSET
         BM    ERROR3              IF LESS THAN ZERO, OUT OF RANGE
         ICM   #0,7,1(#2)          GET LENGTH OF VECTOR TEXT
         CR    #4,#0               IF NOT LESS THAN TOTAL VECTOR LENGTH
         BNL   ERROR3              THEN OUT OF RANGE
         L     #15,##VECDAT(#4,#2) ELSE LOAD ELEMENT N OF VECTOR
         BR    #14                 AND RETURN TO CALLER
         SPACE 1
         DROP  #15
         SPACE 1
         USING VREF,#7
         SPACE 1
ERROR1   DS    0H
         LR    #7,#15
         #ERR  'Argument 1 to VREF not a vector - ',(#2)
ERROR2   DS    0H
         LR    #7,#15
         #ERR  'Argument 2 to VREF not a fixnum - ',(#3)
ERROR3   DS    0H
         LR    #7,#15
         #ERR  'VREF index ',(#3),' out of range for vector ',(#2)
         SPACE 1
         LTORG
         SPACE 1
         END
