         TITLE 'ZILVTOS - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* (ZILVTOS vector1 length2)                                           *
*                                                                     *
*  vector1    - a vector of characters                                *
*  length2    - the number of vector elements to make into a string   *
*                                                                     *
* This function is used by the new ZIL reader to build strings.       *
*                                                                     *
***********************************************************************
ZILVTOS  #ZBEG MINARGS=2,MAXARGS=2,NAME='ZILVTOS'
         #ZPDS 2*4+12+0 = 20       DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
*
* Check arg 1 - must be vector of characters.
*
         LM    #6,#7,0(#11)        Get args 1 and 2
         CLI   ##TYPE(#6),##VECTOR Arg 1 must be a vector
         BNE   ERROR1              else error
         XR    #5,#5               Clear insert register
         ICM   #5,7,1(#6)          Get length of vector arg 1
         SRA   #5,2                Divide by 4 to get # of elements
*
* Check arg 2 - must be fixnum and not greater than length of arg 1.
*
         CLI   ##TYPE(#7),##FIXNUM Arg 2 (length) must be a fixnum
         BNE   ERROR2              else error
         L     #7,##CDR(,#7)       Get numeric length value
         LTR   #7,#7               If requested length is 0
         BZ    RETNULL             then just return null string
         BM    ERROR2              If requested length negative, error
         CR    #7,#5               If positive but longer than vector
         BH    ERROR3              then error
*
* Allocate space for a string of the requested length.
*
         LR    #2,#7               Length for ZILVSS = arg 2
         XR    #1,#1               ENTRY CODE 0 = ALLOCATE NEW SPACE
         L     #15,ZLCVSS          GET ADDRESS OF ZILVSS
         LA    #0,20(,#11)         SET CURRENT STACK POINTER FOR GC
         #BASR #14,#15             CALL ZILVSS TO CREATE A STRING
         LTR   #15,#15             IF STRING BUILD FAILED
         BZ    ERRORGC             THEN ERROR
*
* #4 points to the text portion of the string.
*
         L     #6,0(,#11)          Reload arg 1 pointer from stack
         LA    #3,##VECDAT(,#6)    Point to 1st element of vector arg 1
VLOOP    DS    0H
         L     #2,0(,#3)           Get element of vector
         CLI   ##TYPE(#2),##FIXNUM Each element must be a character
         BNE   ERROR4              (i.e. a fixnum between 0 and 255)
         L     #0,##CDR(,#2)       Get numeric value
         CL    #0,#SFW255          If not within character range
         BH    ERROR4              then error
         STC   #0,0(,#4)           Store character into string
         LA    #3,4(,#3)           Bump input pointer
         LA    #4,1(,#4)           Bump output pointer
         BCT   #7,VLOOP            Continue (arg 2) times
         B     RETURN              Return new-string
         SPACE 1
RETNULL  DS    0H                  Requested length is zero
         LA    #15,NULLSTR         Return null string
******** B     RETURN              Return new-string
         SPACE 1
RETURN   DS    0H
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
NULLSTR  DS    0A                  Align null string to fullword
         DC    YL1(##STRING),AL3(0),XL4'00'  String ""
         SPACE 1
ERROR1   #ERR  'Argument 1 to ZILVTOS not a vector - ',0(#11)
ERROR2   #ERR  'Argument 2 to ZILVTOS not a non-negative fixnum - ',   X
               4(#11)
ERROR3   #ERR  'Argument 2 to ZILVTOS exceeds length of argument 1 - ',X
               4(#11)
ERROR4   #ERR  'Non-character element found in ZILVTOS argument 1 - ', X
               0(#2)
ERRORGC  #ERR  'ZIL4010 ZILVTOS: Not enough vector/string space to builX
               d string.',PREFIX=NO
         SPACE 1
         LTORG
         END
