         TITLE 'ZMVECTOR - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE MAKES A VECTOR OUT OF A LIST.  THE SIZE OF THE      *
* VECTOR IS THE LENGTH OF THE LIST.                                   *
*                                                                     *
* IT TAKES ONE REQUIRED ARGUMENT - THE LIST.                          *
*                                                                     *
***********************************************************************
         SPACE 1
ZMVECTOR #ZBEG MINARGS=1,MAXARGS=1,NAME='ZMVECTOR'
         SPACE 1
         #ZPDS 1*4+12+0 = 16
         #ZSAV ,
         SPACE 1
         L     #1,0(,#11)          LOAD ARG1 = THE LIST
         XR    #7,#7               INITIALIZE LENGTH TO ZERO
LENLOOP  DS    0H                  LOOP TO DETERMINE LENGTH OF LIST
         CR    #1,#10              WHEN END OF LIST
         BE    GOTLEN              WE HAVE THE LENGTH
         TM    ##TYPE(#1),##ATOM   IF ATOM OR IMPROPER LIST
         BO    ERROR1              THEN ERROR.  ELSE...
         L     #1,##CDR(,#1)       GET NEXT CDR OF LIST
         BCT   #7,LENLOOP          DECREMENT COUNT AND CONTINUE
GOTLEN   DS    0H                  #7 = NUMBER OF ELEMENTS IN LIST
         LCR   #7,#7               MAKE IT POSITIVE
         SLA   #7,2                MULTIPLY BY 4 FOR VECTOR LENGTH
         LR    #2,#7               SET LENGTH FOR ZILVSS
         XR    #1,#1               ENTRY CODE 0 = ALLOCATE NEW SPACE
         L     #15,ZLCVSS          GET ADDRESS OF STRING SPACE SERVICE
         LA    #0,16(,#11)         SET CURRENT STACK LOCATION
         #BASR #14,#15             CALL ZILVSS TO BUILD THE OBJECT
         LTR   #4,#4               IF STRING BUILD FAILED
         BZ    ERROR2              THEN ERROR
         MVI   ##TYPE(#15),##VECTOR SET OBJECT TYPE = VECTOR
         L     #2,0(,#11)          GET ARG 1 (THE LIST)
         LA    #6,4                SET INCREMENT REGISTER FOR BXLE
         BCTR  #7,0                SET TERMINATION REGISTER FOR BXLE
         XR    #1,#1               SET INDEX REGISTER
INITLOOP L     #0,##CAR(,#2)       GET EACH ELEMENT OF LIST
         ST    #0,0(#1,#4)         INITIALIZE EACH ELEMENT IN VECTOR
         L     #2,##CDR(,#2)       GET NEXT LIST ELEMENT
         BXLE  #1,#6,INITLOOP
         SPACE 1
RETURN   DS    0H                  #15 POINTS TO VECTOR BUILT
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   #ERR  'Argument to ZMVECTOR not a proper list - ',0(#11)
ERROR2   #ERR  PREFIX=NO,'ZIL4010 ZMVECTOR: Not enough vector/string spX
               ace to build vector.'
         SPACE 1
         LTORG
         SPACE 1
         END
