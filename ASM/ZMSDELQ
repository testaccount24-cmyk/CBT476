         TITLE 'ZMSDELQ - BUILT-IN ZIL 1.3 SUBROUTINE FOR MACSYMA'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ZMSDELQ: SAME AS DELQ BUT WORKS ON NUMBERS AS WELL.                 *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND USES NO STACK SPACE.               *
*                                                                     *
***********************************************************************
         SPACE 1
ZMSDELQ  #ZBEG MINARGS=2,MAXARGS=3,NAME='ZMSDELQ'
         SPACE 1
         LR    #7,#15              LOCAL BASE REGISTER
         DROP  #15
         USING ZMSDELQ,#7
         SPACE 1
         LM    #1,#3,0(#1)         LOAD ARGS 1, 2, 3
         LR    #15,#2              PRESET RETURN VALUE
         SPACE 1
         CR    #3,#13              IF ARG 3 NOT SUPPLIED
         BNE   ARG3SUP             THEN
         LR    #3,#10               SET ARG 3 = NIL
         B     ARG3OK               AND GO PROCESS
ARG3SUP  DS    0H                  ELSE
         SPACE 1
         CR    #3,#10              IF ARG3 (COUNT) IS NIL
         BE    ARG3OK              THEN IT'S OK
         CLI   ##TYPE(#3),##FIXNUM ELSE ARG3 MUST BE A FIXNUM
         BNE   ARG3ERR             OTHERWISE ERROR
         ICM   #6,#15,##CDR(#3)    GET FIXNUM VALUE OF ARG3 = COUNT
         BNPR  #14                 IF NOT POSITIVE, RETURN W/O DELETES
ARG3OK   DS    0H
         LR    #4,#10              INITIALIZE BASHEE POINTER
         SPACE 1
         CLI   ##TYPE(#1),##FIXNUM IF ARG 1 IS A FIXNUM
         BE    FIXP                THEN GO TO DO FIXNUM DELQ USING EQL
         SPACE 1
LOOP     DS    0H                  DO WHILE LIST IS NOT NULL
         CR    #2,#10              WHEN END OF LIST REACHED
         BER   #14                 RETURN WITH VALUE IN #15
         TM    ##TYPE(#2),##ATOM   IF LIST ENDS WITH ATOM
         BOR   #14                 DO LIKE IT ENDED IN NIL
         C     #1,##CAR(,#2)       IF ARG1 IS EQ TO CAR OF (SUB)LIST
         BNE   NOTEQ               THEN...
         L     #2,##CDR(,#2)        DROP IT FROM (SUB)LIST
         CR    #4,#10               IF WE HAVE SOMETHING TO BASH
         BE    NOBASH               THEN
         ST    #2,##CDR(,#4)         BASH IT (RPLACD TO SPLICE OUT CAR)
         B     AFTBASH              ELSE
NOBASH   LR    #15,#2                JUST CHANGE THE RETURN VALUE PTR
AFTBASH  DS    0H
         CR    #3,#10               IF THERE IS A COUNT (ARG3 NOT NIL)
         BE    LOOP                 THEN...
         BCT   #6,LOOP               DECREMENT, THEN LOOP UNLESS COUNT
         BR    #14                   REACHED, IN WHICH CASE RETURN
NOTEQ    DS    0H                  ELSE THIS CAR DOESN'T MATCH
         LR    #4,#2               SET BASHEE POINTER
         L     #2,##CDR(,#2)       CDR DOWN (SUB)LIST
         B     LOOP                CONTINUE LOOPING
         SPACE 1
FIXP     DS    0H                  HERE IF ARG 1 IS A FIXNUM
         SPACE 1
FIXNLOOP DS    0H
         CR    #2,#10              WHEN END OF LIST REACHED
         BER   #14                 RETURN WITH VALUE IN #15
         TM    ##TYPE(#2),##ATOM   IF LIST ENDS WITH ATOM
         BOR   #14                 DO LIKE IT ENDED IN NIL
         L     #5,##CAR(,#2)       LOAD CAR OF (SUB)LIST
         CLC   0(8,#5),0(#1)       IF ARG1 EQL TO CAR OF (SUB)LIST
         BNE   FXNOTEQ             THEN...
         L     #2,##CDR(,#2)        DROP IT FROM (SUB)LIST
         CR    #4,#10               IF WE HAVE SOMETHING TO BASH
         BE    FXNOBASH             THEN
         ST    #2,##CDR(,#4)         BASH IT (RPLACD TO SPLICE OUT CAR)
         B     FXAFBASH             ELSE
FXNOBASH LR    #15,#2                JUST CHANGE THE RETURN VALUE PTR
FXAFBASH DS    0H
         CR    #3,#10               IF THERE IS A COUNT (ARG3 NOT NIL)
         BE    FIXNLOOP             THEN...
         BCT   #6,FIXNLOOP           DECREMENT, THEN LOOP UNLESS COUNT
         BR    #14                   REACHED, IN WHICH CASE RETURN
FXNOTEQ  DS    0H                  ELSE THIS CAR DOESN'T MATCH
         LR    #4,#2               SET BASHEE POINTER
         L     #2,##CDR(,#2)       CDR DOWN (SUB)LIST
         B     FIXNLOOP            CONTINUE LOOPING
         SPACE 1
ARG3ERR  #ERR  'Invalid DELQ count argument - ',(#3)
         SPACE 1
         LTORG
         END
