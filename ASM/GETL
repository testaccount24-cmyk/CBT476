         TITLE 'GETL - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* GETL TAKES TWO ARGUMENTS - AN ATOM AND A LIST OF PROPERTIES         *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND THEREFORE USES NO STACK SPACE.     *
*                                                                     *
* Note: GETL does not exist in Common LISP.  The equivalent CL        *
*       definition might be:                                          *
*                                                                     *
* (defun getl (symbol indicator-list)                                 *
*  (multiple-value-bind (found-indicator found-value plist-tail)      *
*                       (get-properties (symbol-plist symbol)         *
*                                       indicator-list)               *
*                       plist-tail))                                  *
*                                                                     *
***********************************************************************
         SPACE 1
GETL     #ZBEG MINARGS=2,MAXARGS=2,NAME='GETL'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING GETL,#3
         SPACE 1
         LM    #5,#6,0(#1)         LOAD THE ARGUMENTS
         CLI   ##TYPE(#5),##SYMBOL ARG 1 MUST BE A SYMBOL
         BNE   NOTSYM
         L     #15,##PLIST(,#5)    GET PROPERTY LIST POINTER
PROCESS  DS    0H
* Initial loop-bypassing test for null property list
         CR    #15,#10             IF END OF PROPERTY LIST (NULL)
         BER   #14                 THEN RETURN NIL (NO PROPS FOUND)
         SPACE 1
LOOP     DS    0H
         LM    #0,#1,##CAR(#15)    GET CAR AND CDR OF NEXT CELL
*
* If #15 didn't point to a CONS cell, then the high-order bit of #1
* will be one, since it was an "atom" type bit.
*
         LTR   #0,#0               IF PROPERTY (SUB)LIST ATOMIC
         BM    ERROR2              THEN ERROR
*                                  #0 = CAR OF (SUB)LIST = INDICATOR
*                                  #1 = CDR OF (SUB)LIST = NEXT SUBLIST
         LR    #4,#6               START SCANNING LIST OF PROPERTIES
PROPLOOP DS    0H
         TM    ##TYPE(#4),##ATOM   AT END OF ARG2
         BO    NOTFOUND            THIS MATCHES NO PROPS IN THE LIST
         C     #0,##CAR(,#4)       IF INDICATOR IS "EQ" TO PROPERTY
         BER   #14                 THEN RETURN WITH SUBLIST
         L     #4,##CDR(,#4)       ELSE GET CDR OF LIST OF PROPS
         B     PROPLOOP            AND CONTINUE SEARCHING IT
NOTFOUND DS    0H
         TM    ##TYPE(#1),##ATOM   IF PROPERTY (SUB)LIST ATOMIC,
         L     #15,##CDR(,#1)      GETTING CDR OF PROPERTY (SUB)LIST,
         BO    ERROR2              THEN ERROR
         CR    #15,#10             CHECK SUBLIST FOR NIL
         BER   #14                 IF SUBLIST IS NIL, RETURN NIL
         B     LOOP                IF SUBLIST NOT NIL, KEEP LOOPING
         SPACE 1
NOTSYM   DS    0H
         SPACE 1
***********************************************************************
*                                                                     *
* FOR COMPATIBILITY WITH MACLISP AND OTHER APPLICATIONS THAT USE      *
* DISEMBODIED PROPERTY LISTS, IF ARG 1 IS A LIST THEN WE ACCEPT IT.   *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    ##TYPE(#5),##ATOM   See if it's a list
         BO    ERROR1              If so, then...
         L     #15,##CDR(,#5)       get CDR and use as property list
         B     PROCESS             Else it's an atom
         SPACE 1
ERROR1   DS    0H                  Arg 1 not even a list...
         SPACE 1
***********************************************************************
*                                                                     *
* If arg 1 is neither a symbol nor a list, it should be an error.     *
* However, for compatibility with Maclisp and other dialects of LISP  *
* that run Macsyma, we allow GETL to always return the specified      *
* "default value" in such cases.                                      *
*                                                                     *
***********************************************************************
         SPACE 1
         LR    #4,#14              SAVE RETURN ADDRESS
* Issue warning message and continue
         #ERR  1,'Argument 1 to GETL not a symbol, returning NIL - ',  X
               (#5)
         LR    #15,#10             SET RETURN VALUE TO NIL
         BR    #4                  RETURN TO CALLER
         SPACE 1
ERROR2   DS    0H                  INVALID PROPERTY LIST
         #ERR  'ZIL4011 GETL: Invalid property list found for ',(#5),  X
               PREFIX=NO
         SPACE 1
         LTORG
         END
