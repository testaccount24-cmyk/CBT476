         TITLE 'REVERSE - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* REVERSE TAKES 1 ARGUMENT.                                           *
*                                                                     *
***********************************************************************
         SPACE 1
REVERSE  #ZBEG MINARGS=1,MAXARGS=1,NAME='REVERSE'
         SPACE 1
         #ZPDS 1*4+12
         #ZSAV ARGS=1
         SPACE 1
         L     #3,0(,#11)          LOAD THE ARGUMENT
         LR    #15,#10             INITIALIZE RESULT TO NIL
         CR    #3,#10              IF ARG IS NIL
         BE    RETURN              THEN RETURN RESULT
LOOP     DS    0H
         ICM   #1,15,##CAR(#3)     GET CAR OF (SUB)LIST
         BM    ERROR1              IF (SUB)LIST WASN'T A CONS, ERROR
         LR    #2,#15              GET RESULT SO FAR
         L     #15,ZLCCONS         CALL CONS TO ADD ITEM TO RESULT LIST
         #BASR #14,#15             (SETS #15 TO RESULT OF CONS)
         L     #3,##CDR(,#3)       GET CDR OF (SUB)LIST
         CR    #3,#10              WHEN (SUB)LIST IS NIL,
******** BE    RETURN              THEN RETURN RESULT
         BNE   LOOP                CONTINUE
         SPACE 1
RETURN   DS    0H                  #15 CONTAINS RETURN VALUE
         #ZRET ,                   RETURN TO CALLER
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         #ERR  'Argument to REVERSE not a proper list - ',0(#11)
         SPACE 1
         LTORG
         END
