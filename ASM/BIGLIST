         TITLE 'BIGLIST - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
* BIGLIST TAKES 1 ARGUMENT - A BIGNUM TO CONVERT TO A LIST OF FIXNUMS *
***********************************************************************
         SPACE
BIGLIST  #ZBEG MINARGS=1,MAXARGS=1,NAME='BIGLIST'
         #ZPDS 20                  DEFINE STACK SIZE
         #ZSAV ARGS=1              SAVE REGISTERS
         SPACE
*
* CHECK INPUT ARGUMENT - MUST BE A BIGNUM
*
         L     #6,0(,#11)          GET ARG 1
         CLI   ##TYPE(#6),##BIGNUM MUST BE A BIGNUM
         BNE   ERROR1              OR ELSE
* #6 -> BIGNUM
* #3 = LENGTH OF BIGNUM DATA (# OF WORDS * 4)
* #4 -> EACH WORD IN BIGNUM
* #5 -> LIST BEING BUILT
*
* FOR EACH WORD IN THE BIGNUM, MAKE A FIXNUM OUT OF IT.
* WE GO BACKWARDS TO FACILITATE CONSING THEM TOGETHER TO MAKE A LIST.
*
         XR    #3,#3               CLEAR INSERT REGISTER
         ICM   #3,7,1(#6)          GET LENGTH OF BIGNUM DATA
         LA    #4,##VECDAT-4(,#3)  INDEX TO LAST WORD IN BIGNUM
         LR    #15,#10             INITIALIZE OUTPUT LIST TO NIL
         LA    #0,20(,#11)         SET STACK POINTER FOR CONS
         LH    #5,=H'-4'           SET UP BXLE DECREMENT
         AR    #4,#5
LOOP     DS    0H                  LOOP USING BXLE REGS #4, #6, #7
         ST    #15,16(,#11)        (SETQ LIST (CONS FIXNUM LIST))
         L     #6,0(,#11)          RELOAD BIGNUM ARG FROM STACK
         L     #2,##VECDAT(#4,#6)  LOAD BIGNUM WORD VALUE
         #MKAT FIXED               MAKE A FIXNUM OUT OF IT
         LR    #1,#15              ARG 1 TO CONS = BUILT FIXNUM
         L     #2,16(,#11)         ARG 2 TO CONS = LIST SO FAR
         L     #15,ZLCCONS         CALL CONS TO ADD NEW ATOM TO LIST
         #BASR #14,#15
         BXH   #4,#5,LOOP          CONTINUE UNTIL HIT START OF BIGNUM
RETLIST  DS    0H
         SPACE
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 2
ERROR1   DS    0H
         #ERR  'Argument to BIGLIST not a bignum - ',(#6)
         SPACE 1
         LTORG
         END
