         TITLE 'UNEBCDIC - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
* UNEBCDIC TAKES ONE ARGUMENT - A ONE-CHARACTER SYMBOL OR STRING      *
* TO BE CONVERTED TO A FIXNUM (USED AS A "CHARACTER OBJECT").         *
* THIS IS USED BY THE #/ AND #\ READER MACROS.                        *
***********************************************************************
         SPACE
UNEBCDIC #ZBEG MINARGS=1,MAXARGS=1,NAME='UNEBCDIC'
         SPACE
         L     #7,0(,#1)           GET ARG 1
         CLI   ##TYPE(#7),##STRING IF ARG IS A STRING
         BE    PROCESS             THEN GO AHEAD AND PROCESS IT
         CLI   ##TYPE(#7),##SYMBOL IF ARG 1 IS A SYMBOL
         BNE   ERROR1              THEN OK, ELSE ERROR
         L     #7,##PNAME(,#7)     IF A SYMBOL, GET THE PRINT NAME
PROCESS  DS    0H
         XR    #2,#2               CLEAR INSERT REGISTER
         CLC   1(3,#7),=AL3(1)     IF LENGTH IS NOT 1
         BNE   ERROR1              THEN ERROR
         IC    #2,##VECDAT(,#7)    PICK UP CHARACTER TO BE NUMBERIZED
         SLA   #2,3                Convert to index into fixnum table
         LA    #15,#SFT(#2)        Load address of fixnum from table
         BR    #14
         SPACE 1
         DROP  #15
         SPACE 1
ERROR1   DS    0H                  IF NOT 1, ERROR
         LR    #2,#15
         USING UNEBCDIC,#2
         L     #7,0(,#1)           GET ARG 1
         #ERR  'Argument to UNEBCDIC not a 1-character string - ',     X
               (#7)
         SPACE 1
         LTORG
         END
