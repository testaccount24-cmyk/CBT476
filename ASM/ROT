         TITLE 'ROT - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ROT TAKES TWO INTEGER ARGUMENTS.  CURRENTLY THEY MUST BE FIXNUMS.   *
*                                                                     *
* THIS FUNCTION DOES AN ARITHMETIC ROTATE OPERATION.                  *
*                                                                     *
***********************************************************************
ROT      #ZBEG MINARGS=2,MAXARGS=2,NAME='ROT'
         #ZPDS 2*4+12+0 (= 20)     DEFINE STACK SIZE
         #ZSAV ARGS=2              SAVE REGISTERS
         SPACE
         LM    #6,#7,0(#11)        LOAD ARGUMENTS
         CLI   ##TYPE(#6),##FIXNUM
         BNE   ERROR1
         CLI   ##TYPE(#7),##FIXNUM
         BNE   ERROR2
         ICM   #4,15,##CDR(#7)     GET ARG 2 VALUE = SHIFT AMOUNT
         BZ    NOSHIFT             IF ZERO, NO SHIFTING REQUIRED
         BM    RSHIFT              IF NEGATIVE, RIGHT SHIFT REQUIRED
*                                  ELSE POSITIVE, DO LEFT SHIFT
         L     #2,##CDR(,#6)       GET ARG 1 VALUE INTO REGISTER 2
         LR    #3,#2               MAKE A COPY IN OTHER REG OF PAIR
         SLDL  #2,0(#4)            GET IT INTO REG 3 AND CLEAR REG 2
*                                  RESULT IS IN #2
         B     MAKEIT              AND GO MAKE A FIXNUM OUT OF IT
RSHIFT   DS    0H
         LPR   #4,#4               CHANGE SHIFT AMOUNT TO POSITIVE
         L     #2,##CDR(,#6)       GET ARG 1 VALUE INTO REGISTER 2
         LR    #3,#2               MAKE A COPY IN OTHER REG OF PAIR
         SRDL  #2,0(#4)            GET IT INTO REG 3 AND CLEAR REG 2
         LR    #2,#3               GET RESULT INTO #2
******** B     MAKEIT              AND GO MAKE A FIXNUM OUT OF IT
MAKEIT   DS    0H                  NOW #2 CONTAINS SHIFTED VALUE
         LA    #0,20(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM ATOM
         B     RETURN
         SPACE 1
NOSHIFT  DS    0H
         LR    #15,#6              NO SHIFTING - JUST RETURN ARG 1
         SPACE 1
RETURN   DS    0H
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ERROR1   #ERR  'Argument 1 to ROT not a fixnum - ',0(#11)
ERROR2   #ERR  'Argument 2 to ROT not a fixnum - ',4(#11)
         SPACE 1
         LTORG
         END
