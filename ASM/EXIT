         TITLE 'EXIT - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* EXIT TAKES ONE (OPTIONAL) ARGUMENT - RETURN CODE SPEC.              *
*                                                                     *
* IT GETS THE RETURN ADDRESS TO "ZILMAIN" CODE FROM THE BOTTOM OF     *
* THE PUSH-DOWN STACK AND RETURNS TO THERE.  IF THE OPTIONAL OPERAND  *
* IS SPECIFIED (NON-NIL) IT IS ASSUMED TO BE A FIXNUM AND THE VALUE   *
* IS PICKED UP THEREFROM AND INSERTED INTO THE RETURN CODE FIELD.     *
*                                                                     *
***********************************************************************
EXIT     #ZBEG MINARGS=0,MAXARGS=1,NAME='EXIT'
         SPACE
         L     #1,0(,#1)           GET ARGUMENT 1
         CR    #1,#10              IF NOT NIL
         BE    NOSETRC-EXIT(,#15)   AND
         CR    #1,#13                NOT OMITTED
         BE    NOSETRC-EXIT(,#15)     THEN...
*
* DON'T BOTHER CHECKING TYPE - WE GOTTA RETURN ANYHOW
*
         L     #1,##CDR(,#1)        GET FIXNUM VALUE ITSELF
         ST    #1,ZLCRETCD          STORE INTO RETURN CODE VALUE
NOSETRC  DS    0H                  OTHERWISE USE RETURN CODE AS IS
         LR    #15,#10             SET RETURN VALUE (IRRELEVANT)
         L     #1,ZLCPDADR         GET BOTTOM-OF-STACK ADDRESS
         LM    #11,#12,0(#1)       RESTORE STACK AND BASE REGISTERS
         L     #14,8(,#1)          RESTORE RETURN REGISTER
         BR    #14                 RETURN TO MAINLINE
         SPACE
         LTORG
         SPACE
         END
