         TITLE 'ISPFP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ISPFP RETURNS T IF IT IS POSSIBLE TO INVOKE ISPF DIALOG SERVICES,   *
* AND NIL IF IT ISN'T.  THIS CODE IS LARGELY STOLEN FROM THE IBM CODE *
* IN ISPLINK.                                                         *
*                                                                     *
***********************************************************************
         SPACE 1
ISPFP    #ZBEG MINARGS=0,MAXARGS=0,NAME='ISPFP'
         SPACE 1
         L     #1,540              Get TCB address
TRYIT    DS    0H                  See if this TCB belongs to ISPF
         ICM   #2,15,X'70'(#1)     Get TCBFSA - first save area
         BZ    NOPE                If zero, not this one
         CLI   24(#2),X'00'        If saved reg 1 is a 31-bit address
         BNE   NOPE                then not this one
         ICM   #2,15,24(#2)        Get saved reg 1 from save area
         BZ    NOPE                If zero, not this one
         CLI   0(#2),X'00'         If it's a 31-bit pointer
         BNE   NOPE                then not this one
         L     #2,0(,#2)           Else load where reg1 points to
         CLC   0(3,#2),=CL3'TLD'   See if it's the TLD
         BE    TRUE                If it is, success
NOPE     DS    0H                  Else not this TCB...
         L     #1,X'84'(,#1)       Get TCBOTC - mother task TCB
         C     #1,X'7C'(,#1)       Compare with TCBJSTCB - job step TCB
         BNE   TRYIT               If not it, try again with this TCB
*                                  Else job step TCB - no more TCB's
         SPACE 1
FALSE    LR    #15,#10             ISPEXEC is not possible, return NIL
         BR    #14
         SPACE 1
TRUE     LR    #15,#8              ISPEXEC is possible, return T
         BR    #14
         SPACE 1
         LTORG
         SPACE 1
         END
