         TITLE 'CHARUP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* CHARUP TAKES ONE ARGUMENT - A FIXNUM.                               *
* THE FIXNUM IS INTERPRETED AS A CHARACTER TO BE CONVERTED TO ITS     *
* UPPER CASE EQUIVALENT.                                              *
*                                                                     *
* ALIAS "CHAR-UPCASE".                                                *
*                                                                     *
***********************************************************************
         SPACE 1
CHARUP   #ZBEG MINARGS=1,MAXARGS=1,NAME='CHAR-UPCASE'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING CHARUP,#3
         SPACE 1
         L     #15,0(,#1)          GET ATOM (ARG 1 TO CHAR-UPCASE)
         CLI   ##TYPE(#15),##FIXNUM MUST BE A FIXNUM
         BNE   ERROR1              OR ELSE
         L     #4,##CDR(,#15)      GET FIXNUM CONTENTS
         CL    #4,#SFW255          IF NOT 1-BYTE CHARACTER FIXNUM
         BHR   #14                  THEN JUST RETURN IT AS IS
         XR    #2,#2
         IC    #2,TABLE(#4)        IF IN RANGE, LOAD CHAR FROM TABLE
         CR    #2,#4               IF THE SAME
         BER   #14                 THEN JUST RETURN
         SLA   #2,3                Else convert number to table index
         LA    #15,#SFT(#2)        Point to fixnum in fixnum table
         BR    #14                  and return with it
         SPACE 1
TABLE    DC    256YL1(*-TABLE)     ALL CHARS TRANSLATE TO SELVES
         ORG   TABLE+C'A'-X'40'    EXCEPT LOWER CASE TO UPPER CASE
         DC    C'ABCDEFGHI'
         ORG   TABLE+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   TABLE+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG
         SPACE 1
ERROR1   #ERR  'Argument to CHAR-UPCASE not a fixnum - ',0(#11)
         SPACE 1
         LTORG
         END
