         TITLE 'NREVERSE - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* NREVERSE TAKES ONE ARGUMENT - A LIST TO BE REVERSED IN PLACE.       *
*                                                                     *
***********************************************************************
         SPACE 1
NREVERSE #ZBEG MINARGS=1,MAXARGS=1,NAME='NREVERSE'
         SPACE 1
         LR    #7,#15
         DROP  #15
         USING NREVERSE,#7
         SPACE 1
         L     #15,0(,#1)          LOAD THE ARGUMENT
         CR    #15,#10             IF ARGUMENT IS NIL
         BER   #14                 THEN RETURN NIL
         SPACE 1
         TM    ##TYPE(#15),##ATOM  IF ARGUMENT IS NOT A LIST
         BO    ERROR1              THEN ERROR
         SPACE 1
         L     #2,##CDR(,#15)      LOAD OLD-CDR WITH CDR OF ARGUMENT
         LR    #3,#10              SET OLD-CAR TO NIL
         B     GO                  START UP LOOP
NOTNULL  DS    0H
         TM    ##TYPE(#2),##ATOM   IF ARGUMENT IS NOT A LIST
         BO    ERROR2              THEN ERROR
         ST    #3,##CDR(,#15)      BASH CDR OF LIST WITH OLD-CAR
         LR    #3,#15              SET OLD-CAR TO LIST
         LR    #15,#2              SET LIST TO OLD-CDR
         L     #2,##CDR(,#2)       SET OLD-CDR TO CDR OF OLD-CDR
GO       DS    0H
         CR    #2,#10              IF OLD-CDR IS NULL
         BNE   NOTNULL             THEN...
         ST    #3,##CDR(,#15)       BASH CDR OF LIST WITH OLD-CAR
         BR    #14                  RETURN LIST
*                                  ELSE LOOP
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         L     #2,0(,#1)           Reload arg 1 from stack
         #ERR  'Argument 1 to NREVERSE not a list - ',(#2)
         SPACE 1
ERROR2   DS    0H                  ERROR IN ARGUMENT
         L     #2,0(,#1)           Reload arg 1 from stack
         #ERR  'Argument 1 to NREVERSE not a proper list.',            X
               X'0D',              carriage return                     X
               'Value may have been destroyed - ',(#2)
         SPACE 1
         LTORG
         END
