         TITLE 'REMPROP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* REMPROP TAKES 2 ARGUMENTS.                                          *
*                                                                     *
***********************************************************************
         SPACE 1
REMPROP  #ZBEG MINARGS=2,MAXARGS=2,NAME='REMPROP'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING REMPROP,#3
         SPACE 1
         LM    #5,#6,0(#1)         LOAD THE ARGUMENTS
         CLI   ##TYPE(#5),##SYMBOL ARG 1 MUST BE A SYMBOL
         BNE   NOTSYM
         L     #7,##PLIST(,#5)     LOAD PROPERTY LIST OF ARG 1
PROCESS  DS    0H
         CR    #7,#10              IF NULL
         BE    RETNIL              THEN RETURN NIL
         TM    ##TYPE(#7),##ATOM   CHECK VALIDITY OF PROPERTY LIST
         BO    ERROR2
         C     #6,##CAR(,#7)       IF FIRST PROPERTY ON LIST MATCHES
         L     #7,##CDR(,#7)       (SKIP TO REST OF PLIST)
         BNE   LOOP                THEN (REMOVE PROPERTY) ...
         LM    #15,#0,##CAR(#7)     LOAD VALUE AND REST OF PLIST
         LTR   #15,#15              IF IT WAS AN ATOM
         BM    ERROR2               THEN ERROR
         TM    ##TYPE(#5),##ATOM   IF ARG 1 WAS AN ATOM
         BZ    BASHLIST            THEN
         ST    #0,##PLIST(,#5)      SET PLIST TO REST OF CURRENT PLIST
         BR    #14                 ELSE
BASHLIST ST    #0,##CDR(,#5)        SET CDR TO REST OF CURRENT PLIST
         BR    #14                 RETURN THE OLD PROPERTY VALUE
         SPACE 1
LOOP     DS    0H                  POINTING TO CELL BEFORE PAIR
         TM    ##TYPE(#7),##ATOM   CHECK VALIDITY OF PROPERTY LIST
         BO    ERROR2
         L     #4,##CDR(,#7)       ELSE BUMP TO (SUB)PLIST WITH IND
         CR    #4,#10              IF REACHED END OF PROPERTY LIST
         BE    RETNIL              THEN RETURN NIL
         LM    #1,#2,##CAR(#4)     #1 = CAR, #2 = CDR = VALUE
         LTR   #1,#1               IF CON(#4) WASN'T A CONS CELL
         BM    ERROR2              THEN ERROR
         CR    #6,#1               IF PROPERTY INDICATOR MATCHES
         BNE   NOMATCH             THEN...
         LM    #15,#0,##CAR(#2)     LOAD VALUE AND REST OF PLIST
         LTR   #15,#15              IF CON(#2) WASN'T A CONS CELL
         BM    ERROR2               THEN ERROR
         ST    #0,##CDR(,#7)        SPLICE IND/VALUE PAIR OUT OF PLIST
         BR    #14                  RETURN WITH PROPERTY VALUE REMOVED
NOMATCH  DS    0H                  ELSE...
         LR    #7,#2                BUMP TO NEXT INDICATOR/VALUE PAIR
         B     LOOP                 CONTINUE LOOPING
         SPACE 1
RETNIL   DS    0H
         LR    #15,#10             RETURN NIL
         BR    #14                 RETURN TO CALLER
         SPACE 1
NOTSYM   DS    0H                  Arg 1 not a symbol
         SPACE 1
***********************************************************************
*                                                                     *
* FOR COMPATIBILITY WITH MACLISP AND OTHER APPLICATIONS THAT USE      *
* DISEMBODIED PROPERTY LISTS, IF ARG 1 IS A LIST THEN WE ACCEPT IT.   *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    ##TYPE(#5),##ATOM   See if it's a list
         BO    ERROR1              If so, then...
         L     #7,##CDR(,#5)        get CDR and use as property list
         B     PROCESS
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         #ERR  'Argument 1 to REMPROP not a symbol - ',(#5)
         SPACE 1
ERROR2   DS    0H                  ERROR IN ARGUMENT
         #ERR  'ZIL4011 REMPROP: Invalid property list found for ',    X
               (#5),PREFIX=NO
         SPACE 1
         LTORG
         END
