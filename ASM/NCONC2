         TITLE 'NCONC2 - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* NCONC2: NCONC FOR EXACTLY TWO ARGUMENTS.                            *
*                                                                     *
* THE COMPILER IMPLEMENTS NCONC AS A MACRO THAT EXPANDS INTO THE      *
* REQUISITE CALLS TO NCONC2.                                          *
*                                                                     *
* UNDER THE INTERPRETER, NCONC IS A FUNCTION THAT TAKES AN &REST ARG  *
* AND INVOKES NCONC2.                                                 *
*                                                                     *
***********************************************************************
         SPACE 1
NCONC2   #ZBEG MINARGS=2,MAXARGS=2,NAME='NCONC2'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING NCONC2,#3
         SPACE 1
         LM    #15,#0,0(#1)        LOAD THE ARGS, INITIALIZING RESULT
         SPACE 1
         CR    #15,#10             IF LIST 1 IS NIL,
         BE    ARG1NIL             THEN GO TO RETURN LIST 2. ELSE...
         LR    #2,#15              START PROCESSING SUBLISTS OF ARG 1
         B     GO                  SKIP 1ST CDR - JUMP INTO LOOP
         SPACE 1
LOOP     DS    0H
         L     #2,##CDR(,#2)       CONTINUE GOING DOWN LIST 1
GO       DS    0H
         TM    ##TYPE(#2),##ATOM   IF (SUB)LIST 1 NOT A LIST
         BO    ERROR1              THEN ERROR
         C     #10,##CDR(,#2)      IF CDR OF (SUB)LIST IS NOT NIL
         BNE   LOOP                THEN CONTINUE LOOPING
BASH     DS    0H                  ELSE...
         ST    #0,##CDR(,#2)        BASH CDR OF (SUB)LIST WITH LIST 2
         BR    #14                  AND RETURN THE (BASHED) ARG 1
         SPACE 1
ARG1NIL  DS    0H
         LR    #15,#0               RETURN LIST 2
         BR    #14
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         L     #2,0(,#1)           Reload arg 1 from stack
         #ERR  'Argument 1 to NCONC2 not a proper list - ',(#2)
         SPACE 1
         LTORG
         END
