         TITLE 'PEEKB - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
*  PEEKB TAKES 1 ARGUMENT, A FIXNUM, AND USES IT AS A POINTER INTO    *
*       VIRTUAL STORAGE.  IT RETURNS A FIXNUM WHICH CONTAINS THE      *
*       CONTENTS OF THE BYTE AT THE SPECIFIED LOCATION.               *
*                                                                     *
***********************************************************************
*                                                                     *
* NOTE: THIS MODULE USES 31-BIT ADDRESSING AND REQUIRES H ASSEMBLER.  *
*                                                                     *
***********************************************************************
         GBLB  &ZILXA
         #ZSYS
         SPACE 1
PEEKB    #ZBEG MINARGS=1,MAXARGS=1,NAME='PEEKB'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING PEEKB,#3
         SPACE 1
         L     #1,0(,#1)           GET ARGUMENT 1
         CLI   ##TYPE(#1),##FIXNUM MUST BE A FIXNUM
         BNE   ERROR1              ELSE ERROR
         L     #4,##CDR(,#1)       GET FIXNUM VALUE
         XR    #2,#2               CLEAR INSERT REGISTER
         SPACE 1
         AIF   (NOT &ZILXA).SKIP1
*---------------------------------SWITCH FROM CURRENT AMODE TO AMODE 31
         L     #1,=A(CODE31+X'80000000') ADDRESS OF THIS CODE
         LA    #7,CODE31E          RETURN ADDRESS
         BSM   #7,#1
CODE31   DS    0H
*---------------------------------THE AMODE IS NOW 24
.SKIP1   ANOP
*
* WARNING: PROGRAM CHECK MAY OCCUR HERE...
*
         IC    #2,0(,#4)           GET DATA AT POINTER LOCATION
*
* END OF WARNING
*
         AIF   (NOT &ZILXA).SKIP2
*---------------------------------SWITCH FROM AMODE 31 TO CURRENT AMODE
         BSM   0,#7
CODE31E  DS    0H
*---------------------------------THE AMODE IS NOW SAME AS ON ENTRY
.SKIP2   ANOP
         SPACE 1
         SLA   #2,3                Convert to index into fixnum table
         LA    #15,#SFT(#2)        Load address of fixnum from table
         BR    #14                 Return to caller with fixnum
         SPACE 1
ERROR1   DS    0H
         #ERR  'Argument to PEEKB not a fixnum - ',0(#11)
         SPACE 1
         LTORG
         END
