         TITLE 'MKLIST - ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE MAKES A LIST OF A REQUESTED SIZE.                   *
*                                                                     *
* IT TAKES ONE REQUIRED ARGUMENT - A FIXNUM SPECIFYING THE NUMBER OF  *
* ELEMENTS IN THE LIST - AND ONE OPTIONAL ARGUMENT - THE VALUE TO     *
* INITIALIZE THE CARS TO, DEFAULT BEING NIL.                          *
*                                                                     *
***********************************************************************
*                                                                     *
* Note: This is not quite the same as Common Lisp MAKE-LIST.          *
*       Common Lisp MAKE-LIST has the following syntax:               *
*                                                                     *
*       (make-list <size> &key :initial-element)                      *
*                                                                     *
* Thus, a possible definition of MAKE-LIST could be:                  *
*                                                                     *
* (defun make-list (size &key (initial-element nil))                  *
*  (mklist size initial-element))                                     *
*                                                                     *
* (Naturally, the compiler should transform this into a MKLIST call.) *
*                                                                     *
***********************************************************************
         SPACE 1
MKLIST   #ZBEG MINARGS=1,MAXARGS=2,NAME='MKLIST'
         SPACE 1
         #ZPDS 2*4+12+0
         #ZSAV ,
         SPACE 1
         L     #1,0(,#11)          LOAD ARG1
         CLI   ##TYPE(#1),##FIXNUM IF ARG1 IS NOT A FIXNUM
         BNE   ERROR1              THEN ERROR.  ELSE...
         SPACE 1
         C     #13,4(,#11)         IF ARG 2 NOT SUPPLIED
         BNE   *+8                 THEN
         ST    #10,4(,#11)          SET ARG 2 TO NIL
         SPACE 1
         LR    #15,#10             INITIALIZE NEW LIST TO NIL
         ICM   #6,15,##CDR(#1)     GET VALUE OF FIXNUM INTO REGISTER
         BZ    RETURN              IF ZERO, RETURN WITH NULL LIST
         BM    ERROR2              IF NEGATIVE, ERROR
LOOP     DS    0H                  ELSE...
         LR    #2,#15              GET CONS ARG2 = CURRENT LIST
         L     #15,ZLCCONS         GET ADDRESS OF ZILCONS
         L     #1,4(,#11)          GET CONS ARG1 = VALUE
         #BASR #14,#15             CALL CONS TO CONS VALUE ONTO LIST
         BCT   #6,LOOP             CONTINUE
         SPACE 1
RETURN   DS    0H                  #15 POINTS TO LIST BUILT
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   #ERR  'Argument 1 to MKLIST not a non-negative fixnum - ',    X
               0(#11)
ERROR2   EQU   ERROR1
         SPACE 1
         LTORG
         SPACE 1
         END
