         TITLE 'VFILL - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE FILLS A VECTOR WITH A SPECIFIED ELEMENT.            *
*                                                                     *
* IT TAKES TWO REQUIRED ARGUMENTS - A VECTOR AND A FILL VALUE - AND   *
* INITIALIZES THE SLOTS OF THE VECTOR TO THAT VALUE.                  *
*                                                                     *
***********************************************************************
         SPACE 1
VFILL    #ZBEG MINARGS=2,MAXARGS=2,NAME='VFILL'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING VFILL,#3
         SPACE 1
         LM    #15,#0,0(#1)        LOAD ARGS
         XR    #7,#7               CLEAR INSERT REG
         CLI   ##TYPE(#15),##VECTOR  IF ARG1 IS NOT A VECTOR
         BNE   ERROR1              THEN ERROR.  ELSE...
         ICM   #7,7,1(#15)         GET LENGTH OF VECTOR
         BZR   #14                 IF NULL VECTOR, DO NOTHING
         LA    #4,##VECDAT(,#15)   POINT TO VECTOR ELEMENTS
         LA    #6,4                SET INCREMENT REGISTER FOR BXLE
         XR    #1,#1               SET INDEX REGISTER
         BCTR  #7,0                SET TERMINATION REGISTER FOR BXLE
FILLLOOP ST    #0,0(#1,#4)         INITIALIZE EACH ELEMENT TO ARG2
         BXLE  #1,#6,FILLLOOP
         BR    #14                 #15 POINTS TO VECTOR FILLED
         SPACE 1
ERROR1   DS    0H
         LR    #2,#15
         #ERR  'Argument 1 to VFILL not a vector - ',(#2)
         SPACE 1
         LTORG
         SPACE 1
         END
