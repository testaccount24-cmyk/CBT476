         TITLE 'MINUSP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* MINUSP TAKES ONE ARGUMENT - A NUMERIC ATOM                          *
*                                                                     *
***********************************************************************
MINUSP   #ZBEG MINARGS=1,MAXARGS=1,NAME='MINUSP'
         SPACE
         L     #2,0(,#1)           LOAD FIRST (AND ONLY) ARGUMENT
         CLI   ##TYPE(#2),##FLONUM MUST BE NUMERIC ATOM
         BL    ERROR               ELSE ERROR
         DROP  #15
*
* THE FOLLOWING TEST WORKS FOR FIXNUMS, FLONUMS AND BIGNUMS.
* It works for double floats only because ZILMDF duplicates the left
* half of the flonum when it builds one (and so does the compiler).
* If anyone changes that behavior, this code must be changed.
*
         LR    #15,#10             PRESET RETURN VALUE TO NIL
         TM    ##CDR(#2),X'80'     TEST SIGN BIT OF VALUE
         BZR   #14                 IF ZERO (POSITIVE) RETURN NIL
         LR    #15,#8              IF ONE (NEGATIVE) RETURN T
         BR    #14
         SPACE 1
ERROR    DS    0H                  NOT A NUMERIC ATOM
         LR    #3,#15
         USING MINUSP,#3
         #ERR  'Argument to MINUSP not numeric - ',(#2)
         SPACE 1
         LTORG
         END
