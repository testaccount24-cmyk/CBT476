         TITLE 'ZILRDCH - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ZILRDCH TAKES THREE (OPTIONAL) ARGUMENTS:                           *
*                                                                     *
* (1) INPUT FILE SPECIFICATION                                        *
* (2) WHETHER (T) OR NOT (NIL) TO INTERN THE RESULTING ATOM.          *
* (3) WHETHER (T) OR NOT (NIL) TO FOLD THE CHARACTER TO UPPER CASE.   *
*                                                                     *
***********************************************************************
*                                                                     *
* In ZIL 1.2 this function was called READCH.  The syntax of READCH   *
* has been changed to be compatible with Maclisp READCH (PEEKCH was   *
* changed to ZILPKCH similarly).  Common LISP READ-CHAR and PEEK-CHAR *
* should be used instead of these functions.  Also TYI and TYIPEEK.   *
*                                                                     *
***********************************************************************
         SPACE 1
ZILRDCH  #ZBEG MINARGS=0,MAXARGS=3,NAME='ZILRDCH'
         #ZPDS 24                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         #IFIL 1                   ARG 1 = INPUT FILE SPEC
         SPACE
         TM    ZLFFLAG1,ZLFEOF     IF END OF FILE ALREADY HIT
         BO    RDAFTEOF            THEN ERROR - READ AFTER EOF
         SPACE
         L     #3,ZLFCHAR          GET CURRENT POSITION
GOTINPUT DS    0H
         LTR   #3,#3               CHECK INPUT POINTER
         BP    INPUTOK             IF POSITIVE, WE HAVE GOOD STUFF
         BM    RDNL                IF NEGATIVE, RETURN AS A NEWLINE
******** BZ    RDEOF               IF ZERO, EOF ENCOUNTERED
         SPACE
RDEOF    DS    0H                  EOF HIT ON LAST READ
         OI    ZLFFLAG1,ZLFEOF     INDICATE EOF FOR NEXT CALL
         NI    ZLFFLAG1,255-ZLFNEWLN TURN NEWLINE OFF
         LR    #15,#10             RETURN NIL
         B     RETURN
         SPACE
RDNL     DS    0H                  NEWLINE HIT ON LAST READ
*
* THE FOLLOWING IS TO PREVENT AN EXTRA KEYBOARD UNLOCK WHEN THE
* INPUT FILE IS A TERMINAL AND END-OF-LINE HAS BEEN ENCOUNTERED.
* REMEMBER THAT "READ" IGNORES END-OF-LINE IT IT IS THE FIRST
* THING THAT IT SEES - IF THIS WERE NOT THE CASE, READ WOULD
* HAVE TO BE CHANGED AS WELL.
*
         TM    ZLFFLAG1,ZLFNEWLN   IF WE ALREADY RETURNED NEWLINE/NIL,
         BZ    RDNLNEW1            THEN...
         NI    ZLFFLAG1,255-ZLFNEWLN SET IT OFF AGAIN
         #GETC ,                    READ IN THE NEXT CHARACTER
         B     GOTINPUT             AND TRY TO PROCESS IT
RDNLNEW1 DS    0H                  ELSE...
         OI    ZLFFLAG1,ZLFNEWLN   INDICATE NEWLINE
         LR    #15,#10             RETURN NIL
         B     RETURN
         SPACE
INPUTOK  DS    0H                  REG 3 LOCATES CURRENT CHARACTER
         SPACE
         NI    ZLFFLAG1,255-ZLFNEWLN TURN NEWLINE INDICATOR OFF
         LR    #1,#3               CONVERT RELATIVE COLUMN TO
         BCTR  #1,0                 ABSOLUTE LOCATION IN LINE
         A     #1,ZLFCARD            AND PASS IT TO ATOM BUILD RTE
         C     #10,8(,#11)         IF ARG 3 IS NOT NIL
         BE    DONTFOLD             OR
         C     #13,8(,#11)           ARG 3 IS NOT SUPPLIED
         BE    DONTFOLD               THEN
         TR    0(1,#1),UPTBL        FOLD CHARACTER TO UPPER CASE.
DONTFOLD DS    0H
         ST    #3,ZLFCHAR          SAVE CHARACTER POINTER
         LA    #0,1                LENGTH OF ATOM TO BE BUILT = 1
         L     #2,4(,#11)          GET ARG 2 = WHETHER TO INTERN IT
         LR    #3,#10              SET FORMAT FLAG = NIL
         L     #15,ZLCABLD         GET ADDRESS OF ZILABLD
         LA    #4,24(,#11)         SET CURRENT STACK POINTER FOR CONS
         #BASR #14,#15             CALL ZILABLD TO BUILD THE ATOM
*                                  MAKING #15 POINT TO NEW ATOM
         L     #3,ZLFCHAR          RELOAD CHARACTER POINTER
         #GETC ,                   GET NEXT CHARACTER BEFORE RETURN
         SPACE
RETURN   DS    0H
         ST    #3,ZLFCHAR          RESET CHARACTER POINTER
         SPACE
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE
RDAFTEOF DS    0H                  IF NEGATIVE, EOF WAS ALREADY HIT
         L     #2,ZLFATOM          Load file atom for error message
         #ERR  'ZIL2003 ZILRDCH: Read after end of file ',(#2),        X
               PREFIX=NO
         SPACE
         LTORG
         SPACE
UPTBL    DC    256YL1(*-UPTBL)     TABLE TO FOLD TO UPPER CASE
         ORG   UPTBL+X'81'
         DC    C'ABCDEFGHI'
         ORG   UPTBL+X'91'
         DC    C'JKLMNOPQR'
         ORG   UPTBL+X'A2'
         DC    C'STUVWXYZ'
         ORG
         END
