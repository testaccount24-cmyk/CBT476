         TITLE 'REVAPPEN - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* (REVAPPEND X Y) IS EQUIVALENT TO (APPEND (REVERSE X) Y).            *
*                                                                     *
***********************************************************************
         SPACE 1
REVAPPEN #ZBEG MINARGS=2,MAXARGS=2,NAME='REVAPPEND'
         SPACE 1
         #ZPDS 2*4+12
         #ZSAV ARGS=2
         SPACE 1
         LM    #3,#4,0(#11)        LOAD THE ARGUMENTS
         LR    #15,#4              PRESET RETURN VALUE TO ARG2
         CR    #3,#10              IF ARG1 IS NIL
         BE    RETURN              THEN RETURN ARG2
LOOP     DS    0H                  ELSE...
         ICM   #1,15,##CAR(#3)     GET CAR OF (SUB)LIST
         BM    ERROR1              IF (SUB)LIST WASN'T A CONS, ERROR
         LR    #2,#15              GET RESULT SO FAR
         L     #15,ZLCCONS         CALL CONS TO ADD ITEM TO RESULT LIST
         #BASR #14,#15             (SETS #15 TO RESULT OF CONS)
         L     #3,##CDR(,#3)       GET CDR OF (SUB)LIST
         CR    #3,#10              WHEN (SUB)LIST IS NIL,
******** BE    RETURN              THEN RETURN RESULT
         BNE   LOOP                ELSE CONTINUE
         SPACE 1
RETURN   DS    0H                  #15 CONTAINS RETURN VALUE
         #ZRET ,                   RETURN TO CALLER
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         #ERR  'Argument 1 to REVAPPEND not a proper list - ',0(#11)
         SPACE 1
         LTORG
         END
