         TITLE 'LSH - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* LSH TAKES TWO INTEGER ARGUMENTS.  CURRENTLY THEY MUST BE FIXNUMS.   *
*                                                                     *
* THIS FUNCTION DOES A LOGICAL SHIFT OPERATION.  COMPARE ASH.         *
*                                                                     *
***********************************************************************
LSH      #ZBEG MINARGS=2,MAXARGS=2,NAME='LSH'
         #ZPDS 2*4+12+0 (= 20)     DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         LM    #6,#7,0(#11)        LOAD ARGUMENTS
         CLI   ##TYPE(#6),##FIXNUM
         BNE   ERROR1
         CLI   ##TYPE(#7),##FIXNUM
         BNE   ERROR2
         L     #2,##CDR(,#6)       GET FIXED VALUE 1
         L     #3,##CDR(,#7)       GET FIXED VALUE 2
         LTR   #3,#3               TEST SHIFT AMOUNT
         BZ    NOSHIFT             IF ZERO, NO SHIFTING REQUIRED
         BM    RSHIFT              IF NEGATIVE, RIGHT SHIFT REQUIRED
         SLL   #2,0(#3)            ELSE POSITIVE, DO LEFT SHIFT
         B     NOSHIFT             AND PROCESS
RSHIFT   DS    0H
         LPR   #3,#3               CHANGE SHIFT AMOUNT TO POSITIVE
         SRL   #2,0(#3)            DO RIGHT SHIFT
NOSHIFT  DS    0H                  NOW #2 CONTAINS SHIFTED VALUE
         LA    #0,20(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM ATOM
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ERROR1   #ERR  'Argument 1 to LSH not a fixnum - ',(#6)
ERROR2   #ERR  'Argument 2 to LSH not a fixnum - ',(#7)
         SPACE 1
         LTORG
         END
