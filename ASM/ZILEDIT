***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
         TITLE 'ZILEDIT - MACROS'
         MACRO
&SYM     $SPFC
&SYM     DS    0H
         L     #15,@ISPLINK          GET ADDRESS OF ISPLINK
         OI    ZLCIFLGS,ZLCISYS      INDICATE NON-ZIL CODE RUNNING
         #BASR #14,#15               CALL ISPLINK
         TM    ZLCIFLGS,ZLCIATTN     IF ATTENTION INTERRUPT OCCURRED
         BOR   #13                   THEN GO TO HANDLE IT
         NI    ZLCIFLGS,255-ZLCISYS  INDICATE ZIL CODE RUNNING AGAIN
         MEND
         TITLE 'ZILEDIT - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* THIS FUNCTION INVOKES THE ISPF EDITOR ON A SPECIFIED DATASET.       *
*                                                                     *
* ZILEDIT TAKES ONE ARGUMENT: THE STRING CONTAINING THE NAME OF THE   *
* DATA SET TO BE EDITED.                                              *
*                                                                     *
* RESULT IS BASED ON THE RETURN CODE FROM THE ISPF DIALOG SERVICE.    *
*                                                                     *
*   0 - RETURN T (DATASET WAS SAVED)                                  *
*   4 - RETURN NIL (DATASET WAS NOT SAVED)                            *
*   OTHER - SIGNAL ERROR                                              *
*                                                                     *
***********************************************************************
*                                                                     *
* NOTE THAT THE MAXIMUM LENGTH OF A DSNAME FOR ISPF EDIT IS 56.       *
*                                                                     *
***********************************************************************
         SPACE 1
ZILEDIT  #ZBEG MINARGS=1,MAXARGS=1,NAME='ZILEDIT'
         #ZPDS 1*4+12+0            DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE 1
         MVI   $DSNAME,C' '        CLEAR DSNAME TO BLANKS
         MVC   $DSNAME+1(55),$DSNAME
         L     #2,0(,#11)          LOAD ARG 1 = THE ATOM TO BE DUMPED
         CLI   ##TYPE(#2),##STRING IF IT IS A STRING, OK
         BE    OK
         CLI   ##TYPE(#2),##SYMBOL IF IT IS A SYMBOL - WELL, OK
         BNE   ERROR1              ANYTHING ELSE - BAD DSNAME
         L     #2,##PNAME(,#2)     IF A SYMBOL, GET THE PRINT NAME
OK       DS    0H
         XR    #3,#3               CLEAR INSERT REGISTER
         ICM   #3,7,1(#2)          GET LENGTH OF STRING
         BZ    ERROR1              IF ZERO, BAD DSNAME
         CH    #3,=H'56'           IF GREATER THAN MAXIMUM LENGTH
         BH    ERROR1              THEN BAD DSNAME
         BCTR  #3,0                ELSE REDUCE FOR EXECUTE
         EX    #3,MOVEDSN          MOVE DATA SET NAME TO AREA
         EX    #3,TRDSN            TRANSLATE TO UPPER CASE
*
* GET ISPLINK
*
         LOAD  EPLOC==CL8'ISPLINK',ERRET=ERROR2
         ST    #0,@ISPLINK
*
* SAVE ZIL REGS IN CASE OF ABEND OR OTHER INTERRUPT
*
         ST    #9,ZLCISV09
*
* ISPEXEC CONTROL ERRORS RETURN
*
         LA    #1,ISPCRET          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
         LTR   #15,#15             IF IT FAILED (ISPF SERVICES N/A)
         BZ    CERROK              THEN...
         DELETE EPLOC==CL8'ISPLINK'
         B     ERROR2               BLOW UP
CERROK   DS    0H
*
* ISPEXEC CONTROL DISPLAY REFRESH
*
         LA    #1,ISPCREF          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
*
* ISPEXEC EDIT DATASET(...)
*
         LA    #1,ISPEDIT          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
         LTR   #15,#15             IF RC=0 (DATA SET SAVED)
         BZ    RETT                THEN PREPARE TO RETURN T
         CH    #15,=H'4'           IF RC=4 (DATA SET NOT SAVED)
         BE    RETNIL              THEN PREPARE TO RETURN NIL
*
* ELSE EDIT FAILED - REPORT TO USER
*
*
* ISPEXEC CONTROL ERRORS CANCEL
*
         LA    #1,ISPCCAN          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
*
* ISPEXEC DISPLAY PANEL(ISPTERM)
*
         LA    #1,ISPTERM          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
*
* ISPEXEC CONTROL DISPLAY LINE START(1)
*
         LA    #1,ISPCLIN          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
         SPACE 1
         DELETE EPLOC==CL8'ISPLINK'
         SPACE 1
         B     ERROR3              GO BLOW UP
*
* EVERYTHING OK - RETURN A DECENT RETURN VALUE
*
RETT     DS    0H
         LR    #4,#8               RETURN T
         B     CLEANUP
RETNIL   DS    0H
         LR    #4,#10              RETURN NIL
         B     CLEANUP
*
* CLEAN UP ISPF PROCESSING
*
CLEANUP  DS    0H
*
* ISPEXEC CONTROL DISPLAY LINE START(1)
*
         LA    #1,ISPCLIN          POINT TO PARAMETER LIST FOR ISPLINK
         $SPFC ,                   CALL ISPLINK
*
* RELEASE ISPLINK
*
         DELETE EPLOC==CL8'ISPLINK'
*
*
* RESTORE REGISTERS AND COMMON AREA POINTER
*
RETURN   DS    0H
         LR    #15,#4              LOAD RETURN VALUE
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 3
MOVEDSN  MVC   $DSNAME(*-*),##VECDAT(#2) MOVE STRING TO DSNAME AREA
TRDSN    TR    $DSNAME(*-*),UPTBL  TRANSLATE TO UPPER CASE
         SPACE 3
ERROR1   #ERR  'Argument to ZILEDIT not a valid dataset name - ',0(#11)
ERROR2   #ERR  'ZIL0025 ZILEDIT: ISPF dialog services not available, caX
               nnot edit dataset - ',0(#11),PREFIX=NO
ERROR3   #ERR  'ZIL0026 ZILEDIT: Error detected by ISPF EDIT, could notX
                edit dataset - ',0(#11),PREFIX=NO
         SPACE 3
ISPCRET  DS    0A
         DC    A($CONTROL)
         DC    A($ERRORS)
         DC    A($RETURN+X'80000000')
         SPACE 1
ISPCCAN  DS    0A
         DC    A($CONTROL)
         DC    A($ERRORS)
         DC    A($CANCEL+X'80000000')
         SPACE 1
ISPCREF  DS    0A
         DC    A($CONTROL)
         DC    A($DISPLAY)
         DC    A($REFRESH+X'80000000')
         SPACE 1
ISPCLIN  DS    0A
         DC    A($CONTROL)
         DC    A($DISPLAY)
         DC    A($LINE)
         DC    A($START+X'80000000')
         SPACE 1
ISPEDIT  DS    0A
         DC    A($EDIT)
         DC    A($DSNAME+X'80000000')
         SPACE 1
ISPTERM  DS    0A
         DC    A($DISPLAY)
         DC    A($ISPTERM+X'80000000')
         SPACE 1
@ISPLINK DS    A                   ADDRESS OF ISPLINK (NOT IN SUBRS!)
         SPACE 1
$CONTROL DC    CL8'CONTROL'
$ERRORS  DC    CL8'ERRORS'
$RETURN  DC    CL8'RETURN'
$CANCEL  DC    CL8'CANCEL'
$DISPLAY DC    CL8'DISPLAY'
$ISPTERM DC    CL8'ISPTERM'
$REFRESH DC    CL8'REFRESH'
$LINE    DC    CL8'LINE'
$START   DC    F'1'
$EDIT    DC    CL8'EDIT'
$DSNAME  DC    CL56' '
         SPACE 1
UPTBL    DC    256YL1(*-UPTBL)     TABLE TO TRANSLATE TO UPPER CASE
         ORG   UPTBL+X'00'
         DC    C' '                (ALSO NULLS TO BLANKS)
         ORG   UPTBL+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   UPTBL+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   UPTBL+C'S'-X'40'
         DC    C'STUVWXYZ'
         ORG
         SPACE 1
         LTORG
         END
