***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
         TITLE 'ZIFINIT - MACROS'
         MACRO
&SYM     SETCON &FUN,&FORTFUN=
         LCLC  &FUN2,&REALFUN
&FUN2    SETC  '&FORTFUN'
&REALFUN SETC  '&FUN    '(1,4)
         AIF   ('&FUN2' NE '').OK
&FUN2    SETC  '&FUN'
.OK      ANOP
&SYM     L     0,=V(&FUN2)
         ST    0,ZIF@&REALFUN
         MEND
         TITLE 'ZIFINIT - ZIL 1.3 FORTRAN INITIALIZATION ROUTINE'
         SPACE 1
***********************************************************************
*                                                                     *
*  THIS ROUTINE IS LOADED BY ZILFINT AND BRANCHED TO TO INITIALIZE    *
*  THE FORTRAN INTERFACE.  IT IS LINKEDITED WITH THE APPROPRIATE      *
*  FORTRAN INITIALIZATION MODULE - FOR VS FORTRAN RELEASE 4 AND       *
*  SUBSEQUENT RELEASES, THIS IS VFEIN#.                               *
*                                                                     *
***********************************************************************
*                                                                     *
*  !!!! NOTE !!!!  THIS MODULE MUST BE ASSEMBLED/LINKEDITED WITH      *
*  THE FORTRAN LIBRARY - E.G. SYS1.FORTLIB - NOT NCAL!!!              *
*                                                                     *
***********************************************************************
         SPACE 1
ZIFINIT  CSECT
         SPACE 1
         STM   14,12,12(13)        SAVE REGS
         LR    12,15
         USING ZIFINIT,12
*                                  AT ENTRY #4 ALREADY POINTS TO ZIFSA
         ST    13,4(,4)
         ST    4,8(,13)
         LR    13,4                CHAIN SAVE AREAS TOGETHER
         SPACE 1
         LR    4,1                 REGISTER 1 POINTS TO ZIL INTERFACE
         USING ZIF,4                CONTROL BLOCK - SET POINTER TO IT
******** L     15,FORTVCON
******** #BASR 14,15
         SPACE 1
         SETCON SQRT
         SETCON LOG
         SETCON EXP
         SETCON SIN
         SETCON COS
         SETCON TAN
         SETCON ASIN
         SETCON ACOS
         SETCON ATAN,FORTFUN=ATAN2
         SPACE 1
         SETCON DSQRT
         SETCON DLOG
         SETCON DEXP
         SETCON DSIN
         SETCON DCOS
         SETCON DTAN
         SETCON DASIN
         SETCON DACOS
         SETCON DATAN,FORTFUN=DATAN2
         SPACE 1
         L     13,4(,13)
         LM    14,12,12(13)
         BR    14
         SPACE 1
*ORTVCON DC    V(VFEIN#)           !!! THIS IS THE FORTRAN INIT RTE !!!
         SPACE 1
         LTORG
         SPACE 1
         #ZIF  ,                   MAP THE ZIL INTERFACE CONTROL BLOCK
         END
