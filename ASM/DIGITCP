         TITLE 'DIGITCP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ALIAS "DIGIT-CHAR-P".                                               *
*                                                                     *
***********************************************************************
         SPACE
DIGITCP  #ZBEG MINARGS=1,MAXARGS=2,NAME='DIGIT-CHAR-P'
         LR    #7,#15
         DROP  #15
         USING DIGITCP,#7
         SPACE
         LM    #3,#4,0(#1)         GET ARGS 1 AND 2
         CLI   ##TYPE(#3),##FIXNUM MUST BE A FIXNUM
         BNE   ERROR1              OR ELSE ERROR
         CR    #4,#13              IF OPTIONAL ARG 2 IS OMITTED
         BNE   ARG2SPEC            THEN
         LA    #5,10                SET RADIX TO 10
         B     RADIXSET            ELSE
ARG2SPEC CLI   ##TYPE(#4),##FIXNUM  ARG 2 MUST BE A FIXNUM
         BNE   ERROR2               ELSE ERROR
         L     #5,##CDR(,#4)        GET VALUE OF ARG 2
         CH    #5,=H'2'             CHECK RANGE OF RADIX
         BL    ERROR2
         CH    #5,=H'36'
         BH    ERROR2
RADIXSET DS    0H                  #5 = RADIX
*
* GET THE NUMERIC VALUE OF THE FIXNUM.
*
         L     #1,##CDR(,#3)       GET FIXNUM CONTENTS
         CL    #1,#SFW255          MUST BE 1-BYTE CHARACTER FIX
         BH    FALSE               ELSE NOT NUMERIC CHARACTER
         XR    #2,#2
         IC    #2,TABLE(#1)        IF IN RANGE, LOAD WEIGHT FROM TABLE
         CR    #2,#5               IF WEIGHT OK WITHIN RADIX
         BNL   FALSE               THEN...
TRUE     DS    0H
         SLA   #2,3                CONVERT WEIGHT TO INDEX
         LA    #15,#SFT(#2)        LOAD FIXNUM FROM TABLE
         BR    #14                 AND RETURN IT
         SPACE 1
FALSE    LR    #15,#10             NOT A DIGIT - RETURN NIL
         BR    #14                 AND RETURN IT
         SPACE 1
TABLE    DC    256YL1(255)         CHARACTERS INVALID FOR ALL RADICES
*                                  EXCEPT...
         ORG   TABLE+C'0'
         DC    YL1(0,1,2,3,4,5,6,7,8,9)
         ORG   TABLE+C'A'-X'40'
         DC    YL1(10,11,12,13,14,15,16,17,18)
         ORG   TABLE+C'J'-X'40'
         DC    YL1(19,20,21,22,23,24,25,26,27)
         ORG   TABLE+C'S'-X'40'
         DC    YL1(28,29,30,31,32,33,34,35)
         ORG   TABLE+C'A'
         DC    YL1(10,11,12,13,14,15,16,17,18)
         ORG   TABLE+C'J'
         DC    YL1(19,20,21,22,23,24,25,26,27)
         ORG   TABLE+C'S'
         DC    YL1(28,29,30,31,32,33,34,35)
         ORG
         SPACE 1
ERROR1   #ERR  'Argument 1 to DIGIT-CHAR-P not a fixnum - ',(#3)
         SPACE 1
ERROR2   #ERR  'Argument 2 to DIGIT-CHAR-P not a valid radix - ',(#4), X
               ' (must be a fixnum between 2 and 36)'
         SPACE 1
         LTORG
         END
