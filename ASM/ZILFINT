         TITLE 'ZILFINT - ZIL 1.3 FORTRAN INTERFACE SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*                                                                     *
*  THIS ROUTINE, LINKEDITED WITH ZILSUBS AND ACCESSIBLE VIA THE       *
*  ZLCFINT POINTER IN THE ZIL COMMON AREA, EFFECTS THE FORTRAN        *
*  INTERFACE.  THIS ROUTINE IS CALLED THE FIRST TIME ANY ZIL          *
*  FUNCTION THAT REQUIRES THE FORTRAN INTERFACE GETS EXECUTED,        *
*  E.G. SQRT.  IT SHOULD NOT BE CALLED MORE THAN ONCE PER SUCH        *
*  FUNCTION, BUT MAY BE CALLED FOR SEVERAL FUNCTIONS.                 *
*                                                                     *
*  THE FIRST TIME THIS ROUTINE IS CALLED BY ANYBODY, IT SETS UP       *
*  THE FORTRAN INTERFACE BY ALLOCATING THE FORTRAN INTERFACE          *
*  CONTROL BLOCK AND PLACING ITS ADDRESS IN THE ZIL COMMON AREA.      *
*                                                                     *
*  ON ENTRY TO THIS ROUTINE:                                          *
*                                                                     *
*  #1  CONTAINS ZERO IF THIS ROUTINE IS BEING CALLED TO TERMINATE     *
*      THE FORTRAN INTERFACE;                                         *
*  #1  NONZERO IF A FORTRAN SUBROUTINE IS TO BE LOADED                *
*                                                                     *
*  ON EXIT FROM THIS ROUTINE:                                         *
*                                                                     *
*  #15 CONTAINS THE ADDRESS OF THE FORTRAN INTERFACE CONTROL BLOCK.   *
*                                                                     *
*  SAVE AREA CONVENTIONS:                                             *
*                                                                     *
*  THIS ROUTINE SETS UP A SPECIAL SAVE AREA FOR FORTRAN CALLS.        *
*  FUNCTIONS THAT CALL FORTRAN CODE SHOULD SET REGISTER 13 TO         *
*  POINT TO THIS SAVE AREA BY DOING THE FOLLOWING:                    *
*                                                                     *
*       ST   #9,ZLCISV09            SAVE CONS POINTER IN CASE OF ATTN *
*       OI   ZLCIFLGS,ZLCISYS       INDICATE NON-ZIL CODE RUNNING     *
*       L    #13,ZLCFTCBA           POINT TO FORTRAN SAVE AREA        *
*                                                                     *
*  SINCE THE FIRST 18 WORDS OF THE FORTRAN INTERFACE CONTROL BLOCK    *
*  ARE THE SAVE AREA FOR USE BY FORTRAN CODE.  TO RESTORE #13 AFTER   *
*  THE CALL, DO:                                                      *
*                                                                     *
*       L    #13,4(,#13)            POINT BACK TO ZIL COMMON AREA     *
*       NI   ZLCIFLGS,255-ZLCISYS   TURN OFF NON-ZIL-CODE FLAG        *
*       TM   ZLCIFLGS,ZLCIATTN      IF ATTENTION INTERRUPT OCCURRED   *
*       BOR  #13                    THEN GO TO HANDLE IT              *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
ZILFINT  #ZBEG NAME='ZILFINT'
         DROP  #15
         STM   #12,#7,SAVEREGS-ZILFINT(#15) SAVE REGS IN OUR OWN PLACE
         LR    #12,#15
         USING ZILFINT,#12
         SPACE 1
         LTR   #3,#1               SAVE INPUT PARAMETER
         BZ    CLEANUP             IF ZERO, IT'S A TERMINATION REQUEST
         SPACE 1
         L     #4,ZLCFTCBA         GET ADDRESS OF FORTRAN INTERFACE CB
         LTR   #4,#4               IF WE AIN'T GOT ONE YET,
         BNZ   GOTIT               THEN...
         #GETMAIN RC,LV=ZIFLEN,LOC=BELOW GET ONE
         LTR   #15,#15              IF WE COULDN'T GET ONE
         BNZ   ERRORG               THEN BARF
         ST    #1,ZLCFTCBA          STORE ITS ADDRESS
         LR    #4,#1                ESTABLISH ADDRESSABILITY
         USING ZIF,#4
*
* FILL IN FORTRAN INTERFACE CONTROL BLOCK FIELDS
*
         LOAD  EP=ZIFINIT,ERRET=ERRORI LOAD FORTRAN INTERFACE PROCESSOR
         LR    #5,#0               SAVE ENTRY POINT
         ST    #13,ZIFSA+4         SET HSA POINTER IN FTCB SAVE AREA
         SPACE 1
         ST    #13,ZIFSA+4         SET HSA POINTER IN FTCB SAVE AREA
         SPACE 1
         LR    #15,#5              GET ADDRESS OF ZIFINIT
         LR    #1,#4               POINT TO ZIF AREA
         OI    ZLCIFLGS,ZLCISMC    CAN'T INTERRUPT WHILE INIT'ING FTN
         #BASR #14,#15             CALL ZIFINIT TO INITIALIZE FTN ENV
         NI    ZLCIFLGS,255-ZLCISMC
         TM    ZLCIFLGS,ZLCIATTN   If attention interrupt occurred
         BOR   #13                 then go to handle it
         SPACE 1
GOTIT    DS    0H
         SPACE 1
         ST    #4,SAVEREGS+12      SAVE IN WHAT WILL BE RETURNED #15
         SPACE 1
RETURN   DS    0H
         LM    #12,#7,SAVEREGS     RESTORE REGS
         BR    #14                 RETURN TO CALLER
         SPACE 1
CLEANUP  DS    0H
         ICM   #4,15,ZLCFTCBA      GET FTCB ADDRESS
         BZ    NOCLN               IF NONE, NO CLEANUP TO DO
         DELETE EP=ZIFINIT
         LR    #1,#4
         LA    #0,ZIFLEN
         FREEMAIN R,LV=(0),A=(1)   FREE THE CONTROL BLOCK
NOCLN    DS    0H
         LM    #12,#7,SAVEREGS     RESTORE REGS (RETURN VALUE IRRELEV.)
         BR    #14                 RETURN TO CALLER
         SPACE 2
SAVEREGS DS    12A                 REGISTER SAVE AREA
         SPACE 2
ERRORG   #ERR  PREFIX=NO,                                              X
               'ZIL0060 ZILFINT: No storage available for FORTRAN interX
               face.'
         SPACE 1
ERRORI   #ERR  PREFIX=NO,                                              X
               'ZIL0061 ZILFINT: FORTRAN interface initialization faileX
               d, module ZIFINIT could not be loaded.'
         SPACE 1
         LTORG
         EJECT
         #ZIF  ,                   FORTRAN INTERFACE CONTROL BLOCK
         EJECT
         END
