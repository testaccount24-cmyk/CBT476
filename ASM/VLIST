         TITLE 'VLIST - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
* VLIST TAKES 1 ARGUMENT - A VECTOR TO CONVERT TO A LIST              *
***********************************************************************
         SPACE
VLIST    #ZBEG MINARGS=1,MAXARGS=1,NAME='VLIST'
         #ZPDS 20                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
*
* CHECK INPUT ARGUMENT - MUST BE A VECTOR-TYPE OBJECT
*
         L     #6,0(,#11)          GET ARG 1
         TM    ##TYPE(#6),##VEC    MUST BE A VECTOR
         BNO   ERROR1              OR ELSE
*
* FOR EACH WORD IN THE VECTOR, ADD IT TO THE LIST.
* WE GO BACKWARDS TO FACILITATE CONSING THEM TOGETHER TO MAKE A LIST.
*
         XR    #3,#3               CLEAR INSERT REGISTER
         ST    #10,16(,#11)        INITIALIZE OUTPUT LIST TO NIL
         ICM   #3,7,1(#6)          GET LENGTH OF VECTOR DATA
         BZ    RETLIST             IF ZERO, RETURN NIL
         LA    #5,##VECDAT-4(,#3)  INDEX TO LAST WORD IN VECTOR
         LA    #0,20(,#11)         SET STACK POINTER FOR CONS
         LA    #4,4
LOOP     DS    0H
         L     #6,0(,#11)          RELOAD VECTOR ARG FROM STACK
         L     #1,0(#5,#6)         LOAD VECTOR ELEMENT
         L     #15,ZLCCONS         GET ADDRESS OF ZILCONS
         L     #2,16(,#11)         ARG 2 TO CONS = LIST SO FAR
         #BASR #14,#15             CALL CONS TO ADD NEW ATOM TO LIST
         ST    #15,16(,#11)        (SETQ LIST (CONS ELEMENT LIST))
         SR    #5,#4
         BNZ   LOOP                CONTINUE UNTIL HIT START OF VECTOR
RETLIST  DS    0H
         L     #15,16(,#11)        RETURN THE LIST
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ERROR1   DS    0H
         #ERR  'Argument to VLIST not a vector - ',0(#11)
         SPACE 1
         LTORG
         END
