         TITLE 'PLUSP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* PLUSP TAKES ONE ARGUMENT - A NUMERIC ATOM                           *
*                                                                     *
***********************************************************************
PLUSP    #ZBEG MINARGS=1,MAXARGS=1,NAME='PLUSP'
         SPACE 1
         L     #2,0(,#1)           LOAD FIRST (AND ONLY) ARGUMENT
         CLI   ##TYPE(#2),##FLONUM MUST BE NUMERIC ATOM
         BL    ERROR               ELSE ERROR
*
* THE FOLLOWING TEST WORKS FOR FIXNUMS AND FLONUMS.
* It works for double floats only because ZILMDF duplicates the left
* half of the flonum when it builds one (and so does the compiler).
* If anyone changes that behavior, this code must be changed.
*
         ICM   #0,15,##CDR(#2)     LOAD THE VALUE (WORKS FOR FIX&FLO)
         BP    TRUE                IF VALUE POSITIVE, RETURN T
         BM    FALSE               IF VALUE NEGATIVE, RETURN NIL
*                                  IF VALUE ZERO, SEE IF BIGNUM
         CLI   ##TYPE(#2),##BIGNUM IF BIGNUM AND NOT NEGATIVE
         BE    TRUE                THEN MUST BE POSITIVE, RETURN T
******** B     FALSE               ELSE IS ZERO, RETURN NIL
FALSE    LR    #15,#10             RETURN NIL
         BR    #14
TRUE     LR    #15,#8              RETURN T
         BR    #14
         SPACE 1
ERROR    DS    0H                  NOT A NUMERIC ATOM
         LR    #3,#15
         DROP  #15
         USING PLUSP,#3
         #ERR  'Argument to PLUSP not numeric - ',(#2)
         SPACE 1
         LTORG
         END
