         TITLE 'MKFIXNUM - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*
* MKFIXNUM TAKES A FLONUM AND MAKES A FIXNUM OUT OF IT.
* THE NUMBER IS *NOT* CONVERTED; RATHER, THE CONTENTS OF THE FLONUM
* ARE STUCK INTO THE FIXNUM PART.  THIS FUNCTION IS USED, AMONG OTHER
* THINGS, TO HELP THE COMPILER COMPILE FLONUMS MORE ACCURATELY.
*
* If the argument is a single float, a single fixnum is returned.
* If the argument is a double float, a CONS (pair) of fixnums is
* returned.
*
***********************************************************************
MKFIXNUM #ZBEG MINARGS=1,MAXARGS=1,NAME='MAKE-FIXNUM'
         #ZPDS 1*4+12+0 (= 16)     DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         L     #1,0(,#11)          LOAD ADDRESS OF ARG 1
         CLI   ##TYPE(#1),##SFLOAT If arg 1 is a single-float
         BE    MKSF                then process single-float
         CLI   ##TYPE(#1),##DFLOAT If arg 1 is a double-float
         BE    MKDF                then process double-float
         B     ERROR               Else error
         SPACE 1
MKSF     DS    0H                  Single-float:  make one fixnum
         SPACE 1
         L     #2,##CDR(,#1)       Get flonum into GPR as fixnum
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM
         B     RETURN
         SPACE 1
MKDF     DS    0H                  Double-float:  make pair of fixnums
         SPACE 1
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         LM    #3,#4,8(#1)         Get both halves of double flonum
         LR    #2,#3               Get first half
         #MKAT FIXED               Make a fixnum
         ST    #15,0(,#11)         Save first fixnum
         LR    #2,#4               Get second half
         #MKAT FIXED               Make another fixnum
         L     #1,0(,#11)          Get first fixnum
         LR    #2,#15              Get second fixnum
         L     #15,ZLCCONS         Get address of CONS
         #BASR #14,#15             Call CONS to make a pair
         B     RETURN
         SPACE 1
RETURN   DS    0H
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE
ERROR    DS    0H
         #ERR  'Argument to MAKE-FIXNUM not a flonum - ',0(#11)
         SPACE 1
         END
