         TITLE 'CHARDOWN - BUILT-IN ZIL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* CHARDOWN TAKES ONE ARGUMENT - A FIXNUM.                             *
* THE FIXNUM IS INTERPRETED AS A CHARACTER TO BE CONVERTED TO ITS     *
* LOWER CASE EQUIVALENT.                                              *
*                                                                     *
* ALIAS "CHAR-DOWNCASE".                                              *
*                                                                     *
***********************************************************************
         SPACE 1
CHARDOWN #ZBEG MINARGS=1,MAXARGS=1,NAME='CHAR-DOWNCASE'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING CHARDOWN,#3
         SPACE 1
         L     #15,0(,#1)          GET ATOM (ARG 1 TO CHARDOWN)
         CLI   ##TYPE(#15),##FIXNUM MUST BE A FIXNUM
         BNE   ERROR1              OR ELSE
         L     #4,##CDR(,#15)      GET FIXNUM CONTENTS
         CL    #4,#SFW255          IF NOT 1-BYTE CHARACTER FIXNUM
         BHR   #14                  THEN JUST RETURN IT AS IS
         XR    #2,#2
         IC    #2,TABLE(#4)        IF IN RANGE, LOAD CHAR FROM TABLE
         CR    #2,#4               IF THE SAME
         BER   #14                 THEN JUST RETURN
         SLA   #2,3                Else convert number to table index
         LA    #15,#SFT(#2)        Point to fixnum in fixnum table
         BR    #14                  and return with it
         SPACE 1
TABLE    DC    256YL1(*-TABLE)     ALL CHARS TRANSLATE TO SELVES
         ORG   TABLE+C'A'          EXCEPT UPPER CASE TO LOWER CASE
         DC    X'818283848586878889'
         ORG   TABLE+C'J'
         DC    X'919293949596979899'
         ORG   TABLE+C'S'
         DC    X'A2A3A4A5A6A7A8A9'
         ORG
         SPACE 1
ERROR1   #ERR  'Argument to CHAR-DOWNCASE not a fixnum - ',0(#11)
         SPACE 1
         LTORG
         END
