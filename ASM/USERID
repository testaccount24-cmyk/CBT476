         TITLE 'USERID - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
*  USERID RETURNS THE TSO USERID, OR NIL IF NON-TSO ENVIRONMENT.      *
*                                                                     *
***********************************************************************
*                                                                     *
* NOTE: THIS ROUTINE NEED NOT CONS UP A NEW STRING, SINCE THE         *
*       USERID IS THE SAME THROUGHOUT THE ENTIRE ZIL SESSION.         *
*                                                                     *
***********************************************************************
         SPACE 1
USERID   #ZBEG MINARGS=0,MAXARGS=0,NAME='USERID'
         SPACE 1
         TM    FLAG,X'80'          IF USERID WAS ALREADY SET
         BO    RETSTR              THEN RETURN IT
         L     #3,X'21C'           GET TCB ADDRESS
         L     #3,X'B4'(,#3)       GET JSCB ADDRESS
         L     #3,X'15C'(,#3)      GET ACTIVE JSCB ADDRESS
         ICM   #3,15,X'108'(#3)    GET PSCB ADDRESS
         BZ    RETNIL              IF NONE, NON-TSO, RETURN NIL
         XR    #2,#2               CLEAR LENGTH REGISTER
         ICM   #2,1,7(#3)          GET LENGTH OF PSCB USERID
         STCM  #2,7,LENGTH         STORE INTO OUR STRING
         BZ    RETSTR              IF ZERO, BYPASS DATA MOVE
         BCTR  #2,0                REDUCE FOR EXECUTE
         EX    #2,MOVEIT           MOVE DATA FROM PSCB TO OUR STRING
         OI    FLAG,X'80'          INDICATE USERID SET
RETSTR   DS    0H
         LA    #15,STRING
         BR    #14
RETNIL   DS    0H
         LR    #15,#10
         BR    #14
         SPACE 1
MOVEIT   MVC   USERVAL(*-*),0(#3)  EXECUTED: MOVE PSCBUSER TO STRING
         SPACE 1
STRING   DS    0A
         DC    YL1(##STRING)
LENGTH   DC    AL3(0)
USERVAL  DC    XL8'00'
         SPACE 1
FLAG     DC    X'00'
         SPACE 1
         LTORG
         END
