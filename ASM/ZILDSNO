         TITLE 'ZILDSNO - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* (ZILDSNO outputfilespec) returns the actual data set name to which  *
* the output file spec outputfilespec is allocated. If outputfilespec *
* is the terminal, * is returned.                                     *
*                                                                     *
***********************************************************************
         SPACE
ZILDSNO  #ZBEG MINARGS=1,MAXARGS=1,NAME='ZILDSNO'
         #ZPDS 16                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE 1
         #OFIL 1                   ARG 1 = OUTPUT FILE SPEC
         SPACE 1
         USING ZLFILBLK,#7
         TM    ZLFFLAG1,ZLFTERM    If this is a terminal file
         BO    RETTERM             then return "*" for dsname
         TM    ZLFDCB+X'30',X'10'  If DCB is not open
         BNO   NOTOPEN             then error - this shouldn't happen
         XR    #3,#3               Clear register for insert
         ICM   #3,B'0011',ZLFDCB+X'28'  Get TIOT offset
         L     #15,540             Get TCB address
         L     #15,12(,#15)        Get TIOT address
         ALR   #3,#15              Add TIOT offset
         USING TIOENTRY,#3         Address the TIOT entry
         TM    TIOELINK,TIOTTERM   If this is a terminal
         BO    RETTERM             then return "*" for terminal
*
* The following will not work if SWA is above the line in an
* MVS/SP2.2 system.
*
         XR    #5,#5               Prepare register for 24-bit address
         ICM   #5,7,TIOEJFCB       Get address of JFCB
         LA    #5,16(,#5)          Point past SWA prefix
         MVI   DSNAME,C' '         Clear dsname(member) to blanks
         MVC   DSNAME+1(L'DSNAME-1),DSNAME
         MVC   DSNAME(44),0(#5)    Copy data set name
         CLI   DSNAME,C' '         If it is all blank
         BE    RETNULL             (very unlikely, but) return ""
         LA    #1,DSNAME+44-1      Search for last nonblank
DSNLOOP  CLI   0(#1),C' '
         BNE   DSNLEND
         BCT   #1,DSNLOOP
DSNLEND  DS    0H                  #1 -> last nonblank in DSN
         LA    #2,1(,#1)           Point where to put possible member
         CLI   44(#5),C' '         If there is a member name
         BE    NOMEMBER            then...
         MVI   0(#2),C'('           put a left paren there
         MVC   1(8,#2),44(#5)       move member name in
         LA    #1,8-1(,#2)          Search for last nonblank
MEMLOOP  CLI   0(#1),C' '
         BNE   MEMLEND
         BCT   #1,MEMLOOP
MEMLEND  DS    0H                   #1 -> last nonblank in DSN
         MVI   1(#1),C')'           put a right paren there
         LA    #2,2(,#1)           Point to end of dsname(member)
NOMEMBER DS    0H                  #2 -> end of name
         LA    #0,DSNAME
         SR    #2,#0               Length of string
         XR    #1,#1               Set ZILVSS entry code for new space
         L     #15,ZLCVSS          Get address of ZILVSS
         LA    #0,16(,#11)         Set current stack pointer
         #BASR #14,#15             Call ZILVSS to create a string
         LTR   #15,#15             If string build failed
         BZ    ERROR               then error
         LH    #2,2(,#15)          Get length (from built string)
         BCTR  #2,0                Reduce for execute
         EX    #2,MOVEIT           Move dsname to our string
         B     RETURN              Return with string
         SPACE 1
MOVEIT   MVC   ##VECDAT(*-*,#15),DSNAME Executed: move data to string
         SPACE 1
RETNULL  DS    0H
         LA    #15,NULLSTR
         B     RETURN
RETTERM  DS    0H
         LA    #15,STAR
         B     RETURN
         SPACE
RETURN   DS    0H
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
DSNAME   DS    CL(44+8+1+1)        Space to build dsname(member)
NULLSTR  #SATM ''                  String ""
STAR     #SATM '*'                 String "*"
         SPACE 1
NOTOPEN  DS    0H
         L     #2,ZLFATOM
         #ERR  'ZILDSNO: Output file is not open - ',(#2)
         SPACE 1
ERROR    #ERR  PREFIX=NO,'ZIL4010 ZILDSNO: Not enough vector/string spaX
               ce to build string.'
         SPACE 1
         LTORG
         SPACE 1
DUMDEDUM DSECT ,
         IEFTIOT1
         SPACE 1
         END
