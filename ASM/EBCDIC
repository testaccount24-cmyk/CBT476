         TITLE 'EBCDIC - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* EBCDIC TAKES TWO ARGUMENTS, OF WHICH THE SECOND IS OPTIONAL.        *
* THE FIRST IS A FIXNUM TO BE CONVERTED TO A ONE-CHARACTER SYMBOL.    *
* THE SECOND SPECIFIES WHETHER (NON-NIL) OR NOT (NIL) TO INTERN THE   *
*  GENERATED SYMBOL ON THE OBJECT LIST (NOTE DEFAULT IS NOT TO).      *
* THIS ROUTINE IS NOT RECURSIVE AND USES NO EXTRA STACK SPACE.        *
*                                                                     *
* COMPARE MACLISP "ASCII".                                            *
*                                                                     *
***********************************************************************
         SPACE
EBCDIC   #ZBEG MINARGS=1,MAXARGS=2,NAME='EBCDIC'
         #ZPDS 2*4+12+0 = 20       DEFINE STACK SIZE
         #ZSAV ARGS=2              SAVE REGISTERS
         SPACE
         L     #2,0(,#11)          GET ATOM (ARG 1 TO EBCDIC)
         CLI   ##TYPE(#2),##FIXNUM MUST BE AN ATOM
         BNE   ERROR1              OR ELSE
*
* GET THE NUMERIC VALUE OF THE FIXNUM.
*
         L     #15,##CDR(,#2)      GET FIXNUM CONTENTS
         CL    #15,#SFW255         MUST BE 1-BYTE CHARACTER FIXNUM
         BNH   CHAROK
         #ERR  1,'Invalid argument to EBCDIC, substituting null - ',   X
               (#2)
         XR    #15,#15             IF BAD, CHANGE TO NULL
CHAROK   DS    0H
         STC   #15,EBWORK          STORE INTO WORK AREA
*
* BUILD A ONE-CHARACTER ATOM FROM IT.
*
         LA    #0,1                SET LENGTH = 1
         LA    #1,EBWORK           POINT TO CHARACTER TO BE ATOMIZED
         L     #2,4(,#11)          SET INTERN FLAG = ARG 2 TO EBCDIC
         LR    #3,#10              SET FORMAT FLAG = NIL
         L     #15,ZLCABLD         GET ADDRESS OF ZILABLD
         LA    #4,20(,#11)         SET CURRENT STACK POINTER FOR CONS
         #BASR #14,#15             CALL ZILABLD TO MAKE AN ATOM FROM IT
*                                  (RETURNS ATOM IN #15)
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ERROR1   DS    0H
         #ERR  'Argument to EBCDIC not a fixnum - ',(#2)
         SPACE 1
EBWORK   DS    C
         SPACE 1
         LTORG
         END
