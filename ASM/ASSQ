         TITLE 'ASSQ - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ASSQ: SAME AS COMMON LISP (ASSOC A B :TEST #'EQ)                    *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND USES NO STACK SPACE.               *
*                                                                     *
***********************************************************************
*                                                                     *
* NOTE: THERE USED TO BE CODE TO CHECK FOR THE PRESENCE OF NIL IN     *
*       THE MIDDLE OF THE ALIST.  THIS CODE HAS BEEN TAKEN OUT,       *
*       SINCE WHEN IT IS REMOVED IT HAS THE SAME EFFECT ON CORRECT    *
*       PROGRAMS.  TAKING THE "CAR" OF NIL RESULTS IN A POINTER THAT  *
*       CANNOT BE EQ TO ANYTHING ELSE (EXCEPT THE CAR OF NIL, THAT    *
*       IS, WHICH WON'T OCCUR IN A CORRECT PROGRAM), SO THE SEARCH    *
*       ALWAYS PROCEEDS PAST NULL ELEMENTS.                           *
*       WE'VE NEVER CHECKED FOR ATOMIC SUBLISTS ANYHOW.               *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
* Change activity:                                                    *
*                                                                     *
* 03/30/88 - Error checking added, now that ASSQ gets compiled inline.*
*            Note that the checking is rather minimal.                *
*                                                                     *
***********************************************************************
         SPACE 1
ASSQ     #ZBEG MINARGS=2,MAXARGS=2,NAME='ASSQ'
         SPACE 1
         LM    #2,#3,0(#1)         LOAD ARG 1 (COMPARAND)
         LR    #4,#3               LOAD ARG 2 (ALIST)
LOOP     DS    0H
         CR    #4,#10              COMPARE (SUB)ALIST TO NIL
         LM    #3,#4,##CAR(#4)     #2 = SUBLISTS'S CAR, #3 = ITS CDR
         BE    NOTFOUND            IF SUBLIST WAS NIL, RETURN NIL
         LTR   #3,#3               If (sub)alist was an atom
         BM    ERROR1              then error
         C     #2,##CAR(,#3)       IF ARG 1 NOT EQ ITS CAR'S CAR
         BNE   LOOP                THEN CONTINUE WITH SUBALIST'S CDR
         DROP  #15
         LR    #15,#3              ELSE RETURN WITH SUBALIST'S CAR
         BR    #14
NOTFOUND DS    0H
         LR    #15,#10             NOT FOUND IN ALIST, RETURN NIL
         BR    #14
ERROR1   LR    #3,#15
         USING ASSQ,#3
         L     #4,4(,#1)
         #ERR  'Argument 2 to ASSQ not a proper list - ',(#4)
         SPACE 1
         LTORG
         END
