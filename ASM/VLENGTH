         TITLE 'VLENGTH - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* VLENGTH TAKES ONE ARGUMENT - A VECTOR-TYPE OBJECT.                  *
*                                                                     *
***********************************************************************
VLENGTH  #ZBEG MINARGS=1,MAXARGS=1,NAME='VLENGTH'
         #ZPDS 16                  DEFINE STACK SIZE
         #ZSAV ARGS=1              SAVE REGISTERS
         SPACE
         L     #1,0(,#11)          LOAD FIRST (AND ONLY) ARGUMENT
         XR    #2,#2               CLEAR INSERT REGISTER
         TM    ##TYPE(#1),##VEC    IF ARGUMENT IS NOT A VECTOR
         BNO   ERROR1              THEN ERROR
         ICM   #2,7,1(#1)          GET LENGTH OF VECTOR TEXT
         SRA   #2,2                DIVIDE BY 4 TO GET # OF ELEMENTS
         CL    #2,ZLCSFTHI         If within small fixnum range
         BNH   SMALLFIX            then go to return fixnum from table
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXED ATOM
         B     RETURN
SMALLFIX DS    0H                  Return small fixnum from table
         SLA   #2,3                Convert to index into fixnum table
         LA    #15,#SFT(#2)        Return small fixnum from table
RETURN   DS    0H                  RETURN WITH NEW ATOM
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ERROR1   DS    0H                  INVALID ARGUMENT
         #ERR  'Argument to VLENGTH not a vector - ',0(#11)
         SPACE 1
         LTORG
         END
