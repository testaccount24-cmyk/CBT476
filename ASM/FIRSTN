         TITLE 'FIRSTN - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* (FIRSTN 3 '(A B C D E))  ==> (A B C)                                *
*                                                                     *
***********************************************************************
*                                                                     *
* Note: This implementation of FIRSTN exists for Macsyma only.  It    *
*       does not work for negative arguments, and also does not work  *
*       exactly as the LISPM version does, in that it does not pad    *
*       the resultant list out with NILs.  It works the way the       *
*       PDP10/Franz definition in Macsyma's UTILS file does.          *
*                                                                     *
***********************************************************************
         SPACE 1
FIRSTN   #ZBEG MINARGS=2,MAXARGS=2,NAME='FIRSTN'
         SPACE 1
         #ZPDS 2*4+12+4
         #ZSAV ARGS=2
         SPACE 1
         LM    #3,#4,0(#11)        LOAD THE ARGUMENTS
         SPACE 1
         CLI   ##TYPE(#3),##FIXNUM ARG 1 MUST BE A FIXNUM
         BNE   ERROR1              ELSE ERROR
         L     #3,##CDR(,#3)       LOAD FIXNUM VALUE OF ARG 1
         SPACE 1
         CR    #4,#10              IF LIST IS NIL,
         BE    RETNIL              THEN RETURN NIL
         LTR   #3,#3               CHECK SIGN OF COUNT
         BZ    RETNIL              IF ZERO, RETURN NIL
         BM    ERROR1              IF NEGATIVE, WELL...
ARG1NN   DS    0H                  IF POSITIVE, PROCEED
         LR    #6,#10              INITIALIZE RESULT TO NIL
         LA    #0,24(,#11)         SET CURRENT STACK POINTER FOR CONS
         SPACE 1
LOOP     DS    0H
         TM    ##TYPE(#4),##ATOM   IF (SUB)LIST NOT A LIST
         BO    ERROR2              THEN ERROR
INLOOP   DS    0H
         L     #1,##CAR(,#4)       GET CAR OF (SUB)LIST
         LR    #2,#10              GET NIL
         L     #15,ZLCCONS         CALL CONS TO BUILD A PIECE OF THE
         #BASR #14,#15              RESULT LIST
         CR    #6,#10              IF NO RESULT YET,
         BNE   BASHEND             THEN...
         LR    #6,#15               START IT NOW FROM THIS PIECE
         ST    #6,20(,#11)          SAVE ON STACK FOR POSSIBLE GC'ING
         B     AFTBASH             ELSE...
BASHEND  ST    #15,##CDR(,#5)       BASH LAST CONS OF RESULT LIST
AFTBASH  LR    #5,#15              SET POINTER TO LAST CONS IN RESULT
         BCT   #3,CONTINUE         DECREMENT COUNT
RETIT    LR    #15,#6              IF ZERO, RETURN RESULT LIST SO FAR
         B     RETURN              ELSE...
CONTINUE DS    0H
         L     #4,##CDR(,#4)       GO DOWN LIST
         CR    #4,#10              IF END NOT REACHED YET
         BNE   LOOP                THEN CONTINUE
         LR    #15,#6              ELSE RETURN THE RESULT LIST
         B     RETURN
         SPACE 1
RETNIL   LR    #15,#10             RETURN NIL
         SPACE 1
RETURN   DS    0H                  #15 CONTAINS RETURN VALUE
         SPACE 1
         #ZRET ,                   RETURN TO CALLER
         SPACE 3
ERROR1   DS    0H                  ERROR IN ARGUMENT
         #ERR  'Argument 1 to FIRSTN not a nonnegative fixnum - ',     X
               0(#11)
         SPACE 1
ERROR2   DS    0H                  ERROR IN ARGUMENT
         #ERR  'Argument 2 to FIRSTN not a proper list - ',            X
               4(#11)
         SPACE 1
         LTORG
         END
