         TITLE 'ZRMINIT - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBR IS CALLED BY READ OR RDRMACRO WHEN THE READTABLE IS NOT   *
* YET ALLOCATED.  IT ALLOCATES AND INITIALIZES THE READTABLE,         *
* ESTABLISHING THE DEFAULT SYSTEM READER MACRO DEFINITIONS.           *
*                                                                     *
* It also initializes the ZIL work area, used to build atoms.         *
*                                                                     *
* Also, this function may be used to reinitialize an                  *
* existing readtable (e.g. after RMMAC has changed it).               *
*                                                                     *
* THIS ROUTINE USES REGISTERS 14 THROUGH 2.                           *
*                                                                     *
***********************************************************************
         SPACE 1
ZRMINIT  #ZBEG MINARGS=0,MAXARGS=0,NAME='ZRMINIT',SUBRS=ZILSUBR
         SPACE 1
         DROP  #15
         SPACE 1
         LR    #2,#15
         USING ZRMINIT,#2
         ICM   #1,15,ZLCRDTBL      IF WE DON'T HAVE A READTABLE YET,
         BNZ   GOTIT               THEN...
         LA    #0,256               GET LENGTH OF STORAGE FOR READTABLE
         GETMAIN R,LV=(0)           GET STORAGE FOR IT
         ST    #1,ZLCRDTBL          SAVE ITS ADDRESS
GOTIT    DS    0H                  NOW INITIALIZE THE READTABLE
         MVC   0(256,#1),READTBL   COPY CONSTANT READ TABLE TO OURS
         L     #15,@RMQUOTE        GET ADDRESS OF ZRMQUOTE SUBROUTINE
         STCM  #15,7,QUOTEMAC+1    STORE INTO READER MACRO FORM
         L     #15,@RMSHARP        GET ADDRESS OF ZRMSHARP SUBROUTINE
         STCM  #15,7,SHARPMAC+1    STORE INTO READER MACRO FORM
         L     #15,@RMBQ           GET ADDRESS OF ZRMBQ SUBROUTINE
         STCM  #15,7,BACKQMAC+1    STORE INTO READER MACRO FORM
         L     #15,@RMCOMMA        GET ADDRESS OF ZRMCOMMA SUBROUTINE
         STCM  #15,7,COMMAMAC+1    STORE INTO READER MACRO FORM
         LA    #15,QUOTEMAC        POINT TO READER MACRO CHAIN
         ST    #15,ZLCRMCHN        ADD TO (EMPTY) READER MACRO CHAIN
         OI    C''''(#1),X'80'     IDENTIFY READER MACRO CHARACTER
         OI    C'#'(#1),X'80'      IDENTIFY READER MACRO CHARACTER
         OI    C'`'(#1),X'80'      IDENTIFY READER MACRO CHARACTER
         OI    C','(#1),X'80'      IDENTIFY READER MACRO CHARACTER
         SPACE 1
         ICM   #1,15,ZLCWA         GET ADDRESS OF ZIL WORK AREA
         BNZ   RETURN              IF ALREADY ALLOCATED, RETURN
         LA    #0,512              ELSE SET UP INITIAL LENGTH
         ST    #0,ZLCWALEN         SAVE ITS LENGTH
         #GETMAIN RU,LV=(0),LOC=BELOW GETMAIN THE WORK AREA
         ST    #1,ZLCWA            SAVE ITS ADDRESS
RETURN   DS    0H
         LR    #15,#10             SET RETURN VALUE TO NIL
         BR    #14                 RETURN
         SPACE 1
ZILSUBRS DS    0A                  EXTERNAL SUBROUTINES
@RMQUOTE DC    V(ZRMQUOTE)         ADDRESS OF ' PROCESSOR
         DC    CL8'ZRMQUOTE'
@RMSHARP DC    V(ZRMSHARP)         ADDRESS OF # PROCESSOR
         DC    CL8'ZRMSHARP'
@RMBQ    DC    V(ZRMBQ)            ADDRESS OF ` PROCESSOR
         DC    CL8'ZRMBQ'
@RMCOMMA DC    V(ZRMCOMMA)         ADDRESS OF , PROCESSOR
         DC    CL8'ZRMCOMMA'
ZILSUBRE DS    0A                  END OF SUBROUTINES
         SPACE 1
*
* THE (STATIC) INITIAL READER-MACRO CHAIN.
*
QUOTEMAC DC    C'''',AL3(*-*),A(SHARPMAC)
SHARPMAC DC    C'#',AL3(*-*),A(BACKQMAC)
BACKQMAC DC    C'`',AL3(*-*),A(COMMAMAC)
COMMAMAC DC    C',',AL3(*-*),A(0)
         SPACE 1
READTBL  #RTBL ,                   DEFINE THE READ TABLE
         SPACE 1
         LTORG
         SPACE 1
         END
