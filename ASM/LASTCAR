         TITLE 'LASTCAR - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* LASTCAR TAKES ONE ARGUMENT - A LIST.                                *
*                                                                     *
* THIS FUNCTION IS NON-RECURSIVE AND TAKES NO STACK SPACE.            *
*                                                                     *
***********************************************************************
         SPACE 1
LASTCAR  #ZBEG MINARGS=1,MAXARGS=1,NAME='LASTCAR'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING LASTCAR,#3
         SPACE 1
         L     #15,0(,#1)          LOAD THE ARGUMENT
* Null test subsumed by atom test.
******** CR    #15,#10             IF ARG IS NIL
******** BER   #14                 THEN RETURN NIL
         TM    ##TYPE(#15),##ATOM  IF ARG 1 NOT A LIST
         BOR   #14                 THEN RETURN IT
LOOP     DS    0H
         LM    #15,#0,##CAR(#15)   #15 = CAR, #0 = CDR
         CR    #0,#10              IF CDR IS NIL
         BER   #14                 THEN RETURN #15 = CAR OF THIS CONS
         LR    #15,#0              ELSE LOAD UP THE CDR
         TM    ##TYPE(#15),##ATOM  IF IT IS ATOMIC
         BOR   #14                 THEN RETURN IT AS THE VALUE
         B     LOOP                ELSE CONTINUE WITH THE CDR
         SPACE
         LTORG
         END
