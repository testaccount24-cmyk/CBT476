         TITLE 'GET - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* GET TAKES TWO REQUIRED AND ONE OPTIONAL ARGUMENT.                   *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND THEREFORE USES NO STACK SPACE.     *
*                                                                     *
***********************************************************************
*                                                                     *
* There should be related functions, like GETF, that do similar       *
* things, as well as "optimized-call-path" ones that remove the       *
* requirement to check argument type or inspect default value.        *
*                                                                     *
***********************************************************************
         SPACE 1
GET      #ZBEG MINARGS=2,MAXARGS=3,NAME='GET'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING GET,#3
         SPACE 1
         LM    #5,#7,0(#1)         LOAD THE ARGUMENTS
         CLI   ##TYPE(#5),##SYMBOL ARG 1 MUST BE A SYMBOL
         BNE   NOTSYM
         L     #15,##PLIST(,#5)    GET PROPERTY LIST POINTER
PROCESS  DS    0H
* Initial loop-bypassing test for null property list
         CR    #15,#10             IF END OF PROPERTY LIST (NULL)
         BE    NOTFOUND            THEN RETURN NO-PROPERTY-FOUND VALUE
         SPACE 1
LOOP     DS    0H
         LM    #1,#2,##CAR(#15)    GET CAR AND CDR OF NEXT CELL
*                                  #1 = CAR, #2 = CDR
         CR    #6,#1               IF (CAR SUBPLIST) IS "EQ" TO ARG 2
         BE    FOUND               THEN PROPERTY FOUND, GO GET VALUE
*
* If #15 didn't point to a CONS cell, then the high-order bit of #1
* will be one, since it was an "atom" type bit.
*
         LTR   #1,#1               IF PROPERTY (SUB)LIST ATOMIC
         BM    ERROR2              THEN ERROR
*                                  ELSE NOT THE ONE - CONTINUE
         L     #15,##CDR(,#2)      GET CDR OF PROPERTY (SUB)LIST
         TM    ##TYPE(#2),##ATOM   IF PROPERTY (SUB)LIST WAS ATOMIC,
         BO    ERROR2              THEN ERROR
         CR    #15,#10             IF NOT NIL, THEN CONTINUE SEARCHING
         BNE   LOOP                ELSE WE CAME TO THE END OF THE PLIST
NOTFOUND DS    0H                  PROPERTY NOT FOUND
         LR    #15,#7              LOAD ARG 3
         CR    #7,#13              IF ARG 3 SUPPLIED
         BNER  #14                 THEN RETURN WITH IT
         LR    #15,#10             ELSE RETURN DEFAULT OF NIL
         BR    #14
         SPACE 1
FOUND    DS    0H                  PROPERTY FOUND
         ICM   #15,15,##CAR(#2)    SET RETURN VALUE = CAR OF SUBLIST
         BNMR  #14                 IF VALID, RETURN TO CALLER
         B     ERROR2              IF #2 DIDN'T POINT TO CONS, ERROR
         SPACE 1
NOTSYM   DS    0H                  Arg 1 not a symbol
         SPACE 1
***********************************************************************
*                                                                     *
* FOR COMPATIBILITY WITH MACLISP AND OTHER APPLICATIONS THAT USE      *
* DISEMBODIED PROPERTY LISTS, IF ARG 1 IS A LIST THEN WE ACCEPT IT.   *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    ##TYPE(#5),##ATOM   See if it's a list
         BO    ERROR1              If so, then...
         L     #15,##CDR(,#5)       get CDR and use as property list
         B     PROCESS             Else it's an atom
         SPACE 1
ERROR1   DS    0H                  Arg 1 not even a list...
         SPACE 1
***********************************************************************
*                                                                     *
* If arg 1 is neither a symbol nor a list, it should be an error.     *
* However, for compatibility with Maclisp and other dialects of       *
* LISP that run Macsyma, we allow GET to always return the specified  *
* "default value" in such cases.                                      *
*                                                                     *
***********************************************************************
         SPACE 1
         LR    #4,#14              SAVE RETURN ADDRESS
* Issue warning message and continue
         #ERR  1,'Argument 1 to GET not a symbol, returning default valX
               ue of ',(#7),' - ',(#5)
         LR    #14,#4              Restore return address
         B     NOTFOUND            Go to return default value
         SPACE 1
ERROR2   DS    0H                  INVALID PROPERTY LIST
         #ERR  'ZIL4011 GET: Invalid property list found for ',(#5),   X
               PREFIX=NO
         SPACE 1
         LTORG
         END
