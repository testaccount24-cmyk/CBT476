         TITLE 'PUTPROP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* PUTPROP TAKES 3 ARGUMENTS.                                          *
*                                                                     *
***********************************************************************
         SPACE 1
PUTPROP  #ZBEG MINARGS=3,MAXARGS=3,NAME='PUTPROP'
         SPACE 1
         #ZPDS 3*4+12
         #ZSAV ,
         SPACE 1
         LM    #5,#7,0(#11)        LOAD THE ARGUMENTS
         CLI   ##TYPE(#5),##SYMBOL ARG 1 MUST BE A SYMBOL
         BNE   NOTSYM
         L     #2,##PLIST(,#5)     LOAD PROPERTY LIST OF ARG 1
PROCESS  DS    0H
         CR    #2,#10              IF NONEXISTENT OR NIL
         BE    NOMORE              THEN WE HAVE TO MAKE A NEW PLIST
         LR    #4,#2               SAVE CURRENT PROPERTY LIST ADDRESS
LOOP     DS    0H                  ELSE SEARCH EXISTING PLIST
         TM    ##TYPE(#4),##ATOM   CHECK VALIDITY OF PROPERTY LIST
         BO    ERROR2
         L     #1,##CDR(,#4)       GET READY WITH CDR OF (SUB)PLIST
         TM    ##TYPE(#1),##ATOM   CHECK VALIDITY OF PROPERTY LIST
         BO    ERROR2
         C     #7,##CAR(,#4)       IF CAR OF (SUB)PLIST EQ TO INDICATOR
         BNE   NOTEQ               THEN...
         ST    #6,##CAR(,#1)        BASH CADR THEREOF TO REPLACE VALUE
         B     RETURN              ELSE...
NOTEQ    DS    0H                   (HAVEN'T FOUND IT YET)
         L     #4,##CDR(,#1)        BUMP TO NEXT PAIR IN (SUB)PLIST
         CR    #4,#10               IF ANY MORE PROP/VALUE PAIRS LEFT
         BNE   LOOP                 THEN CONTINUE LOOP
NOMORE   DS    0H                  OTHERWISE INDICATOR ISN'T THERE NOW
*
* AT THIS POINT ZIL USED TO STICK PROPERTY LIST STUFF ONTO THE END OF
* THE EXISTING PROPERTY LIST.  IT'S REALLY SUPPOSED TO BE CONSED ONTO
* THE FRONT OF IT, HOWEVER, AND THAT'S WHAT WE'RE GOING TO DO NOW.
*
*                                  (CONS #7 (CONS #6 OLD-PLIST))
*                                  #2 ALREADY POINTS TO OLD PLIST
*
         LR    #1,#6               PROPERTY VALUE
         L     #15,ZLCCONS         GET ADDRESS OF ZILCONS
         LA    #0,24(,#11)         POINT TO CURRENT STACK LOCATION
         #BASR #14,#15             CONS (PROPERTY-VALUE) NIL
         L     #1,8(,#11)          INDICATOR (ARG 3)
         LR    #2,#15              RESULT OF PREVIOUS CONS
         L     #15,ZLCCONS         GET ADDRESS OF ZILCONS
         LA    #0,24(,#11)         POINT TO CURRENT STACK LOCATION
         #BASR #14,#15             CONS INDICATOR *
         LM    #5,#6,0(#11)        RELOAD OTHER ARGS
         TM    ##TYPE(#5),##ATOM   If arg 1 was a list
         BO    BASHSYM             then
         ST    #15,##CDR(,#5)       bash cdr of the list
         B     RETURN              else
BASHSYM  DS    0H
         ST    #15,##PLIST(,#5)     BASH PLIST OF THE SYMBOL
         SPACE 1
RETURN   DS    0H
         LR    #15,#6              RETURN THE PROPERTY AS VALUE
         #ZRET ,                   RETURN TO CALLER
         SPACE 1
NOTSYM   DS    0H                  Arg 1 not a symbol
         SPACE 1
***********************************************************************
*                                                                     *
* FOR COMPATIBILITY WITH MACLISP AND OTHER APPLICATIONS THAT USE      *
* DISEMBODIED PROPERTY LISTS, IF ARG 1 IS A LIST THEN WE ACCEPT IT.   *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    ##TYPE(#5),##ATOM   See if it's a list
         BO    ERROR1              If so, then...
         L     #2,##CDR(,#5)        get CDR and use as property list
         B     PROCESS
         SPACE 1
ERROR1   DS    0H                  Arg 1 not even a list...
         #ERR  'Argument 1 to PUTPROP not a symbol - ',0(#11)
         SPACE 1
ERROR2   DS    0H
         #ERR  'ZIL4011 PUTPROP: Invalid property list found for ',    X
               0(#11),PREFIX=NO
         SPACE 1
         LTORG
         END
