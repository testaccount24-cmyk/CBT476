         TITLE 'UNWORD - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
* UNWORD TAKES ONE ARGUMENT - A FOUR-CHARACTER SYMBOL OR STRING       *
* TO BE CONVERTED TO A FIXNUM (USED AS A "CHARACTER OBJECT").         *
***********************************************************************
         SPACE
UNWORD   #ZBEG MINARGS=1,MAXARGS=1,NAME='UNWORD'
         #ZPDS 16                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         L     #1,0(,#11)          GET ARG 1
         CLI   ##TYPE(#1),##STRING IF ARG IS A STRING
         BE    PROCESS             THEN GO AHEAD AND PROCESS IT
         CLI   ##TYPE(#1),##SYMBOL IF ARG 1 IS A SYMBOL
         BNE   ERROR1              THEN OK, ELSE ERROR
         L     #1,##PNAME(,#1)     IF A SYMBOL, GET THE PRINT NAME
PROCESS  DS    0H
         CLC   1(3,#1),=AL3(4)     IF LENGTH IS NOT 4
         BNE   ERROR1              THEN ERROR
         L     #2,##VECDAT(,#1)    PICK UP CHARACTER TO BE NUMBERIZED
         LA    #0,16(,#11)         PASS CURRENT STACK POINTER TO CONS
         #MKAT FIXED               MAKE A FIXNUM OUT OF IT
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
ERROR1   DS    0H                  IF NOT 1, ERROR
         #ERR  'Argument to UNWORD not a 4-character string - ',0(#11)
         SPACE 1
         LTORG
         END
