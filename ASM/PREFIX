         TITLE 'PREFIX - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
*  PREFIX RETURNS THE TSO PREFIX, OR NIL IF NON-TSO ENVIRONMENT.      *
*                                                                     *
*  NOTE THAT THE PREFIX MAY BE A NULL STRING.                         *
*                                                                     *
***********************************************************************
         SPACE 1
PREFIX   #ZBEG MINARGS=0,MAXARGS=0,NAME='PREFIX'
         #ZPDS 12
         #ZSAV ,
         SPACE 1
***********************************************************************
*                                                                     *
* FIRST, GET THE UPT PREFIX VALUE.                                    *
*                                                                     *
***********************************************************************
         L     #3,X'21C'           GET TCB ADDRESS
         L     #3,X'B4'(,#3)       GET JSCB ADDRESS
         L     #3,X'15C'(,#3)      GET ACTIVE JSCB ADDRESS
         ICM   #3,15,X'108'(#3)    GET PSCB ADDRESS
         BZ    RETNIL              IF NONE, NON-TSO, RETURN NIL
         USING PSCB,#3
         L     #3,PSCBUPT          GET UPT ADDRESS
         DROP  #3
         USING UPT,#3              GET UPT ADDRESS
         XR    #2,#2               CLEAR LENGTH REGISTER
         ICM   #2,1,UPTPREFL       GET LENGTH OF UPT PREFIX
         ST    #2,LENGTH           SAVE LENGTH
         BZ    RETSTR              IF ZERO, BYPASS DATA MOVE
         SPACE 1
***********************************************************************
*                                                                     *
* THEN, GET VECTOR/STRING SPACE TO HOLD THE PREFIX.                   *
*                                                                     *
***********************************************************************
         SPACE 1
******** L     #2,LENGTH           GET LENGTH INTO REG FOR ZILVSS
         XR    #1,#1               ENTRY CODE 0 = ALLOCATE NEW SPACE
         L     #15,ZLCVSS          GET ADDRESS OF ZILVSS
         LA    #0,12(,#11)         SET CURRENT STACK POINTER FOR GC
         #BASR #14,#15             CALL ZILVSS TO CREATE A STRING
         LTR   #15,#15             IF STRING BUILD FAILED
         BZ    ERROR               THEN ERROR
         L     #2,LENGTH           GET LENGTH OF TSO PREFIX
         BCTR  #2,0                REDUCE FOR EXECUTE
         EX    #2,MOVEIT           MOVE DATA FROM UPT TO OUR STRING
         B     RETURN
RETNIL   DS    0H                  HERE IF TSO NOT ACTIVE
         LR    #15,#10
         B     RETURN
RETSTR   DS    0H                  HERE IF TSO BUT NO PREFIX
         LA    #15,NULLSTR
******** B     RETURN
         SPACE 1
RETURN   DS    0H
         #ZRET ,
         SPACE 1
ERROR    #ERR  PREFIX=NO,          NOT ENUF ROOM                       X
               'ZIL4010 PREFIX: Not enough vector/string space to returX
               n TSO prefix.'
         SPACE 1
MOVEIT   MVC   ##VECDAT(*-*,#15),UPTPREFX X'D: MOVE UPTPREFX TO STRING
         SPACE 1
NULLSTR  DS    0A                  THE NULL STRING
         DC    YL1(##STRING),AL3(0),XL4'00'
         SPACE 1
LENGTH   DC    F'0'
         SPACE 1
         LTORG
         SPACE 1
         IKJPSCB
         IKJUPT
         END
