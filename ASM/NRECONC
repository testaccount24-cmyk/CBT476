         TITLE 'NRECONC - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* (NRECONC X Y) IS EQUIVALENT TO (NCONC (NREVERSE X) Y).              *
*                                                                     *
***********************************************************************
         SPACE 1
NRECONC  #ZBEG MINARGS=2,MAXARGS=2,NAME='NRECONC'
         SPACE 1
         LR    #7,#15
         DROP  #15
         USING NRECONC,#7
         SPACE 1
         LM    #15,#0,0(#1)        #15 = ARG1, #0 = ARG2
         CR    #15,#10             IF ARG1 IS NIL
         BNE   GOAHEAD             THEN
         LR    #15,#0               RETURN WITH ARG2
         BR    #14
GOAHEAD  DS    0H
         SPACE 1
         TM    ##TYPE(#15),##ATOM  IF ARGUMENT IS NOT A LIST
         BO    ERROR1              THEN ERROR
         SPACE 1
         L     #2,##CDR(,#15)      LOAD OLD-CDR WITH CDR OF ARGUMENT
         LR    #3,#0               SET OLD-CAR TO ARG2
         B     GO                  START UP LOOP
NOTNULL  DS    0H                  ELSE...
         TM    ##TYPE(#2),##ATOM   IF ARGUMENT IS NOT A LIST
         BO    ERROR2              THEN ERROR
         ST    #3,##CDR(,#15)      BASH CDR OF LIST WITH OLD-CAR
         LR    #3,#15              SET OLD-CAR TO LIST
         LR    #15,#2              SET LIST TO OLD-CDR
         L     #2,##CDR(,#2)       SET OLD-CDR TO CDR OF OLD-CDR
GO       DS    0H
         CR    #2,#10              IF OLD-CDR IS NULL
         BNE   NOTNULL             THEN...
         ST    #3,##CDR(,#15)       BASH CDR OF LIST WITH OLD-CAR
         BR    #14                  RETURN LIST
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         L     #2,0(,#1)           Reload arg 1 from stack
         #ERR  'Argument 1 to NRECONC not a list - ',(#2)
         SPACE 1
ERROR2   DS    0H                  ERROR IN ARGUMENT
         L     #2,0(,#1)           Reload arg 1 from stack
         #ERR  'Argument 1 to NRECONC not a proper list.',             X
               X'0D',              carriage return                     X
               'Value may have been destroyed - ',(#2)
         SPACE 1
         LTORG
         END
