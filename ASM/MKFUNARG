         TITLE 'MKFUNARG - ZIL 1.3 INTERNAL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE MAKES A FUNARG (LEXICAL CLOSURE).                   *
*                                                                     *
* IT TAKES TWO ARGUMENTS - THE FUNCTION AND THE ENVIRONMENT.          *
*                                                                     *
***********************************************************************
*                                                                     *
*            THIS ROUTINE HAS BEEN COMPLETELY RECODED TO BUILD THE    *
*            NEW TYPE OF FUNARG OBJECT.  THE NEW OBJECT IS BUILT IN   *
*            VECTOR/STRING SPACE RATHER THAN CONS SPACE, AND LOOKS    *
*            LIKE A VECTOR.                                           *
*                                                                     *
***********************************************************************
         SPACE 1
MKFUNARG #ZBEG MINARGS=2,MAXARGS=2,NAME='MAKE-FUNARG'
         SPACE 1
         #ZPDS 20
         #ZSAV ,
         SPACE 1
         LA    #0,20(,#11)         SET CURRENT STACK POINTER FOR GC
         SPACE 1
RETRY    DS    0H
         SPACE 1
         L     #15,ZLCVSNXT        GET ADDRESS OF CURRENT VSS SLOT
         LA    #2,12(,#15)         POINT TO WHERE OUR END WILL BE
         L     #3,ZLCVSADR         ADDRESS + LENGTH OF V/S SPACE
         A     #3,ZLCVSLEN         GIVES POINTER TO END OF SPACE
         CR    #2,#3               IF OUR END IS PAST AVAILABLE END
         BH    NOROOM              THEN NO ROOM - TRY TO GC
         MVI   ##TYPE(#15),##FUNARG SET TYPE
         LA    #0,8                 SET LENGTH
         STCM  #0,7,1(#15)          (CURRENTLY ALWAYS 2 ELEMENTS*4=8)
         MVC   ##VECDAT(8,#15),0(#11) MOVE ARGS 1&2 TO OUR "VECTOR"
         ST    #2,ZLCVSNXT         UPDATE NEXT-SLOT POINTER
         #ZRET ,                   RETURN WITH NEW OBJECT IN #15
         SPACE 1
NOROOM   DS    0H                  WHAT IF THERE'S NOT ENOUGH ROOM?
         SPACE 1
         LTR   #0,#0               IF WE'VE TRIED TO GC BEFORE
         BZ    ERROR               THEN GIVE UP, NO V/S SPACE AVAILABLE
         L     #9,ZLCGCEND         ELSE CLOBBER CONS SPACE POINTER
         LR    #1,#10              DUMMY ARG 1 TO CONS = NIL
         LR    #2,#10              DUMMY ARG 2 TO CONS = NIL
         L     #15,ZLCCONS         GET ADDRESS OF ZILCONS
         #BASR #14,#15             CALL CONS TO FORCE GARB COLLECTION
         XR    #0,#0               CLEAR #0 TO STOP RETRY OF GC
         B     RETRY               TRY AGAIN
         SPACE 1
ERROR    #ERR  'ZIL2133 MAKE-FUNARG: No vector/string space available fX
               or FUNARG.',PREFIX=NO
         SPACE 1
         LTORG
         SPACE 1
         END
