         TITLE 'MINUS - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* MINUS TAKES ONE ARGUMENT - A FIXNUM, FLONUM OR BIGNUM               *
*                                                                     *
***********************************************************************
         SPACE 1
MINUS    #ZBEG MINARGS=1,MAXARGS=1,NAME='MINUS'
         #ZPDS 16
         #ZSAV ,
         SPACE
         L     #15,0(,#11)         GET THE ARG
         CLI   0(#15),##FIXNUM
         BE    MINUSFIX
         CLI   0(#15),##SFLOAT
         BE    MINUSSFL
         CLI   0(#15),##BIGNUM
         BE    MINUSBIG
         CLI   0(#15),##DFLOAT
         BE    MINUSDFL
         B     ERROR
         SPACE 1
MINUSFIX DS    0H                  MINUS OF A FIXNUM
         SPACE 1
         ICM   #2,15,##CDR(#15)    GET FIXNUM VALUE
         BZ    RETURN              IF ALREADY ZERO, RETURN ITSELF
         LCR   #2,#2               NEGATE IT
         BO    FIXOVFLO            IF OVERFLOW, MUST HAVE BEEN -2**31
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM
RETURN   DS    0H
         SPACE 1
         #ZRET ,                   RETURN WITH VALUE
         SPACE 1
FIXOVFLO DS    0H                  HERE IF INPUT IS -2**31
         LA    #15,P2TO31          RETURN BIGNUM +2**31
         B     RETURN
         SPACE 1
MINUSSFL DS    0H                  MINUS OF A SINGLE FLOAT
         SPACE 1
         LE    #F2,##CDR(,#15)     GET FLONUM VALUE
         LCER  #F2,#F2             NEGATE IT
         BZ    RETURN              IF WAS ZERO, RETURN SELF, NOT THIS
         STE   #F2,ZLCWORK
         L     #2,ZLCWORK
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FLOAT               MAKE A FLONUM
         B     RETURN              AND RETURN WITH IT
         SPACE 1
MINUSDFL DS    0H                  MINUS OF A DOUBLE FLOAT
         SPACE 1
         LD    #F0,8(,#15)         GET FLONUM VALUE
         LCDR  #F0,#F0             NEGATE IT
         BZ    RETURN              IF WAS ZERO, RETURN SELF, NOT THIS
         L     #15,ZLCMDF          GET ADDRESS OF ZILMDF
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #BASR #14,#15             CALL ZILMDF TO MAKE A DOUBLE FLOAT
         B     RETURN              AND RETURN WITH IT
         SPACE 1
MINUSBIG DS    0H                  MINUS OF A BIGNUM
         SPACE 1
         CLC   0(12,#15),P2TO31    IF BIGNUM IS 2**31
         BE    BIG2FIX             THEN GO TO RETURN A FIXNUM -2**31
         XR    #2,#2               ELSE CLEAR INSERT REGISTER
         ICM   #2,7,1(#15)         GET BIGNUM LENGTH
         LA    #2,4(,#2)           PLUS LENGTH OF HEADER
         LR    #3,#2               SAVE THIS LENGTH
         L     #15,ZLCBIG          GET ADDRESS OF BIGNUM MAKER
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #BASR #14,#15             CALL ZILBIG
         LR    #4,#15              SAVE ADDRESS OF NEW BIGNUM
         LR    #0,#4               DESTINATION ADDRESS
         L     #14,0(,#11)         SOURCE ADDRESS
         LR    #1,#3               DESTINATION LENGTH
         LR    #15,#3              SOURCE LENGTH
         MVCL  #0,#14              MOVE OLD BIGNUM TO NEW BIGNUM
         ST    #0,ZLCVSNXT         UPDATE V/S-SPACE POINTER
         LR    #15,#4              GET ADDRESS OF BIGNUM AGAIN
         XI    ##VECDAT(#15),X'80' FLIP SIGN BIT
         B     RETURN
         SPACE 1
BIG2FIX  DS    0H                  HERE IF INPUT IS +2**31
         LA    #15,M2TO31          RETURN FIXNUM -2**31
         B     RETURN
         SPACE 1
         SPACE 2
M2TO31   #IATM -2147483648         FIXNUM -2**31
         SPACE 1
P2TO31   DS    0A                  ALIGN BIGNUM TO FULLWORD
         DC    YL1(##BIGNUM),AL3(8),F'0,1'   BIGNUM 2**31
         SPACE 1
ERROR    DS    0H
         #ERR  'Argument to MINUS not numeric - ',0(#11)
         SPACE 1
         LTORG
         END
