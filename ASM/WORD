         TITLE 'WORD - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* WORD TAKES TWO ARGUMENTS, OF WHICH THE SECOND IS OPTIONAL.          *
* THE FIRST IS A FIXNUM TO BE CONVERTED TO A FOUR-CHARACTER SYMBOL.   *
* THE SECOND SPECIFIES WHETHER (NON-NIL) OR NOT (NIL) TO INTERN THE   *
*  GENERATED SYMBOL ON THE OBJECT LIST (NOTE DEFAULT IS NOT TO).      *
* THIS ROUTINE IS NOT RECURSIVE AND USES NO EXTRA STACK SPACE.        *
*                                                                     *
***********************************************************************
         SPACE
WORD     #ZBEG MINARGS=1,MAXARGS=2,NAME='WORD'
         #ZPDS 2*4+12+0 = 20       DEFINE STACK SIZE
         #ZSAV ARGS=2              SAVE REGISTERS
         SPACE
         L     #2,0(,#11)          GET ATOM (ARG 1 TO WORD)
         CLI   ##TYPE(#2),##FIXNUM MUST BE AN ATOM
         BNE   ERROR1              OR ELSE
*
* BUILD A FOUR-CHARACTER ATOM FROM IT.
*
         MVC   WORDWORK(4),##CDR(#2) MOVE FIXNUM CONTENTS TO WORK AREA
         LA    #0,4                SET LENGTH = 4
         LA    #1,WORDWORK         POINT TO WORD TO BE ATOMIZED
         L     #2,4(,#11)          SET INTERN FLAG = ARG 2 TO WORD
         LR    #3,#10              SET FORMAT FLAG = NIL
         LA    #4,20(,#11)         SET CURRENT STACK POINTER FOR CONS
         L     #15,ZLCABLD         CALL ZILABLD TO MAKE AN ATOM FROM IT
         #BASR #14,#15             (RETURNS ATOM IN #15)
         SPACE 2
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 2
ERROR1   DS    0H
         #ERR  'Argument to WORD not a fixnum - ',0(#11)
         SPACE 1
WORDWORK DS    CL4
         SPACE 1
         LTORG
         END
