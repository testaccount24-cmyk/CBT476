         TITLE 'NTHCDR - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* NTHCDR TAKES TWO ARGUMENTS - A NUMBER AND A LIST.                   *
*                                                                     *
* THIS FUNCTION IS NON-RECURSIVE AND TAKES NO STACK SPACE.            *
*                                                                     *
***********************************************************************
         SPACE 1
NTHCDR   #ZBEG MINARGS=2,MAXARGS=2,NAME='NTHCDR'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING NTHCDR,#3
         SPACE 1
         LM    #5,#6,0(#1)         LOAD THE ARGUMENTS
         CLI   ##TYPE(#5),##FIXNUM IF ARG 1 NOT A FIXNUM
         BNE   ERROR1              THEN ERROR
         ICM   #2,15,##CDR(#5)     GET FIXNUM VALUE
         BM    ERROR1              IF NEGATIVE, THEN ERROR
         LR    #15,#6
LOOP     DS    0H
         CR    #15,#10             IF ARG 2 IS NULL
         BER   #14                 THEN RETURN NIL
         TM    ##TYPE(#15),##ATOM  IF ARG 2 IS AN ATOM
         BO    ERROR2              THEN AN IMPROPER LIST WAS PASSED
         LTR   #2,#2               IF ZERO
         BZR   #14                 THEN RETURN THIS PART OF LIST
         L     #15,##CDR(,#15)     ELSE GET CDR OF LIST
         BCTR  #2,0                SET N = N - 1
         B     LOOP                REPEAT UNTIL N = 0 OR END OF LIST
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT 1
         L     #2,0(,#1)
         #ERR  'Argument 1 to NTHCDR not a nonnegative fixnum - ',(#2)
         SPACE 1
ERROR2   DS    0H                  ERROR IN ARGUMENT 1
         L     #2,4(,#1)
         #ERR  'Argument 2 to NTHCDR not a proper list - ',(#2)
         SPACE 1
         LTORG
         END
