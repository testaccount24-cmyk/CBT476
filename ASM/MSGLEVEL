         TITLE 'MSGLEVEL - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* MSGLEVEL TAKES ONE (OPTIONAL) ARGUMENT - A NUMBER FROM 0 TO 3.      *
*                                                                     *
* IF NIL IS SPECIFIED OR DEFAULTED, THE CURRENT MSGLEVEL IS RETURNED. *
*                                                                     *
***********************************************************************
         SPACE
MSGLEVEL #ZBEG MINARGS=0,MAXARGS=1,NAME='MSGLEVEL'
         SPACE
         L     #7,0(,#1)           PICK UP THE ARGUMENT
         CR    #7,#10              IF ARGUMENT IS NIL
         BE    ISNIL                OR
         CR    #7,#13                ARGUMENT IS NOT SUPPLIED
         BNE   NOTNIL                 THEN
ISNIL    XR    #2,#2                PICK UP CURRENT MSG LEVEL
         IC    #2,ZLCMSLVL          FROM THE COMMON AREA
         SLA   #2,3                 CONVERT TO INDEX INTO FIXNUM TABLE
         LA    #15,#SFT(#2)         POINT TO THE APPROPRIATE VALUE
         BR    #14                  AND RETURN WITH IT
         SPACE 1
NOTNIL   DS    0H                  ELSE NOT NIL
         CLI   ##TYPE(#7),##FIXNUM IF ARG IS NOT A FIXNUM
         BNE   BADVALUE            THEN ERROR. ELSE...
         L     #2,##CDR(,#7)       GET FIXNUM VALUE
         CL    #2,#SFW3            IF NOT (0 <= VALUE <= 3)
         BH    BADVALUE            THEN VALUE IS OUT OF RANGE
         STC   #2,ZLCMSLVL         ELSE SET THE VALUE
         LR    #15,#7              LOAD ARG INTO RETURN REG
         BR    #14                 RETURN WITH ARG AS THE RETURN
         SPACE 1
         DROP  #15
         SPACE 1
         LTORG
         SPACE 1
BADVALUE DS    0H
         LR    #2,#15
         USING MSGLEVEL,#2
         #ERR  'Argument to MSGLEVEL not a fixnum between 0 and 3 - ', X
               (#7)
         SPACE 1
         END
