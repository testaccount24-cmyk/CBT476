         TITLE 'ZMSASSQ - BUILT-IN ZIL 1.3 SUBROUTINE FOR MACSYMA'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ZMSASSQ: SAME AS ASSQ BUT WORKS ON NUMBERS AS WELL.                 *
*                                                                     *
* NEEDED TO RUN MACSYMA, WHICH THINKS (ASSQ 1 '((1 . FOO))) WORKS.    *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND USES NO STACK SPACE.               *
*                                                                     *
***********************************************************************
         SPACE 1
ZMSASSQ  #ZBEG MINARGS=2,MAXARGS=2,NAME='ZMSASSQ'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING ZMSASSQ,#3
         SPACE 1
         LM    #5,#6,0(#1)         LOAD THE ARGUMENTS
         LR    #7,#6
         CLI   ##TYPE(#5),##FIXNUM IF ARG 1 IS A FIXNUM
         BE    PFIX                THEN DO FIXNUM SEARCH
*                                  ELSE DO NORMAL ASSQ SEARCH
         SPACE 1
LOOP     DS    0H
         CR    #7,#10              COMPARE (SUB)ALIST TO NIL
         LM    #6,#7,##CAR(#7)     #6 = SUBLISTS'S CAR, #7 = ITS CDR
         BE    FAIL                IF SUBLIST WAS NIL, RETURN NIL
         C     #5,##CAR(,#6)       IF ARG 1 NOT EQ ITS CAR'S CAR
         BNE   LOOP                THEN CONTINUE WITH SUBALIST'S CDR
         LR    #15,#6              ELSE RETURN WITH SUBALIST'S CAR
         BR    #14
         SPACE 1
PFIX     DS    0H                  SEARCH FOR EQL FIXNUM
FIXNLOOP DS    0H                  SEARCH FOR EQL FIXNUM
         CR    #7,#10              COMPARE (SUB)ALIST TO NIL
         LM    #6,#7,##CAR(#7)     #6 = SUBLISTS'S CAR, #7 = ITS CDR
         BE    FAIL                IF SUBLIST WAS NIL, RETURN NIL
         ICM   #4,15,##CAR(#6)     GET CAAR OF IT (LEFT ELT OF PAIR)
         BM    FIXNLOOP            IF IT WASN'T A CONS, NO MATCH
         CLC   0(8,#5),0(#4)       IF TYPE AND VALUE ARE NOT EQUAL
         BNE   FIXNLOOP            THEN CONTINUE WITH SUBALIST'S CDR
         LR    #15,#6              ELSE RETURN WITH SUBALIST'S CAR
         BR    #14
         SPACE 1
FAIL     DS    0H                  NOT FOUND IN ALIST
         LR    #15,#10             RETURN NIL
         BR    #14
         SPACE 1
         LTORG
         END
