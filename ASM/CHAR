         TITLE 'CHAR - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* CHAR TAKES TWO ARGUMENTS - A STRING AND A FIXNUM.                   *
*                                                                     *
* IT RETURNS A FIXNUM.  INDEXING IS ZERO-BASED, UNLIKE GETCHAR(N).    *
*                                                                     *
* Note: Because this function does not need to CONS, it does not      *
*       have to save anything on the stack.                           *
*                                                                     *
***********************************************************************
         SPACE 1
CHAR     #ZBEG MINARGS=2,MAXARGS=2,NAME='CHAR'
         SPACE 1
         LM    #6,#7,0(#1)         LOAD ARGS
         CLI   ##TYPE(#6),##STRING IF ARGUMENT IS A STRING
         BE    ARG1OK              THEN OK
         CLI   ##TYPE(#6),##SYMBOL IF ARGUMENT IS A SYMBOL
         BNE   ERROR1              THEN
         L     #6,##PNAME(,#6)      TAKE THE PRINT NAME, ELSE ERROR
ARG1OK   DS    0H
         CLI   ##TYPE(#7),##FIXNUM IF ARG 2 IS NOT A FIXNUM
         BNE   ERROR2              THEN ERROR
         ICM   #5,15,##CDR(#7)     GET FIXNUM VALUE
         BM    RETNIL              IF NEGATIVE, RETURN NIL
         XR    #4,#4               CLEAR INSERT REG
         ICM   #4,7,1(#6)          GET STRING LENGTH
         CR    #5,#4               IF INDEX EXCEEDS LENGTH
         BNL   RETNIL              THEN RETURN NIL
         XR    #2,#2               CLEAR INSERT REGISTER
         IC    #2,##VECDAT(#5,#6)  GET STRING CHARACTER INTO REGISTER
         SLA   #2,3                Convert to index into fixnum table
         LA    #15,#SFT(#2)        Point to fixnum in table
         BR    #14                 Return with #15 pointing to fixnum
         SPACE 1
RETNIL   DS    0H                  INDEX OUT OF BOUNDS
         LR    #15,#10             RETURN NIL
         BR    #14                 Return with #15 pointing to fixnum
         SPACE 1
         DROP  #15
         USING CHAR,#3
ERROR1   LR    #3,#15
         #ERR  'Argument 1 to CHAR not a string - ',(#6)
ERROR2   LR    #3,#15
         #ERR  'Argument 2 to CHAR not a fixnum - ',(#7)
         SPACE 1
         LTORG
         END
