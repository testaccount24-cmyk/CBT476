         TITLE 'ZMSMEMQ - BUILT-IN ZIL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* ZMSMEMQ: SAME AS MEMQ BUT WORKS ON NUMBERS AS WELL.                 *
*                                                                     *
* NEEDED TO RUN MACSYMA, WHICH THINKS THAT FIXNUMS CAN BE EQ.         *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND USES NO STACK SPACE.               *
*                                                                     *
***********************************************************************
         SPACE 1
ZMSMEMQ  #ZBEG MINARGS=2,MAXARGS=2,NAME='ZMSMEMQ'
         SPACE 1
         LR    #2,#15
         DROP  #15
         USING ZMSMEMQ,#2
         LR    #4,#14              SAVE RETURN ADDRESS
         LM    #14,#15,0(#1)       LOAD ARGS 1 AND 2
         CLI   ##TYPE(#14),##FIXNUM IF ARG 1 IS A FIXNUM
         BE    PFIX                THEN DO FIXNUM SEARCH
         SPACE 1
LOOP     DS    0H                  ELSE DO NORMAL (EQ) SEARCH
         SPACE 1
         CR    #15,#10             WHEN (SUB)LIST IS NIL
         BER   #4                  THEN RETURN NIL
         LM    #5,#6,##CAR(#15)    #14 = CAR OF (SUB)LIST, #15 = CDR
         LTR   #5,#5               IF (SUB)LIST CELL WAS REALLY ATOMIC
         BM    BADLIST             THEN BAD LIST, RETURN NIL
         CR    #14,#5              IF ARG1 IS EQ TO CAR OF (SUB)LIST
         BER   #4                  THEN RETURN
         LR    #15,#6              ELSE GET CDR OF (SUB)LIST
         B     LOOP                AND CONTINUE.
BADLIST  DS    0H                  (SUB)LIST IS NOT ATOMIC
         LR    #15,#10             WHAT THE HECK...
         BR    #4                  JUST RETURN NIL
         SPACE 1
PFIX     DS    0H                  SEARCH FOR EQL FIXNUM
         SPACE 1
FIXNLOOP DS    0H
         SPACE 1
         CR    #15,#10             WHEN (SUB)LIST IS NIL
         BER   #4                  THEN RETURN NIL
         LM    #5,#6,##CAR(#15)    #5 = CAR OF (SUB)LIST, #6 = CDR
         LTR   #5,#5               IF (SUB)LIST CELL WAS REALLY ATOMIC
         BM    BADLIST             THEN BAD LIST, RETURN NIL
         CLC   0(8,#14),0(#5)      IF ARGS ARE EQL (BOTH FIXNA)
         BER   #4                  THEN RETURN
         LR    #15,#6              ELSE GET CDR OF (SUB)LIST
         B     FIXNLOOP            AND CONTINUE.
         SPACE 1
         LTORG
         END
