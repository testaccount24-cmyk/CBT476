         TITLE 'SLENGTH - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* SLENGTH TAKES ONE ARGUMENT - A STRING                               *
*                                                                     *
***********************************************************************
SLENGTH  #ZBEG MINARGS=1,MAXARGS=1,NAME='STRING-LENGTH'
         #ZPDS 16                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         L     #1,0(,#11)          LOAD FIRST (AND ONLY) ARGUMENT
         XR    #2,#2               CLEAR INSERT REGISTER
         CLI   ##TYPE(#1),##STRING ARG MUST BE A STRING OR SYMBOL
         BE    OK                  ELSE ERROR
         CLI   ##TYPE(#1),##SYMBOL IF SYMBOL, THEN USE PRINT NAME
         BNE   ERROR1
         L     #1,##PNAME(,#1)
OK       DS    0H
         ICM   #2,7,1(#1)          GET LENGTH OF STRING TEXT
         CL    #2,ZLCSFTHI         If within small fixnum range
         BNH   SMALLFIX            then go return a fixnum from table
         LA    #0,16(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FIXED               MAKE A FIXNUM
         B     RETURN
SMALLFIX DS    0H                  If fixnum is small enough...
         SLA   #2,3                convert to index into fixnum table
         LA    #15,#SFT(#2)        return small fixnum from table
RETURN   DS    0H
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 3
ERROR1   DS    0H                  INVALID ARGUMENT
         #ERR  'Argument to STRING-LENGTH not a string - ',0(#11)
         SPACE 1
         LTORG
         END
