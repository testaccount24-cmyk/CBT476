         TITLE 'GETK - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* GETK TAKES TWO REQUIRED ARGUMENTS - A LIST AND A KEYWORD INDICATOR  *
* - AND AN OPTIONAL ARGUMENT INDICATING WHETHER AN ERROR IS TO        *
*   BE SIGNALLED IF THE LIST IS BAD.                                  *
*                                                                     *
* THE LIST IS NOT A "DISEMBODIED PROPERTY LIST"; RATHER, IT IS A      *
* LIST OF ALTERNATING KEYWORDS AND VALUES.                            *
*                                                                     *
* THIS FUNCTION RETURNS THE SUBLIST OF THE PROPERTY LIST BEGINNING    *
* WITH THE VALUE (NOT THE KEYWORD), OR NIL IF THE INDICATOR IS        *
* NOT FOUND.                                                          *
*                                                                     *
* THIS FUNCTION IS DESIGNED FOR USE WITH CODE THAT ACCESSES THE       *
* VALUE OF KEYWORDS USING A LAMBDA LIST WITH &KEY SPECIFIED.          *
*                                                                     *
* THIS ROUTINE IS NONRECURSIVE AND THEREFORE USES NO STACK SPACE.     *
*                                                                     *
* This is a ZIL-only function.                                        *
*                                                                     *
***********************************************************************
         SPACE 1
GETK     #ZBEG MINARGS=2,MAXARGS=3,NAME='GETK'
         SPACE 1
         LR    #3,#15
         DROP  #15
         USING GETK,#3
         SPACE 1
         LM    #15,#0,0(#1)        #15 = LIST, #0 = KEYWORD
         SPACE 1
LOOP     DS    0H
         CR    #15,#10             IF END OF PROPERTY LIST (NULL)
         BER   #14                 THEN RETURN NIL
         TM    ##TYPE(#15),##ATOM  IF PROPERTY (SUB)LIST ATOMIC
         BO    ERROR1              THEN ERROR
         C     #0,##CAR(,#15)      IF (CAR SUBPLIST) IS "EQ" TO ARG 2
         BNE   NEXT                THEN...
         L     #15,##CDR(,#15)      GET CDR OF PROPERTY (SUB)LIST
         BR    #14                 RETURN TO CALLER
         SPACE 1
NEXT     DS    0H                  ELSE NOT THE ONE - CONTINUE
         L     #15,##CDR(,#15)      GET CDR OF PROPERTY (SUB)LIST
         TM    ##TYPE(#15),##ATOM  IF PROPERTY (SUB)LIST ATOMIC
         BO    ERROR1              THEN ERROR
         L     #15,##CDR(,#15)     GET CDR OF PROPERTY (SUB)LIST
         B     LOOP                CONTINUE PROCESSING
         SPACE 1
ERROR1   DS    0H                  ERROR IN ARGUMENT
         C     #13,8(,#1)          IF ARG 3 IS UNSPECIFIED
         BE    ERROR                OR
         C     #10,8(,#1)            ARG 3 IS NIL
         BE    ERROR                  THEN SIGNAL AN ERROR
         LR    #15,#10             ELSE RETURN NIL
         BR    #14
         SPACE 1
ERROR    DS    0H
         L     #2,0(,#1)
         #ERR  'ZIL4012 GETK: Improper keyword list specified - ',     X
               (#2),PREFIX=NO
         SPACE 1
         LTORG
         END
