         TITLE 'GETCHARN - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* GETCHARN TAKES TWO ARGUMENTS - A STRING AND A FIXNUM.               *
*                                                                     *
* IT RETURNS A FIXNUM.  INDEXING IS ZERO-BASED, UNLIKE CHAR.          *
*                                                                     *
* This is a Maclisp function, not a Common Lisp function.             *
*                                                                     *
***********************************************************************
GETCHARN #ZBEG MINARGS=2,MAXARGS=2,NAME='GETCHARN'
         SPACE
         LM    #6,#7,0(#1)         LOAD ARGS
         CLI   ##TYPE(#6),##STRING IF ARGUMENT IS A STRING
         BE    ARG1OK              THEN OK
         CLI   ##TYPE(#6),##SYMBOL IF ARGUMENT IS A SYMBOL
         BNE   ERROR1              THEN
         L     #6,##PNAME(,#6)      TAKE THE PRINT NAME, ELSE ERROR
ARG1OK   DS    0H
         CLI   ##TYPE(#7),##FIXNUM IF ARG 2 IS NOT A FIXNUM
         BNE   ERROR2              THEN ERROR
         ICM   #5,15,##CDR(#7)     GET FIXNUM VALUE
         BNP   RET00               IF ZERO OR NEGATIVE, RETURN X'00'
         XR    #3,#3               CLEAR INSERT REG
         ICM   #3,7,1(#6)          GET STRING LENGTH
         CR    #5,#3               IF INDEX EXCEEDS LENGTH
         BH    RET00               THEN RETURN X'00'
         XR    #2,#2               CLEAR INSERT REG
         IC    #2,##VECDAT-1(#5,#6) GET STRING CHARACTER INTO REGISTER
         SLA   #2,3                Convert to index into fixnum table
         DROP  #15
         LA    #15,#SFT(#2)        Point to entry in fixnum table
         BR    #14
         SPACE 1
RET00    DS    0H                  INDEX OUT OF BOUNDS
         LA    #15,#SFT0           RETURN X'00'
         BR    #14
         SPACE 1
         USING GETCHARN,#2
ERROR1   LR    #2,#15
         #ERR  'Argument 1 to GETCHARN not a string - ',(#6)
ERROR2   LR    #2,#15
         #ERR  'Argument 2 to GETCHARN not a fixnum - ',(#7)
         SPACE 1
         LTORG
         END
