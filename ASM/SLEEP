         TITLE 'SLEEP - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*
* SLEEP PUTS ZIL TO SLEEP FOR THE SPECIFIED NUMBER OF SECONDS.
*
***********************************************************************
SLEEP    #ZBEG MINARGS=1,MAXARGS=1,NAME='SLEEP'
         #ZPDS 16                  DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         L     #2,0(,#11)          GET ARG 1
         CLI   ##TYPE(#2),##FIXNUM IF ARG 1 IS NOT A FIXNUM
         BNE   ERROR               THEN ERROR
         ICM   #3,15,##CDR(#2)     ELSE GET VALUE
         BM    ERROR
         MH    #3,=H'100'          CONVERT TO HUNDREDTHS OF A SECOND
         ST    #3,TIME             STORE TIME VALUE FOR STIMER
         SPACE
         STIMER WAIT,BINTVL=TIME   GO TO SLEEP FOR N SECONDS
         SPACE
         LR    #15,#10             RETURN NIL
         SPACE
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE
ERROR    #ERR  'Argument to SLEEP not a positive fixnum - ',0(#11)
         SPACE
TIME     DS    F
         SPACE
         LTORG
         SPACE
         END
