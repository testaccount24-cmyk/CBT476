         TITLE 'ZMSTRUCT - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE MAKES A STRUCTURE OUT OF A LIST.  THE SIZE OF THE   *
* STRUCT IS THE LENGTH OF THE LIST.                                   *
*                                                                     *
* IT TAKES ONE REQUIRED ARGUMENT - THE LIST.                          *
*                                                                     *
* NOTE:  THE LIST MUST BE NON-NULL AND ITS CAR MUST BE A SYMBOL.      *
*                                                                     *
***********************************************************************
         SPACE 1
ZMSTRUCT #ZBEG MINARGS=1,MAXARGS=1,NAME='ZMSTRUCT'
         SPACE 1
         #ZPDS 1*4+12+0 = 16
         #ZSAV ARGS=1
         SPACE 1
         L     #1,0(,#11)          LOAD ARG1 = THE LIST
         TM    ##TYPE(#1),##ATOM   IF LIST IS NIL OR ATOMIC
         BO    ERROR1              THEN ERROR
         L     #2,##CAR(,#1)       GET CAR OF LIST
         CLI   ##TYPE(#2),##SYMBOL IF NOT A SYMBOL
         BNE   ERROR3              THEN ERROR
         XR    #7,#7               INITIALIZE LENGTH TO ZERO
LENLOOP  DS    0H                  LOOP TO DETERMINE LENGTH OF LIST
         TM    ##TYPE(#1),##ATOM   IF ATOM OR IMPROPER LIST
         BO    ERROR1              THEN ERROR.  ELSE...
         LA    #7,1(,#7)           INCREMENT COUNT
         L     #1,##CDR(,#1)       GET NEXT CDR OF LIST
         CR    #1,#10              UNTIL END OF LIST
         BNE   LENLOOP             CONTINUE
GOTLEN   DS    0H                  #7 = NUMBER OF ELEMENTS IN LIST
         SLA   #7,2                MULTIPLY BY 4 FOR STRUCT LENGTH
         LR    #2,#7               SET LENGTH FOR ZILVSS
         XR    #1,#1               ENTRY CODE 0 = ALLOCATE NEW SPACE
         L     #15,ZLCVSS          GET ADDRESS OF STRING SPACE SERVICE
         LA    #0,16(,#11)         SET CURRENT STACK LOCATION
         #BASR #14,#15             CALL ZILVSS TO BUILD THE OBJECT
         LTR   #4,#4               IF STRUCT BUILD FAILED
         BZ    ERROR4              THEN ERROR
         MVI   ##TYPE(#15),##STRUCT SET OBJECT TYPE = STRUCT
         L     #2,0(,#11)          GET ARG 1 (THE LIST)
         LA    #6,4                SET INCREMENT REGISTER FOR BXLE
         BCTR  #7,0                SET TERMINATION REGISTER FOR BXLE
         XR    #1,#1               SET INDEX REGISTER
INITLOOP L     #0,##CAR(,#2)       GET EACH ELEMENT OF LIST
         ST    #0,0(#1,#4)         INITIALIZE EACH ELEMENT IN STRUCT
         L     #2,##CDR(,#2)       GET NEXT LIST ELEMENT
         BXLE  #1,#6,INITLOOP
         SPACE 1
RETURN   DS    0H                  #15 POINTS TO STRUCT BUILT
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   #ERR  'Argument to ZMSTRUCT not a non-null proper list - ',   X
               0(#11)
ERROR3   #ERR  'First element of structure to be allocated not a symbolX
                - ',0(#11)
ERROR4   #ERR  PREFIX=NO,'ZIL4010 ZMSTRUCT: Not enough vector/string spX
               ace to build structure.'
         SPACE 1
         LTORG
         SPACE 1
         END
