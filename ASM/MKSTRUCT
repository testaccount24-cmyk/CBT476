         TITLE 'MKSTRUCT - ZIL 1.3 INTERNAL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE MAKES A STRUCTURE OF A REQUESTED SIZE.              *
*                                                                     *
* IT TAKES TWO REQUIRED ARGUMENTS: A FIXNUM SPECIFYING THE NUMBER OF  *
* ELEMENTS IN THE STRUCTURE, AND A SYMBOL IDENTIFYING THE TYPE OF     *
* THE STRUCTURE.                                                      *
*                                                                     *
* ALL ELEMENTS IN THE STRUCTURE ARE INITIALIZED TO NIL.               *
*                                                                     *
* COMPARE ZMSTRUCT, WHICH TRANSFORMS A LIST INTO A STRUCTURE.         *
*                                                                     *
***********************************************************************
         SPACE 1
MKSTRUCT #ZBEG MINARGS=2,MAXARGS=2,NAME='MKSTRUCT'
         SPACE 1
         #ZPDS 2*4+12+0
         #ZSAV ,
         SPACE 1
         LM    #1,#2,0(#11)        ARG1 = LENGTH, ARG2 = TYPE
         CLI   ##TYPE(#1),##FIXNUM IF ARG1 IS NOT A FIXNUM
         BNE   ERROR1              THEN ERROR.  ELSE...
         CLI   ##TYPE(#2),##SYMBOL IF ARG2 IS NOT A SYMBOL
         BNE   ERROR2              THEN ERROR.  ELSE...
         ICM   #7,15,##CDR(#1)     GET VALUE OF FIXNUM INTO REGISTER
         BNP   ERROR1              IF NOT POSITIVE, ERROR
         SLA   #7,2                MULTIPLY BY 4 FOR VECTOR LENGTH
         LR    #2,#7               SET LENGTH TO ALLOCATE
         XR    #1,#1               ENTRY CODE 0 = ALLOCATE NEW SPACE
         L     #15,ZLCVSS          GET ADDRESS OF STRING SPACE SERVICE
         LA    #0,20(,#11)         SET CURRENT STACK LOCATION
         #BASR #14,#15             CALL ZILVSS TO BUILD THE OBJECT
         LTR   #4,#4               IF STRING BUILD FAILED
         BZ    ERROR3              THEN ERROR
         MVI   ##TYPE(#15),##STRUCT SET OBJECT TYPE = STRUCTURE
         L     #2,4(,#11)          GET ARG 2 (TYPE OF STRUCTURE)
         ST    #2,0(,#4)           SET STRUCTURE TYPE
         LA    #6,4                SET INCREMENT REGISTER FOR BXLE
         SH    #7,=H'5'            SET TERMINATION REGISTER FOR BXLE
         XR    #1,#1               SET INDEX REGISTER
INITLOOP ST    #10,4(#1,#4)        INITIALIZE EACH ELEMENT TO NIL
         BXLE  #1,#6,INITLOOP
         SPACE 1
RETURN   DS    0H                  #15 POINTS TO STRUCTURE BUILT
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   #ERR  'Arg 1 to MKSTRUCT not a positive fixnum - ',0(#11)
ERROR2   #ERR  'Arg 2 to MKSTRUCT not a symbol - ',4(#11)
ERROR3   #ERR  'ZIL4010 MKSTRUCT: Not enough vector/string space to buiX
               ld structure ',4(#11),'.',PREFIX=NO
         SPACE 1
         LTORG
         SPACE 1
         END
