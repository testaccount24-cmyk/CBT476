         TITLE 'MKVECTOR - ZIL 1.3 INTERNAL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS SUBROUTINE MAKES A VECTOR OF A REQUESTED SIZE.                 *
*                                                                     *
* IT TAKES ONE REQUIRED ARGUMENT - A FIXNUM SPECIFYING THE NUMBER OF  *
* ELEMENTS IN THE VECTOR - AND ONE OPTIONAL ARGUMENT - THE VALUE TO   *
* INITIALIZE THE SLOTS TO, DEFAULT BEING NIL.                         *
*                                                                     *
* COMPARE ZMVECTOR, WHICH TRANSFORMS A LIST INTO A VECTOR.            *
*                                                                     *
***********************************************************************
*                                                                     *
* Note: It might be desirable to have MAKE-VECTOR be a LISP function  *
*       that takes a keyword argument, much like MAKE-LIST.           *
*                                                                     *
***********************************************************************
         SPACE 1
MKVECTOR #ZBEG MINARGS=1,MAXARGS=2,NAME='MKVECTOR'
         SPACE 1
         #ZPDS 2*4+12+0
         #ZSAV ,
         SPACE 1
         L     #7,0(,#11)          LOAD ARG1 = THE REQUESTED LENGTH
         CLI   ##TYPE(#7),##FIXNUM IF ARG1 IS NOT A FIXNUM
         BNE   ERROR1              THEN ERROR.  ELSE...
         L     #7,##CDR(,#7)       GET VALUE OF FIXNUM INTO REGISTER
         SLA   #7,2                MULTIPLY BY 4 FOR VECTOR LENGTH
         LR    #2,#7               SET LENGTH TO ALLOCATE
         XR    #1,#1               ENTRY CODE 0 = ALLOCATE NEW SPACE
         L     #15,ZLCVSS          GET ADDRESS OF STRING SPACE SERVICE
         LA    #0,20(,#11)         SET CURRENT STACK LOCATION
         #BASR #14,#15             CALL ZILVSS TO BUILD THE OBJECT
         LTR   #4,#4               IF STRING BUILD FAILED
         BZ    ERROR2              THEN ERROR
         MVI   ##TYPE(#15),##VECTOR SET OBJECT TYPE = VECTOR
         L     #2,4(,#11)          GET ARG 2 (INITIAL VALUE OF VECTOR)
         SPACE 1
         CR    #2,#13              IF ARG 2 NOT SUPPLIED
         BNE   *+6                 THEN
         LR    #2,#10               SET ARG 2 TO NIL
         SPACE 1
         LA    #6,4                SET INCREMENT REGISTER FOR BXLE
         BCTR  #7,0                SET TERMINATION REGISTER FOR BXLE
         XR    #1,#1               SET INDEX REGISTER
INITLOOP ST    #2,0(#1,#4)         INITIALIZE EACH ELEMENT TO NIL
         BXLE  #1,#6,INITLOOP
         SPACE 1
RETURN   DS    0H                  #15 POINTS TO VECTOR BUILT
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   #ERR  'Argument to MKVECTOR not a fixnum - ',0(#11)
ERROR2   #ERR  'ZIL4010 MKVECTOR: Not enough vector/string space to buiX
               ld vector.',PREFIX=NO
         SPACE 1
         LTORG
         SPACE 1
         END
