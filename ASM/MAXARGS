         TITLE 'MAXARGS - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS MODULE RETURNS THE MAXIMUM NUMBER OF ARGUMENTS DEFINED FOR A   *
* COMPILED FUNCTION, PRIMARILY FOR THE EVALUATOR/INTERPRETER.         *
* THE INPUT PARAMETER MUST BE CODE, I.E. THE VALUE OF THE ZILLOAD     *
* FUNCTION OR OF THE SUBR, FSUBR, ETC. PROPERTY OF A FUNCTION ATOM.   *
*                                                                     *
***********************************************************************
         SPACE
MAXARGS  #ZBEG MINARGS=1,MAXARGS=1,NAME='MAXARGS'
         SPACE
         L     #6,0(,#1)           PICK UP PARAMETER (FUNCTION CODE)
         TM    ##TYPE(#6),##CODE   MUST BE A COMPILED CODE OBJECT
         BNO   ERROR               (SUBR OR CLOSURE)
         XR    #4,#4               ELSE CLEAR INSERT REGISTER
         XR    #2,#2               CLEAR INSERT REG
         ICM   #4,7,##CAR+1(#6)    GET POINTER TO COMPILED CODE
         IC    #2,4(,#4)           GET LENGTH OF IDENTIFIER
         LA    #3,5+3(#2,#4)       POINT TO MODULE CODE PAST IDENTIFIER
         N     #3,=X'FFFFFFFC'     ROUNDING UP TO NEXT FULLWORD
         USING ZINT,#3             TO POINT TO "ZINT" AREA
         IC    #2,ZINMAXAR         PICK UP MAXARGS VALUE
         DROP  #15
         SLA   #2,3                Convert to index into fixnum table
         LA    #15,#SFT(#2)        Point to fixnum in table
         BR    #14
         SPACE 1
ERROR    DS    0H
         LR    #2,#15
         USING MAXARGS,#2
         #ERR  'Argument to MAXARGS not a compiled code object (SUBR orX
                closure) - ',(#6)
         SPACE 1
         LTORG
         SPACE 1
         #ZINT ,                   MAP THE CODE HEADER AREA
         END
