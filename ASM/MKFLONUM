         TITLE 'MKFLONUM - BUILT-IN ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*
* MKFLONUM TAKES A FIXNUM AND MAKES A FLONUM OUT OF IT.
* THE NUMBER IS *NOT* CONVERTED; RATHER, THE CONTENTS OF THE FIXNUM
* ARE STUCK INTO THE FLONUM PART.  THIS FUNCTION MAY BE USED TO
* GENERATE SPECIFIC KNOWN-HEX-CONTENT FLONUMS.
*
* IF ONE ARG IS GIVEN, A SINGLE-FLOAT IS CREATED.
* IF 2 ARGS ARE GIVEN, A DOUBLE-FLOAT IS CREATED.
*
***********************************************************************
MKFLONUM #ZBEG MINARGS=1,MAXARGS=2,NAME='MAKE-FLONUM'
         #ZPDS 2*4+12+0 (= 20)     DEFINE STACK SIZE
         #ZSAV ,                   SAVE REGISTERS
         SPACE
         LM    #1,#2,0(#11)        LOAD ARGS 1 AND 2
         CLI   ##TYPE(#1),##FIXNUM ARG MUST BE A FIXNUM
         BNE   ERROR1              ELSE ERROR
         CR    #2,#13              IF ARG2 IS OMITTED
         BE    MKSINGLE            THEN MAKE A SINGLE-FLOAT
         CR    #2,#10              IF ARG2 IS NIL
         BE    MKSINGLE            THEN MAKE A SINGLE-FLOAT
         CLI   ##TYPE(#2),##FIXNUM IF ARG2 IS SUPPLIED BUT NOT A FIXNUM
         BNE   ERROR2              THEN ERROR
         B     MKDOUBLE            ELSE MAKE A DOUBLE-FLOAT
         SPACE 1
MKSINGLE DS    0H                  MAKE A SINGLE-FLOAT
         SPACE 1
         LE    #F2,##CDR(,#1)      GET FIXNUM PART AS FLONUM
         AE    #F2,=E'0'           NORMALIZE
         STE   #F2,ZLCWORK         STORE
         L     #2,ZLCWORK          LOAD INTO REG FOR CONSING
         CL    #2,##CDR(,#1)       IF BITS DON'T MATCH
         BE    OK                  THEN
*                                   WARN USER NUMBER GOT NORMALIZED
         #ERR  1,'MAKE-FLONUM is normalizing the value generated by itsX
                argument - ',0(#11)
OK       DS    0H
         LA    #0,20(,#11)         PASS CURRENT STACK LOCATION TO CONS
         #MKAT FLOAT               MAKE A FLONUM
         B     RETURN
         SPACE 1
MKDOUBLE DS    0H                  MAKE A DOUBLE-FLOAT
         SPACE 1
         L     #3,##CDR(,#1)       FIXNUM VALUE 1
         L     #4,##CDR(,#2)       FIXNUM VALUE 2
         STM   #3,#4,ZLCWORK       STORE TWO WORDS
         SDR   #F0,#F0             NORMALIZE WHEN WE...
         AD    #F0,ZLCWORK         LOAD AS A FLONUM
         STD   #F0,TEMP            STORE
         CLC   TEMP(8),ZLCWORK     IF BITS DON'T MATCH
         BE    OK2                 THEN
*                                   WARN USER NUMBER GOT NORMALIZED
         #ERR  1,'MAKE-FLONUM is normalizing the value generated by itsX
                arguments - ',0(#11),' and ',4(#11)
OK2      DS    0H
         LA    #0,20(,#11)         PASS CURRENT STACK LOCATION TO CONS
         L     #15,ZLCMDF          GET ADDRESS OF ZILMDF
         #BASR #14,#15             CALL ZILMDF TO MAKE A DOUBLE-FLOAT
******** B     RETURN
         SPACE 1
RETURN   DS    0H
         SPACE 1
         #ZRET ,                   RESTORE REGISTERS AND RETURN
         SPACE 1
TEMP     DS    D
         SPACE 1
ERROR1   DS    0H
         #ERR  'Argument 1 to MAKE-FLONUM not a fixnum - ',0(#11)
         SPACE 1
ERROR2   DS    0H
         #ERR  'Argument 2 to MAKE-FLONUM not a fixnum - ',4(#11)
         SPACE 1
         LTORG
         SPACE 1
         END
