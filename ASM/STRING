         TITLE 'STRING - ZIL 1.3 SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* This is the Common Lisp STRING function (CLtL p. 304).              *
*                                                                     *
* (string x)                                                          *
*                                                                     *
* If x is a string, it is returned.                                   *
* If x is a symbol, its print name is returned.                       *
* If x is a string character (a fixnum between 0 and 255 in ZIL),     *
* then a string containing that one character is returned.            *
* In any other situation, an error is signalled.                      *
*                                                                     *
***********************************************************************
         SPACE 1
STRING   #ZBEG MINARGS=1,MAXARGS=1,NAME='STRING'
         SPACE 1
         #ZPDS 1*4+12+0
         #ZSAV ,
         SPACE 1
         L     #15,0(,#11)         Load arg1
         SPACE 1
         CLI   ##TYPE(#15),##STRING  If arg1 is a string
         BE    RETURN                then just return it
         CLI   ##TYPE(#15),##SYMBOL  If arg1 is a symbol
         BE    SYMBOL                then process symbol
         CLI   ##TYPE(#15),##FIXNUM  If arg1 is a fixnum
         BNE   ERROR1                then
         L     #5,##CDR(,#15)        get numeric value
         CL    #5,#SFW255            If not 1-byte character fixnum
         BH    ERROR1                then error
DUMPST   DS    0H                    else dump into 1-character string
         LA    #2,1                Get length of string to create
         LA    #6,##CDR+3(#15)     Get address of character to dump
         XR    #1,#1               Entry code 0 = allocate new space
         L     #15,ZLCVSS          Get address of ZILVSS
         LA    #0,16(,#11)         Set current stack pointer
         #BASR #14,#15             Call ZILVSS to build the string
         LTR   #4,#4               If string build failed
         BZ    ERROR2              then error
         STC   #5,##VECDAT(,#15)   Store character into string
         B     RETURN
         SPACE 1
SYMBOL   DS    0H                  Here if arg1 is a symbol
         L     #15,##PNAME(,#15)   Get the print name
*******  B     RETURN              and return that
         SPACE 1
RETURN   DS    0H
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   LR    #2,#15
         #ERR  'Argument to STRING not a string, symbol or character - X
               ',0(#11)
         SPACE 1
ERROR2   #ERR  'ZIL4010 STRING: Not enough vector/string space to buildX
                string.',PREFIX=NO
         SPACE 1
         LTORG
         SPACE 1
         END
