         TITLE 'VCOPY - ZIL 1.3 INTERNAL SUBROUTINE'
***********************************************************************
*                                                                     *
* ZIL Version 1.3 (c) 1989 The Charles Stark Draper Laboratory Inc.   *
*                     All rights reserved.                            *
*                                                                     *
***********************************************************************
*                                                                     *
* This function copies a vector-type object.  Syntax:                 *
*                                                                     *
*  (VCOPY arg1)                                                       *
*                                                                     *
* The one required argument is a vector-type object to be copied.     *
*                                                                     *
***********************************************************************
         SPACE 1
VCOPY    #ZBEG MINARGS=1,MAXARGS=1,NAME='VCOPY'
         SPACE 1
         #ZPDS 1*4+12+0
         #ZSAV ,
         SPACE 1
         L     #7,0(,#11)          Get arg1
         TM    ##TYPE(#7),##VEC    If arg1 is not a vectorish object
         BNO   ERROR1              then error
         XR    #2,#2               Clear insert register
         ICM   #2,7,1(#7)          Get length of current object
         XR    #1,#1               Entry code 0 = allocate new space
         L     #15,ZLCVSS          Get address of string space service
         LA    #0,16(,#11)         Set current stack location
         #BASR #14,#15             Call ZILVSS to build new vec-object
         LTR   #4,#4               If vector build failed
         BZ    ERROR2              then error
         LR    #0,#15              Get address of new object
         L     #2,0(,#11)          Get address of old object from stack
         XR    #3,#3               (in case it got moved by GC)
         ICM   #3,7,1(#2)          Get length of old object
         LA    #3,4(,#3)           Add prefix length
         LR    #1,#3               Same as length of new object
         MVCL  #0,#2               Copy old object to new object
RETURN   DS    0H                  #15 points to object built
         SPACE 1
         #ZRET ,
         SPACE 1
ERROR1   #ERR  'Argument to VCOPY not a vector or structure - ',0(#11)
ERROR2   #ERR  'ZIL4010 VCOPY: Not enough vector/string space to copy vX
               ector.',PREFIX=NO
         SPACE 1
         LTORG
         SPACE 1
         END
