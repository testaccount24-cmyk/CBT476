        PROC 0 XTRACE
        /**********************************************************************
        /*                                                                    *
        /* ZIL VERSION 1.3 - SEB1525 - 02/88                                  *
        /*                                                                    *
        /* ISPF ZIL option 8                                                  *
        /*                                                                    *
        /**********************************************************************
        IF &XTRACE = XTRACE THEN DO
         CONTROL CONLIST SYMLIST LIST
         ISPEXEC CONTROL DISPLAY LINE
        END
        CONTROL NOFLUSH NOCAPS
        SET CURSOR = XZIL8FN
        LOOP:+
        CONTROL NOPROMPT
        ISPEXEC CONTROL DISPLAY REFRESH
        ISPEXEC DISPLAY PANEL(XZIL8) CURSOR(&CURSOR)
        IF &LASTCC ¬= 0 THEN GOTO EXIT
        IF &XTRACE = XTRACE THEN ISPEXEC CONTROL DISPLAY LINE
        SET SYSDVAL = &STR(&XZIL8FL)
        READDVAL XZZZ8FL
        %XZCHECK '&XZZZ8FL(&XZIL8FN)' SETMSG
        SET CC = &LASTCC
        IF &CC ¬= 0 THEN DO
         IF &CC = 4 THEN SET CURSOR = XZIL8FN
         ELSE SET CURSOR = XZIL8FL
         GOTO LOOP
        END
        SET SYSDVAL = &STR(&XZIL8PL)
        READDVAL XZZZ8PL
        %XZCHECK '&XZZZ8PL(&XZIL8PN)' SETMSG
        SET CC = &LASTCC
        IF &CC ¬= 0 THEN DO
         IF &CC = 4 THEN SET CURSOR = XZIL8PN
         ELSE SET CURSOR = XZIL8PL
         GOTO LOOP
        END
        ALLOC F(ZFUNLIB) SHR REU DA('&XZZZ8FL')
        IF &LASTCC ¬= 0 THEN DO
         SET CURSOR = XZIL8FL
         GOTO LOOP
        END
        ALLOC F(SYSLMOD) SHR REU DA('&XZZZ8PL')
        IF &LASTCC ¬= 0 THEN DO
         SET CURSOR = XZIL8FL
         GOTO LOOP
        END
        ISPEXEC CONTROL DISPLAY LINE START(1)
        CONTROL PROMPT
        IF &STR(&XZIL8PN) = ZILSUBS THEN DO
         LINK * +
              LOAD('&XZZZ8PL') +
              PRINT(*) +
              LIST MAP LET TEST
         DATA PROMPT
         ORDER &XZIL8PN,&XZIL8FN
         INCLUDE ZFUNLIB(&XZIL8FN)
         INCLUDE SYSLMOD(&XZIL8PN)
         NAME &XZIL8PN(R)
        &STR()
         ENDDATA
        END
        ELSE DO
         LINK * +
              LOAD('&XZZZ8PL') +
              PRINT(*) +
              LIST LET /* MAP */
         DATA PROMPT
         ORDER ZILMAIN,&XZIL8PN,&XZIL8FN
         INCLUDE ZFUNLIB(&XZIL8FN)
         INCLUDE SYSLMOD(&XZIL8PN)
         ENTRY ZILMAIN
         NAME &XZIL8PN(R)
        &STR()
         ENDDATA
        END
        SET XZILCC = &LASTCC
        IF &XZILCC > 0 THEN WRITE Possible linkedit error, RC=&XZILCC
        WRITE Load module &XZIL8PN replaced in '&XZZZ8PL'.
        GOTO LOOP
        ERROR:+
        ISPEXEC SETMSG MSG(XZIL005)
        GOTO LOOP
        EXIT:+
        EXIT
