        PROC 0 XTRACE
        /**********************************************************************
        /*                                                                    *
        /* ZIL VERSION 1.3 - SEB1525 - 02/88                                  *
        /*                                                                    *
        /* ISPF ZIL option 2                                                  *
        /*                                                                    *
        /**********************************************************************
        IF &XTRACE = XTRACE THEN DO
         CONTROL CONLIST SYMLIST LIST
         ISPEXEC CONTROL DISPLAY LINE
        END
        SET CURSOR = XZILMEM
        LOOP:+
        ISPEXEC CONTROL DISPLAY REFRESH
        ISPEXEC DISPLAY PANEL(XZIL2) CURSOR(&CURSOR)
        IF &LASTCC ¬= 0 THEN GOTO EXIT
        IF &XTRACE = XTRACE THEN ISPEXEC CONTROL DISPLAY LINE
        SET DSLEN = &LENGTH(&NRSTR(&XZILSRC))
        IF &SUBSTR(1,&NRSTR(&XZILSRC))&SUBSTR(&DSLEN,&NRSTR(&XZILSRC)) = '' +
         THEN SET XZZZSRC = &SUBSTR(2:&DSLEN-1,&NRSTR(&XZILSRC))
        ELSE SET XZZZSRC = &NRSTR(&SYSPREF..&XZILSRC)
        LISTDSI '&XZZZSRC' NORECALL
        IF &SYSREASON = 9 /* migrated */ THEN DO
         SET MIGDSN = &STR('&XZZZSRC')
         ISPEXEC DISPLAY PANEL(XZCKMIG)
         IF &LASTCC ¬= 0 THEN GOTO LOOP
        END
        IF &STR(&XZILMEM) = THEN SET EDITDS = '&XZZZSRC'
        ELSE SET EDITDS = '&XZZZSRC(&XZILMEM)'
        ISPEXEC CONTROL ERRORS RETURN
        ISPEXEC EDIT DATASET(&EDITDS)
        SELECT &LASTCC
         WHEN (0)  DO
                    ISPEXEC SETMSG MSG(XZIL016)
                   END
         WHEN (4)  DO
                   END
         OTHERWISE DO
                    ISPEXEC SETMSG MSG(&ZERRMSG)
                   END
        END
        SET CURSOR = XZILMEM
        ISPEXEC CONTROL ERRORS CANCEL
        GOTO LOOP
        EXIT:+
        EXIT
