        PROC 2 FILENAME DATASETNAME REUSE RELEASE NEW OLD MOD NOINFORM XTRACE
        /**********************************************************************
        /*                                                                    *
        /* ZIL VERSION 1.2 - SEB1525 - 03/86                                  *
        /*                                                                    *
        /* This CLIST allocates a ZIL output file.                            *
        /* The DD name is ZILOxxxx, where xxxx is the file name.              *
        /*                                                                    *
        /**********************************************************************
        IF &XTRACE = XTRACE THEN CONTROL CONLIST SYMLIST LIST
        CONTROL NOFLUSH NOCAPS
        IF &LENGTH(&STR(&FILENAME)) > 4 THEN DO
         WRITE XZOFILE Error: File name must not exceed 4 characters.
         EXIT C(12) Q
        END
        IF &STR(&FILENAME) = NIL +
         | &STR(&FILENAME) = T +
         | &DATATYPE(&STR(&FILENAME)) = NUM THEN DO
         WRITE XZOFILE Error: "&FILENAME" is not a valid file name.
         EXIT C(12) Q
        END
        IF      &NEW&MOD&OLD = NEW THEN SET DISPOSITION = NEW
        ELSE IF &NEW&MOD&OLD = MOD THEN SET DISPOSITION = MOD
        ELSE IF &NEW&MOD&OLD = OLD THEN SET DISPOSITION = OLD
        ELSE DO
         SET STAT = SYSDSN(&DATASETNAME)
         IF &STR(&STAT) = DATASET NOT FOUND THEN SET DISPOSITION = NEW
         ELSE SET DISPOSITION = OLD
        END
        IF &DISPOSITION = NEW | &DISPOSITION = MOD THEN +
         SET OTHERPARMS = &STR(RECFM(F B) LRECL(80) BL(800) SP(10 10))
        ELSE SET OTHERPARMS =
        ALLOC FI(ZILO&FILENAME) +
              DA(&DATASETNAME) +
              &DISPOSITION +
              &OTHERPARMS +
              &RELEASE +
              &REUSE
        IF &LASTCC = 0 THEN DO
         IF &NOINFORM = THEN +
          WRITE XZOFILE: Output file &FILENAME allocated to data set &DATASETNAME..
        END
        ELSE DO
         WRITE XZOFILE Error: Output file &FILENAME allocation failed.
         EXIT C(12) Q
        END
