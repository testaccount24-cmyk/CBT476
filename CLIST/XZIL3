        PROC 0 XTRACE
        /**********************************************************************
        /*                                                                    *
        /* ZIL VERSION 1.3 - SEB1525 - 02/88                                  *
        /*                                                                    *
        /* ISPF ZIL option 3                                                  *
        /*                                                                    *
        /**********************************************************************
        IF &XTRACE = XTRACE THEN DO
         CONTROL CONLIST SYMLIST LIST
         ISPEXEC CONTROL DISPLAY LINE
        END
        CONTROL NOFLUSH NOCAPS
        /* Configuration section. */
        SET HELDSYSOUTCLASS = H
        SET EXECPGM_IS_AVAILABLE = N
        /* End configuration section. */
        SET CURSOR = XZILINIT
        LOOP:+
        ISPEXEC CONTROL ERRORS CANCEL
        ISPEXEC CONTROL DISPLAY REFRESH
        ISPEXEC DISPLAY PANEL(XZIL3) CURSOR(&CURSOR)
        IF &LASTCC ¬= 0 THEN GOTO EXIT
        IF &XTRACE = XTRACE THEN ISPEXEC CONTROL DISPLAY LINE
        IF &SUBSTR(1,&ZCMD ) = E THEN DO
         SET EDITDS = &SYSNSUB(2,&&&XZILCUR)
         IF &STR(&EDITDS) = THEN DO
          ISPEXEC SETMSG MSG(XZIL013)
          SET CURSOR = &XZILCUR
          GOTO LOOP
         END
         ISPEXEC CONTROL ERRORS RETURN
         ISPEXEC EDIT DATASET(&EDITDS)
         SELECT &LASTCC
          WHEN (0)  DO
                     ISPEXEC SETMSG MSG(XZIL016)
                    END
          WHEN (4)  DO
                    END
          OTHERWISE DO
                     ISPEXEC SETMSG MSG(&ZERRMSG)
                     SET CURSOR = &XZILCUR
                     GOTO LOOP
                    END
         END
         SET CURSOR = &XZILCUR
         SET ZCMD =
         GOTO LOOP
        END

        IF &STR(&XZILSUB1 &XZILSUB2 &XZILSUB3 &XZILSUB4) NE THEN DO
         IF &EXECPGM_IS_AVAILABLE = N THEN DO
          ISPEXEC SETMSG MSG(XZIL020)
          SET CURSOR = XZILSUB1
          GOTO LOOP
         END
         SET SUBMETHOD = EXECPGM
        END
        ELSE SET SUBMETHOD =

        IF &STR(&XZILACSZ) ¬= THEN SET ACSIZE = &STR(A=&XZILACSZ)
        ELSE SET ACSIZE =
        IF &STR(&XZILCSSZ) ¬= THEN SET CSSIZE = &STR(C=&XZILCSSZ)
        ELSE SET CSSIZE =
        IF &STR(&XZILPDSZ) ¬= THEN SET PDSIZE = &STR(P=&XZILPDSZ)
        ELSE SET PDSIZE =
        IF &STR(&XZILVSSZ) ¬= THEN SET VSSIZE = &STR(V=&XZILVSSZ)
        ELSE SET VSSIZE =

        IF &NRSTR(&XZILINIT) ¬= THEN DO
         ALLOC F(ZILIINIT) SHR REU DA(&XZILINIT)
         IF &LASTCC ¬= 0 THEN GOTO ERROR
        END
        ELSE DO
         IF &XTRACE = THEN CONTROL NOMSG
         FREE F(ZILIINIT)
         CONTROL MSG
        END
        IF &NRSTR(&XZILSODS) ¬= THEN DO
         ALLOC F(ZILDRIBL) MOD REU DA(&XZILSODS) T SP(50 50)
         IF &LASTCC ¬= 0 THEN GOTO ERROR
         SET DRIBBLE = Y
        END
        ELSE IF &XZILSOSC ¬= THEN DO
         IF &XZILSOSC = &HELDSYSOUTCLASS THEN SET HOLD = HOLD
         ELSE SET HOLD =
         ALLOC F(ZILDRIBL) SYSOUT(&XZILSOSC) &HOLD REU
         IF &LASTCC ¬= 0 THEN GOTO ERROR
         SET DRIBBLE = Y
        END
        ELSE DO
         IF &XTRACE = THEN CONTROL NOMSG
         FREE F(ZILDRIBL)
         CONTROL MSG
         SET DRIBBLE = N
        END
        ALLOC F(ZILI) DA(*) REU
        IF &LASTCC ¬= 0 THEN GOTO ERROR
        ALLOC F(ZILO) DA(*) REU
        IF &LASTCC ¬= 0 THEN GOTO ERROR
        ISPEXEC CONTROL DISPLAY LINE START(1)
        SET LIBRARY = PROGLIB.ZIL.&XZILSYS.LOAD
        SET ZILINPARMS = &NRSTR(\&ACSIZE,&CSSIZE,&PDSIZE,&VSSIZE\&XZILINIT)
        IF &SUBMETHOD = EXECPGM THEN DO
         ALLOC FI(ZILLIB) SHR REU +
               DA(&XZILSUB1 &XZILSUB2 &XZILSUB3 &XZILSUB4 '&LIBRARY')
         EXECPGM ZILIN PARM('&NRSTR(&ZILINPARMS)') TASKLIB(ZILLIB)
         SET XZILCC = &LASTCC
         FREE FI(ZILLIB)
        END
        ELSE DO
         CALL '&LIBRARY(ZILIN)' '&NRSTR(&ZILINPARMS)'
         SET XZILCC = &LASTCC
        END
        ISPEXEC SETMSG MSG(XZIL004)
        IF &NRSTR(&XZILINIT) ¬= THEN FREE F(ZILIINIT)
        IF &DRIBBLE = Y THEN FREE F(ZILDRIBL)
        GOTO LOOP
        ERROR:+
        ISPEXEC SETMSG MSG(XZIL005)
        GOTO LOOP
        EXIT:+
        EXIT
