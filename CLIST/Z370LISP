        PROC 0 +
             INITFILE() NOINITFILE +
             ACSIZE() CSSIZE() PDSIZE() VSSIZE() +
             DRIBBLE HOLDCLASS() +
             LIBRARY(PROGLIB.ZIL.LOAD) +
             XTRACE
        /*
        /**********************************************************************
        /*                                                                    *
        /*  ZIL version 1.3 - (c) 1989 Charles Stark Draper Laboratory Inc.   *
        /*                                                                    *
        /**********************************************************************
        /*
        IF &XTRACE = XTRACE THEN CONTROL CONLIST SYMLIST LIST
        /*
        SET HELDSYSOUTCLASS = H
        IF &STR(&HOLDCLASS) = THEN SET HOLDCLASS = &STR(&HELDSYSOUTCLASS)
        SET PRESENT_FILES =
        SET MISSING_FILES =
        IF &STR(&INITFILE) = THEN SET NOINITFILE = NOINITFILE
        IF &NOINITFILE = THEN DO
         IF &XTRACE = THEN CONTROL NOMSG
         FREE FI(ZILIINIT)
         CONTROL MSG
         ALLOC FI(ZILIINIT) DA(&INITFILE) SHR
         SET PRESENT_FILES = &PRESENT_FILES ZILIINIT
        END
        ELSE DO
         SET MISSING_FILES = &MISSING_FILES ZILIINIT
        END
        IF &DRIBBLE = DRIBBLE THEN DO
         IF &XTRACE = THEN CONTROL NOMSG
         FREE FI(ZILDRIBL)
         CONTROL MSG
         ALLOC FI(ZILDRIBL) SYSOUT(&HOLDCLASS) HOLD
         SET PRESENT_FILES = &PRESENT_FILES ZILDRIBL
        END
        ELSE DO
         SET MISSING_FILES = &MISSING_FILES ZILDRIBL
        END
        IF &MISSING_FILES ¬= THEN DO
         IF &XTRACE = THEN CONTROL NOMSG
         FREE FI(&MISSING_FILES)
         CONTROL MSG
        END
        /*
        IF &STR(&ACSIZE) ¬= THEN SET ACSIZE = &STR(A=&ACSIZE)
        ELSE SET ACSIZE =
        IF &STR(&CSSIZE) ¬= THEN SET CSSIZE = &STR(C=&CSSIZE)
        ELSE SET CSSIZE =
        IF &STR(&PDSIZE) ¬= THEN SET PDSIZE = &STR(P=&PDSIZE)
        ELSE SET PDSIZE =
        IF &STR(&VSSIZE) ¬= THEN SET VSSIZE = &STR(V=&VSSIZE)
        ELSE SET VSSIZE =
        /*
        CALL '&LIBRARY(ZILIN)' '\&ACSIZE,&CSSIZE,&PDSIZE,&VSSIZE\&INITFILE'
        SET CC = &LASTCC
        IF &CC ¬= 0 THEN WRITE *** LISP INTERPRETER EXITED WITH CODE &CC ***
        IF &PRESENT_FILES ¬= THEN FREE FI(&PRESENT_FILES)
        EXIT
