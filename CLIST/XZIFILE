        PROC 2 FILENAME DATASETNAME REUSE DISPOSITION(SHR) NOINFORM XTRACE
        /**********************************************************************
        /*                                                                    *
        /* ZIL VERSION 1.2 - SEB1525 - 03/86                                  *
        /*                                                                    *
        /* This CLIST allocates a ZIL input file.                             *
        /* The DD name is ZILIxxxx, where xxxx is the file name.              *
        /*                                                                    *
        /**********************************************************************
        IF &XTRACE = XTRACE THEN CONTROL CONLIST SYMLIST LIST
        CONTROL NOFLUSH NOCAPS
        IF &LENGTH(&STR(&FILENAME)) > 4 THEN DO
         WRITE XZIFILE Error: File name must not exceed 4 characters.
         EXIT C(12) Q
        END
        IF &STR(&FILENAME) = NIL +
         | &STR(&FILENAME) = T +
         | &DATATYPE(&STR(&FILENAME)) = NUM THEN DO
         WRITE XZIFILE Error: "&FILENAME" is not a valid file name.
         EXIT C(12) Q
        END
        SET STAT = SYSDSN(&DATASETNAME)
        IF &STR(&STAT) Â¬= OK THEN DO
         WRITE XZIFILE Error: Data set &DATASETNAME is not valid.
         WRITE XZIFILE Reason: &STAT
         EXIT C(12) Q
        END
        ALLOC FI(ZILI&FILENAME) +
              DA(&DATASETNAME) +
              &DISPOSITION +
              &REUSE
        IF &LASTCC = 0 THEN DO
         IF &NOINFORM = THEN +
          WRITE XZIFILE: Input file &FILENAME allocated to data set &DATASETNAME..
        END
        ELSE DO
         WRITE XZIFILE Error: Input file &FILENAME allocation failed.
         EXIT C(12) Q
        END
