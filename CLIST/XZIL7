        PROC 0 XTRACE
        /**********************************************************************
        /*                                                                    *
        /* ZIL VERSION 1.3 - SEB1525 - 02/88                                  *
        /*                                                                    *
        /* ISPF ZIL option 7                                                  *
        /*                                                                    *
        /* 06/05/86 - SEB1525 - Support for compiling modules and EDIT.       *
        /*                                                                    *
        /* 04/14/88 - SEB1525 - Support for additional compiler libraries.    *
        /*                                                                    *
        /**********************************************************************
        IF &XTRACE = XTRACE THEN DO
         CONTROL CONLIST SYMLIST LIST
         ISPEXEC CONTROL DISPLAY LINE
        END
        CONTROL NOFLUSH NOCAPS
        SET CURSOR = XZILMEM
        LOOP:+
        ISPEXEC CONTROL DISPLAY REFRESH
        ISPEXEC DISPLAY PANEL(XZIL7) CURSOR(&CURSOR)
        IF &LASTCC ¬= 0 THEN GOTO EXIT
        IF &XTRACE = XTRACE THEN ISPEXEC CONTROL DISPLAY LINE
        SET SYSDVAL = &STR(&XZILSRC)
        READDVAL XZZZSRC
        IF &XZILSEL = E THEN DO
         IF &STR(&XZILSRC) = THEN DO
          SET CURSOR = XZILSRC
          ISPEXEC SETMSG MSG(XZIL013)
          GOTO LOOP
         END
         ISPEXEC CONTROL ERRORS RETURN
         ISPEXEC EDIT DATASET('&XZZZSRC(&XZILMEM)')
         SELECT &LASTCC
          WHEN (0)  ISPEXEC SETMSG MSG(XZIL015)
          WHEN (4)  /* nothing */
          OTHERWISE ISPEXEC SETMSG MSG(&ZERRMSG)
         END
         ISPEXEC CONTROL ERRORS CANCEL
         GOTO LOOP
        END
        SET LIBRARY = 'PROGLIB.ZIL.&XZILSYS.LOAD'
        IF &STR(&XZILACSZ) ¬= THEN SET ACSIZE = &STR(A=&XZILACSZ)
        ELSE SET ACSIZE =
        IF &STR(&XZILCSSZ) ¬= THEN SET CSSIZE = &STR(C=&XZILCSSZ)
        ELSE SET CSSIZE =
        IF &STR(&XZILPDSZ) ¬= THEN SET PDSIZE = &STR(P=&XZILPDSZ)
        ELSE SET PDSIZE =
        IF &STR(&XZILVSSZ) ¬= THEN SET VSSIZE = &STR(V=&XZILVSSZ)
        ELSE SET VSSIZE =
        SET ISAPROG = &XZILSEL
        IF &STR(&XZILNAM) ¬= THEN SET THENAME = &STR(&XZILNAM)
        ELSE SET THENAME = &STR(&XZILMEM)
        SET PARM = &STR(&ISAPROG&XZILDBG&THENAME)
        IF &STR(&XZILMAC1&XZILMAC2&XZILMAC3&XZILMAC4) = THEN DO
         ALLOC F(ZILICMAC) DUMMY REU
        END
        ELSE DO
         ALLOC F(ZILICMAC) SHR REU DA(&XZILMAC1 &XZILMAC2 &XZILMAC3 &XZILMAC4)
         IF &LASTCC ¬= 0 THEN DO
          SET CURSOR = XZILMAC1
          GOTO ERROR
         END
        END
        ALLOC F(ZILOCTRN) DA(*) REU
        ALLOC F(ZILI) DA(*) REU
        IF &LASTCC ¬= 0 THEN GOTO ERROR
        ALLOC F(ZILO) DA(*) REU
        IF &LASTCC ¬= 0 THEN GOTO ERROR
        ALLOC F(ZILOCODE) DA(*) REU
        IF &LASTCC ¬= 0 THEN GOTO ERROR
        IF &STR(&XZILALL) = THEN DO
         ALLOC F(ZILISRC) DA(*) REU
         IF &LASTCC ¬= 0 THEN DO
          SET CURSOR = XZILSRC
          GOTO ERROR
         END
        END
        ELSE DO
         %XZCHECK '&XZZZSRC(&XZILMEM)' SETMSG
         SET CHKCC = &LASTCC
         IF &CHKCC ¬= 0 THEN DO
          IF &CHKCC = 4 THEN SET CURSOR = XZILMEM
          ELSE SET CURSOR = XZILSRC
          GOTO LOOP
         END
         IF &XTRACE = THEN CONTROL NOMSG
         ALLOC F(ZILISRC) SHR REU DA('&XZZZSRC(&XZILMEM)')
         IF &LASTCC ¬= 0 THEN DO
          CONTROL MSG
          SET CURSOR = XZILMAC1
          GOTO ERROR
         END
         CONTROL MSG
        END
        /* Make XCMD handle attentions
        IF &STR(&XZILSUB1.&XZILSUB2.&XZILSUB3.&XZILSUB4.&XZILRUN) = THEN +
         SET LIBRARY = &STR(&LIBRARY &LIBRARY)
        ISPEXEC CONTROL DISPLAY LINE
        XCMD "ZILCO \&ACSIZE,&CSSIZE,&PDSIZE,&VSSIZE\&PARM" +
             NOFILE +
             LIB(&XZILSUB1 &XZILSUB2 &XZILSUB3 &XZILSUB4 &XZILRUN &LIBRARY)
        SET XZILCC = &LASTCC
        FREE F(ZILISRC ZILICMAC ZILI ZILO ZILOCODE ZILOCTRN)
        ISPEXEC SETMSG MSG(XZIL011)
        GOTO LOOP
        ERROR:+
        ISPEXEC SETMSG MSG(XZIL005)
        GOTO LOOP
        EXIT:+
        EXIT
