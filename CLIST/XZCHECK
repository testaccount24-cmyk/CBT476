        PROC 1 DSNAME SETMSG SMSG() LMSG() NOMEMBER RECALL NORECALL XTRACE
        /*
        /* This CLIST is used by ISPF dialog CLISTs to verify the existence
        /* of a dataset and:
        /*
        /*   If SMSG is specified, return short message in a dialog variable.
        /*   If LMSG is specified, return long message in a dialog variable.
        /*
        /*   If SETMSG is specified, set the ISPF message appropriately.
        /*   Otherwise, display message at user's terminal.
        /*
        /* Automatic recall of migrated datasets is optional.
        /*
        IF &XTRACE = XTRACE THEN CONTROL CONLIST SYMLIST LIST
        CONTROL NOFLUSH NOCAPS
        ERROR DO
         SET CC = &LASTCC
         IF &CC = 12 THEN RETURN
         IF &CC = 20 THEN RETURN
         ERROR OFF
         CONTROL MSG
         IF &CC = 952 THEN -
          WRITE XZCHECK: Data set '&NRSTR(&DSNAME)' is not available.
         ELSE IF &CC = 904 | &CC = 900 THEN +
          WRITE XZCHECK: Invalid syntax in data set name '&NRSTR(&DSNAME)'.
         ELSE -
          WRITE XZCHECK: CLIST error &CC occurred processing '&NRSTR(&DSNAME)'.
         EXIT C(&CC)
        END
        IF &SUBSTR(1,&DSNAME) = ' +
         THEN IF &SUBSTR(&LENGTH(&STR(&DSNAME)),&DSNAME) = ' +
          THEN SET DSNAME = &SUBSTR(2:&LENGTH(&STR(&DSNAME))-1,&DSNAME)
         ELSE SET DSNAME = &STR(&DSNAME)
        ELSE IF &STR(&SYSPREF) NE THEN +
         SET DSNAME = &STR(&SYSPREF..&DSNAME)
        SET MIGRATED = N
        IF &RECALL&NORECALL = RECALL THEN LISTDSI '&DSNAME'
        ELSE LISTDSI '&DSNAME' NORECALL
        SET LISTDSICC = &LASTCC
        IF &LISTDSICC = 16 && &SYSREASON = 0009 THEN DO
         SET MIGRATED = Y
        END
        /**********************************************************************/
        /*                                                                    */
        /* If a member name was specified, extract it and isolate dsname.     */
        /*                                                                    */
        /**********************************************************************/
        SET LP = &SUBSTR(1,())
        SET RP = &SUBSTR(2,())
        IF &SYSINDEX(&LP,&DSNAME) NE 0 THEN DO
         SET I = &LENGTH(&STR(&DSNAME))
         DO WHILE &I > 0
          IF &SUBSTR(&I,&DSNAME) = &STR(&LP) THEN GOTO FOUNDLP
          SET I = &I-1
         END
         FOUNDLP:+
         SET PDSN = &SUBSTR(1:&I-1,&DSNAME)
         SET MEMBER = &SUBSTR(&I+1:&LENGTH(&STR(&DSNAME))-1,&DSNAME)
        END
        ELSE DO
         SET PDSN = &STR(&DSNAME)
         SET MEMBER =
        END
        IF &XTRACE = THEN CONTROL NOMSG
        IF &MIGRATED = Y THEN DO
         SET XCC = 40
         SET SM = &STR(DATASET IS MIGRATED)
         SET LM = &STR(DATA SET '&DSNAME' IS MIGRATED (UNDER HSM CONTROL).)
        END
        ELSE DO
         SET STATUS = &SYSDSN('&DSNAME')
         SELECT &SUBSTR(1:8,&STATUS         )
          WHEN (OK      ) DO
           SET XCC = 0
           IF &SYSNEST = NO THEN +
              WRITE XZCHECK: Data set '&DSNAME' exists and is online.
          END
          WHEN (DATASET ) DO
           SET XCC = 8
           SET SM = &STR(DATASET NOT CATALOGED)
           SET LM = &STR(DATA SET '&DSNAME' NOT IN CATALOG.)
          END
          WHEN (MEMBER N) DO
           SET XCC = 4
           SET SM = &STR(MEMBER NOT FOUND)
           SET LM = &STR(MEMBER &MEMBER NOT IN DATA SET '&PDSN'.)
          END
          WHEN (MEMBER S) DO
           SET XCC = 12
           SET SM = &STR(DATASET NOT PARTITIONED)
           SET LM = &STR(MEMBER &MEMBER SPECIFIED BUT '&PDSN' IS NOT A PDS.)
          END
          WHEN (VOLUME N) DO
           SET XCC = 24
           SET SM = &STR(VOLUME NOT AVAILABLE)
           SET LM = &STR(VOLUME NOT ON SYSTEM AND CANNOT BE ACCESSED.)
          END
          WHEN (UNAVAILA) DO
           SET XCC = 28
           SET SM = &STR(DATASET IN USE)
           SET LM = &STR(+
                    '&PDSN' IN USE, CANNOT DETERMINE IF MEMBER &MEMBER EXISTS.)
          END
          WHEN (PROTECTE) DO
           SET XCC = 913
           SET SM = &STR(ACCESS DENIED)
           SET LM = &STR(YOU MAY NOT ACCESS THIS PROTECTED DATASET '&PDSN'.)
          END
          WHEN (INVALID ) DO
           SET XCC = 12
           SET SM = &STR(INVALID DATASET NAME)
           SET LM = &STR(INVALID SYNTAX IN DATA SET NAME '&DSNAME'.)
          END
          OTHERWISE DO
           SET XCC = 16
           SET SM = &STR(UNABLE TO ACCESS DATASET)
           SET LM = &STR(ERROR ACCESSING '&PDSN': &STATUS)
          END
         END
        END
        CONTROL MSG
        IF &SMSG ¬= THEN DO
         SET &&SMSG = &NRSTR(&SM)   /* SET REQUESTED VARIABLE TO SHORT MESSAGE
         ISPEXEC VPUT &SMSG
        END
        IF &LMSG ¬= THEN DO
         SET &&LMSG = &NRSTR(&LM)   /* SET REQUESTED VARIABLE TO LONG MESSAGE
         ISPEXEC VPUT &LMSG
        END
        IF &SETMSG = SETMSG THEN DO
         SET XZCKSM = &STR(&SM)
         SET XZCKLM = &STR(&LM)
         ISPEXEC SETMSG MSG(XZCK000)
        END
        ELSE DO
         SET CC = 0
         CONTROL NOMSG
         ISPEXEC CONTROL DISPLAY LINE
         SET ISPCC = &CC
         CONTROL MSG
         IF &NRSTR(&LM) NE THEN +
          WRITE XZCHECK: &NRSTR(&LM)
         IF &ISPCC = 0 THEN ISPEXEC CONTROL DISPLAY REFRESH
        END
        IF &SYSNEST = NO THEN WRITE Exiting with code &XCC.
        EXIT C(&XCC) Q
