PROC 3 WHAT DSNAME ATTRLIST SIZE(1)

SELECT &ATTRLIST
 WHEN (LISTF120) DO
  SET ARECFM   = F B A
  SET ALRECL   = 121
  SET ABLKSIZE = 6171
 END
 WHEN (TEXT) DO
  SET ARECFM   = V B
  SET ALRECL   = 255
  SET ABLKSIZE = 6233
 END
 WHEN (OBJ) DO
  SET ARECFM   = F B
  SET ALRECL   = 80
  SET ABLKSIZE = 2960
 END
 WHEN (ASMLIST) DO
  SET ARECFM   = F B A
  SET ALRECL   = 121
  SET ABLKSIZE = 6171
 END
 WHEN (LINKLIST) DO
  SET ARECFM   = F B A
  SET ALRECL   = 121
  SET ABLKSIZE = 4235
 END
 OTHERWISE DO
  SET ARECFM   = F B
  SET ALRECL   = 80
  SET ABLKSIZE = 6160
 END
END

SET ASPACE1  = 16 * &SIZE
SET ASPACE2  = 10 * &SIZE
SET ASPUNITS = BLOCK(&ABLKSIZE)

/* first see if it already exists

SET XMSTAT = &SYSDSN(&NRSTR(&DSNAME))

SELECT &NRSTR(&XMSTAT)
 WHEN (OK) DO
  EXIT CODE(0)
 END
 WHEN (DATASET NOT FOUND) DO
  /* go ahead */
 END
 WHEN (MEMBER NOT FOUND) DO
  EXIT CODE(0)
 END
 OTHERWISE DO
  WRITE &NRSTR(Error: Cannot use data set &DSNAME: &XMSTAT)
  EXIT CODE(16)
 END
END

SET ALLOCPARMS = &NRSTR(+
      RECFM(&ARECFM) LRECL(&ALRECL) BLKSIZE(&ABLKSIZE) +
      SPACE(&ASPACE1 &ASPACE2) &ASPUNITS +
      )
IF &WHAT = PDS THEN DO
 SET ALLOCPARMS = &NRSTR(&ALLOCPARMS) DIR(32)
END

ALLOC DA(&NRSTR(&DSNAME)) &NRSTR(&ALLOCPARMS)
IF &LASTCC NE 0 THEN DO
 WRITE Error: Could not allocate new data set &DSNAME..
 EXIT CODE(12)
END

EXIT CODE(0)

