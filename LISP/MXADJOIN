
;***********************************************************************
;*                                                                     *
;* ZIL (c) Copyright 1989 The Charles Stark Draper Laboratory Inc.     *
;*         All rights reserved.                                        *
;*                                                                     *
;***********************************************************************

;
; The expander for the ADJOIN macro.
;
;
;***********************************************************************
;*                                                                     *
;* Change activity:                                                    *
;*                                                                     *
;* 06/16/88 - Always generates MEMBER with :TEST keyword.              *
;*            No longer will it generate function of the ASSOC family, *
;*            which is probably more correct since such functions do   *
;*            something different if NIL is a member of the list.      *
;*                                                                     *
;***********************************************************************
;

(eval-when (eval)
 (defmacro adjoin (a b &key ((:test c)) ((:key d)))
           (mxadjoin a b c d))
)

(defun mxadjoin (item list test key
                 &aux bindings itemvar listvar kwds itemtest)

 (setq listvar (gensym))
 (push `(,listvar ,list) bindings)

 (cond
  ((or (atom item) (eq (car item) 'quote))
   (setq itemvar item))
  (t
   (setq itemvar (gensym))
   (push `(,itemvar ,item) bindings)))

 (setq itemtest
       (cond
        ((null key)
         itemvar)
        ((and (consp key)
              (memq (car key) '(quote function)))
         `(,(cadr key) ,itemvar))
        (t
         `(funcall ,key ,itemvar))))

 (when key  (setq kwds `(:key  ,key  . ,kwds)))
 (when test (setq kwds `(:test ,test . ,kwds)))

 `(let ,bindings
       (cond
        ((member ,itemtest ,listvar . ,kwds)
         ,listvar)
        (t
         (cons ,itemvar ,listvar)))))

